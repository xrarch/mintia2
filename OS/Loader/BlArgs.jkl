//
// Argument management for the Loader.
//

#INCLUDE "Bl.hjk"

FN BlGetExistenceArg (
    IN str : ^UBYTE,
) : UWORD

    // Check if an argument matching the provided string was passed.

    argcount := BlInfoBlock.ArgCount
    argtable := BlInfoBlock.ArgTable

    i := 0

    WHILE i < argcount DO
        IF RtlCompareString ( str, argtable[i] ) == 0 THEN
            RETURN TRUE
        END

        i += 1
    END

    RETURN FALSE
END

FN BlGetValueArg (
    IN key : ^UBYTE,
) : ^UBYTE

    // Return a pointer to the value part of a key=value argument.
    // This is a pointer into the argument string itself and shouldn't
    // be manipulated. If the key does not exist, NULLPTR is returned.

    argcount := BlInfoBlock.ArgCount
    argtable := BlInfoBlock.ArgTable

    i := 0

    WHILE i < argcount DO
        str := argtable[i]
        j := 0
        name : UBYTE[16]

        WHILE str^ DO
            IF str^ == '=' THEN
                name[j] = 0

                IF RtlCompareString ( key, &name[0] ) == 0 THEN
                    RETURN str + 1
                END

                BREAK
            END

            IF j == 15 THEN
                BREAK
            END

            name[j] = str^

            j += 1
            str += 1
        END

        i += 1
    END

    RETURN NULLPTR
END