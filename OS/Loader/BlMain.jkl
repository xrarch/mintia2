//
// Main routine for the MINTIA Loader.
//

#INCLUDE "Bl.hjk"

#DEFINE PLATFORM_FULL_NAME_STR ( STRCAT ( STRCAT "\"" PLATFORM_FULL_NAME ) "\"" )

PUBLIC BlInfoBlock : BlInfoRecord

PUBLIC BlDoRelinkAll : UWORD

EXTERN FN BlInitializeMemory ()
EXTERN FN BlInitializeIo ()
EXTERN FN BlLoadResources ()
EXTERN FN BlLoadDrivers ()

FN BlMain () : UWORD

    RtlPrint (
        "\nMINTIA 2 START! %s. Base Level %d.\n", // fmt
        PLATFORM_FULL_NAME_STR,
        BLD_BASE_LEVEL,
    )

    BlDoRelinkAll = BlGetExistenceArg ( "-relink" )

    // Initialize memory management.

    BlInitializeMemory ()

    // Initialize the simple I/O layer.

    BlInitializeIo ()

    // Load the boot resources.

    BlLoadResources ()

    // Initialize the DLL list.

    RtlInitializeList ( &BlInfoBlock.DllListHead )

    // Load the executive.

    RtlPrint ( "Loading the system...\n" )

    exec := BlLoadDll ( "mpx.sys" )

    IF NOT exec THEN
        BlCrash ( "Failed to load the Executive.\n" )
    END

    kemain : ^VOID

    IF NOT BlLookupSymbol (
        exec, // dll
        "KeMain", // name
        OUT kemain, // value
    ) THEN
        BlCrash ( "KeMain() not found in the Executive.\n" )
    END

    // Load the drivers.

    BlLoadDrivers ()

    // Start the system.

    RtlPrint ( "Starting the system...\n" )

    BxStartSystem (
        kemain, // entrypoint
        BlInfoBlock.PrbBasePhysical, // prbphysical
    )

    BlCrash ( "BxStartSystem returned\n" )
END

FN BlCrash (
    IN fmt : ^UBYTE,
    ... argv argc
)

    RtlPrint ( "*** Cannot continue: " )

    RtlPrintByVarTable (
        NULLPTR, // handle
        fmt, // fmt
        argv, // argv
        argc, // argc
    )

    RtlPrint ( "\n" )

    BlExit ( -1 )
END

EXTERN FN BxExit (
    IN status : UWORD,
)

FN BlExit (
    IN status : UWORD,
)

    // Call the platform exit code.

    BxExit ( status )
END