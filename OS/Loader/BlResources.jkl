//
// Boot resource management for the MINTIA Loader.
//

#INCLUDE "Bl.hjk"

BlResourceDirectory : ^BlFile

BlConfigBumpPointer : ^UBYTE
BlConfigBumpRemaining : ULONG = 0

#DEFINE BL_CONFIG_BUMP_EXTENSION [RTL_PAGE_SIZE]

FN BlAllocateConfigFile (
    IN bytes : UWORD,
) : ^UBYTE

    // Allocate space to load a configuration file into.
    // We load a lot of configuration files (one for each driver present in the
    // Drivers directory at boot time) so we want to squish them together to
    // save memory.

    desc : ^BlMemoryDescriptor

    IF bytes >= BL_CONFIG_BUMP_EXTENSION THEN
        RETURN BlAllocateMappedRange (
            BlInfoBlock.BootNode, // node
            (bytes + RTL_PAGE_SIZE - 1) >> RTL_PAGE_SHIFT, // pages
            BL_KERNEL_STRUCTURES, // preferredva
            FALSE, // fixed
            BL_MEMORY_USED, // type
            OUT desc, // desc
        )
    END

    IF BlConfigBumpRemaining < bytes THEN
        pages := BL_CONFIG_BUMP_EXTENSION + RTL_PAGE_SIZE - 1
        pages >>= RTL_PAGE_SHIFT

        BlConfigBumpPointer = BlAllocateMappedRange (
            BlInfoBlock.BootNode, // node
            pages, // pages
            BL_KERNEL_STRUCTURES, // preferredva
            FALSE, // fixed
            BL_MEMORY_USED, // type
            OUT desc, // desc
        )

        IF NOT BlConfigBumpPointer THEN
            BlConfigBumpRemaining = 0

            RETURN NULLPTR
        END

        BlConfigBumpRemaining = pages << RTL_PAGE_SHIFT
    END

    ptr := BlConfigBumpPointer

    BlConfigBumpPointer += bytes
    BlConfigBumpRemaining -= bytes

    RETURN ptr
END

FN BlLoadResource (
    IN device : ^BlFile,
    IN name : ^UBYTE,
    IN path : ^UBYTE,
    IN ioconfig : UWORD,
) : ^BlResource

    // Load a single resource. Returns NULLPTR on error.

    file := BlOpenFile (
        device, // device
        path, // path
        BL_FILETYPE_FILE, // type
    )

    IF NOT file THEN
        RETURN NULLPTR
    END

    rsrc := CAST BlAllocateMemory (
        SIZEOF BlResource, // bytes
    ) TO ^BlResource

    rsrc^.Jettison = TRUE
    rsrc^.IoConfig = ioconfig

    RtlCopyString (
        &rsrc^.Name[0], // dest
        name, // src
        BL_RESOURCE_NAME_MAX, // bufsize
    )

    rsrc^.Size = file^.Size

    IF ioconfig THEN
        rsrc^.Data = BlAllocateConfigFile ( file^.Size )

    ELSE
        filepages := file^.Size + 1
        filepages += RTL_PAGE_SIZE - 1
        filepages >>= RTL_PAGE_SHIFT

        desc : ^BlMemoryDescriptor

        rsrc^.Data = CAST BlAllocateMappedRange (
            BlInfoBlock.BootNode, // node
            filepages, // pages
            BL_KERNEL_STRUCTURES, // preferredva
            FALSE, // fixed
            BL_MEMORY_USED, // type
            OUT desc, // desc
        ) TO ^UBYTE
    END

    IF NOT rsrc^.Data THEN
        RETURN NULLPTR
    END

    // Read the file contents.

    BlReadFile (
        file, // file
        0, // offset
        rsrc^.Data, // buf
        file^.Size, // length
    )

    // Insert in resources list.

    RtlInsertAtTailList (
        &BlInfoBlock.ResourceListHead, // head
        &rsrc^.Entry, // entry
    )

    BlInfoBlock.ResourceCount += 1

    RETURN rsrc
END

#DEFINE BL_RESOURCE_PATH_MAX 64

FN (BlCallbackPerLineF) BlLoadResourceCallback (
    IN buf : ^UBYTE,
    IN context : ^VOID,
    IN linenum : UWORD,
)

    WHILE buf^ == ' ' OR buf^ == '\t' DO
        buf += 1
    END

    IF buf^ == '#' THEN
        // Comment line. Ignore.

        LEAVE
    END

    // Parse a resource line of the format:
    // name path

    name : UBYTE[BL_RESOURCE_NAME_MAX]
    path : UBYTE[BL_RESOURCE_PATH_MAX]

    next := RtlTokenize (
        buf, // buffer
        &name[0], // tokenbuffer
        BL_RESOURCE_NAME_MAX, // bufsize
        ' ', // delimiter
    )

    IF NOT next THEN
        // Empty line.

        LEAVE
    END

    next = RtlTokenize (
        next, // buffer
        &path[0], // tokenbuffer
        BL_RESOURCE_PATH_MAX, // bufsize
        ' ', // delimiter
    )

    IF NOT next THEN
        RtlPrint ( "WARNING: Resources.txt:%d: Badly formatted\n", linenum )

        LEAVE
    END

    rsrc := BlLoadResource (
        BlResourceDirectory, // device
        &name[0], // name
        &path[0], // path
        FALSE, // ioconfig
    )

    IF NOT rsrc THEN
        RtlPrint (
            "WARNING: Couldn't load %s (%s)\n", // fmt
            &name[0],
            &path[0],
        )
    END
END

FN BlLoadResources ()

    // Initialize the resource list.

    RtlInitializeList ( &BlInfoBlock.ResourceListHead )

    // Open the Resources directory.

    BlResourceDirectory = BlOpenFile (
        BlSystemDirectory, // file
        "Resources", // path
        BL_FILETYPE_DIRECTORY, // type
    )

    IF NOT BlSystemDirectory THEN
        RtlPrint ( "WARNING: Couldn't open %s/Resources/\n", BlSystemPath )

        LEAVE
    END

    // Load all of the boot resources.

    IF NOT BlCallbackPerLineFile (
        BlSystemDirectory, // device
        "Resources.txt", // path
        &BlLoadResourceCallback, // callback
        NULLPTR, // context
    ) THEN

        RtlPrint ( "WARNING: Couldn't open %s/Resources.txt\n", BlSystemPath )
    END
END