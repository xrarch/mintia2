//
// Implements stack tracing for the XR/17032 architecture.
//

#INCLUDE "../Dbg.hjk"

#ASM [

DbgGetLinks:
    mov  a2, sp
    mov  a1, lr
    ret

]

EXTERN FN DbgGetLinks (
    OUT sp : ^VOID,
    OUT pc : ^VOID,
)

EXTERN KiExceptionHandler : UBYTE
EXTERN DbgExcEnd : UBYTE

#DEFINE DBG_TRACE_NAME_MAX 40

DbgTraceNameBuffer : UBYTE[DBG_TRACE_NAME_MAX]

FN DbgTrace (
    IN sp : ^VOID,
    IN pc : ^VOID,
    IN lr : ^VOID,
)

    infostr := "InstrPtr StackPtr Name"

    RtlPrint ( "%s\n", infostr )

    links := 0

    leaf := TRUE

    WHILE pc DO
        IF links >= 128 THEN
            RtlPrint ( "!! Maximum trace !!\n" )

            LEAVE
        END

        dll : ^BlDll
        symvalue : ^VOID
        symname : ^UBYTE
        dllname : ^UBYTE

        sym := DbgGetSymbolAndDll (
            pc, // address
            OUT dll, // dll
            OUT symvalue, // symvalue
            OUT symname, // symname
        )

        IF NOT sym THEN
            symname = "UNKNOWN"
            symvalue = 0
            dllname = "UNK"
        
        ELSE
            dllname = &dll^.Name[0]
        END

        RtlFormat (
            &DbgTraceNameBuffer[0], // dest
            DBG_TRACE_NAME_MAX, // bufsize
            "%s!%s+0x%x", // fmt
            dllname,
            symname,
            pc - symvalue,
        )

        RtlPrint ( "%p %p %s\n", pc, sp, &DbgTraceNameBuffer[0] )

        IF NOT sp THEN
            RtlPrint ( "!! SP=0 !!\n" )

            LEAVE
        END

        IF sp & 3 THEN
            RtlPrint ( "!! SP & 3 !!\n" )

            LEAVE
        END

        IF pc & 3 THEN
            RtlPrint ( "!! PC & 3 !!\n" )

            LEAVE
        END

        IF pc < 0x80000000 THEN
            RtlPrint ( "!! PC < 0x80000000 !!\n" )

            LEAVE
        END

        IF pc >= 0xA0000000 THEN
            RtlPrint ( "!! PC >= 0xA0000000 !!\n" )

            LEAVE
        END

        // Get next link.

        IF pc >= &KiExceptionHandler AND pc < &DbgExcEnd THEN
            // Step out of the exception handler. The "stack frame" is really
            // an OsContext record.

            ctx := CAST sp TO ^OsContext

            IF NOT MmIsVirtualValid ( ctx ) THEN
                RtlPrint ( "#\n" )

                LEAVE
            END

            IF NOT MmIsVirtualValid ( ctx + SIZEOF OsContext ) THEN
                RtlPrint ( "#\n" )

                LEAVE
            END

            IF ctx^.Rs & 1 THEN
                // Was usermode. Can't step there.

                LEAVE
            END

            sp = CAST ctx^.Sp TO ^VOID
            lr = CAST ctx^.Lr TO ^VOID
            pc = CAST ctx^.Epc TO ^VOID

            leaf = TRUE

        ELSE
            offset := 0
            ulong : ^ULONG

            WHILE pc >= symvalue DO
                IF NOT MmIsVirtualValid ( pc ) THEN
                    RtlPrint ( "#\n" )

                    LEAVE
                END

                ulong = CAST pc TO ^ULONG

                IF ulong[0] == 0 THEN
                    // Doorstop.

                    BREAK
                END

                IF ulong[0] & 0xFFFF == 0xF7B4 THEN
                    // Found the "subi sp, sp, X" instruction that created the
                    // stack frame. Extract the X to find out how big the stack
                    // frame is, so we can offset to the next one.

                    offset = ulong[0] >> 16

                    BREAK

                ELSEIF ulong[0] & 0xFFFF == 0xFFAA THEN
                    // Found a "mov long [sp + X], lr" instruction. that means
                    // this is NOT a leaf function.

                    leaf = FALSE
                END

                pc -= 4
            END

            IF leaf THEN
                pc = lr

            ELSE
                IF offset < 4 OR offset & 3 THEN
                    BREAK
                END

                ulong = CAST sp + offset - 4 TO ^ULONG

                IF NOT MmIsVirtualValid ( ulong ) THEN
                    RtlPrint ( "#\n" )

                    LEAVE
                END

                pc = CAST ulong^ TO ^VOID
            END

            leaf = FALSE

            sp += offset
        END

        links += 1
    END
END

FN (KeCrashCallbackF) DbgStackTrace (
    IN columns : UWORD,
)

    sp : ^VOID
    pc : ^VOID

    DbgGetLinks (
        OUT sp, // sp
        OUT pc, // pc
    )

    DbgTrace (
        sp, // sp
        pc, // pc
        NULLPTR, // lr
    )
END

FN DbgDumpRegisters (
    IN context : ^OsContext,
)

    RtlPrint (
        " t0=%08x  t1=%08x  t2=%08x  t3=%08x  t4=%08x  t5=%08x\n", // fmt
        context^.T0, context^.T1, context^.T2, context^.T3, context^.T4,
        context^.T5,
    )

    RtlPrint (
        " a0=%08x  a1=%08x  a2=%08x  a3=%08x\n", // fmt
        context^.A0, context^.A1, context^.A2, context^.A3,
    )

    RtlPrint (
        " s0=%08x  s1=%08x  s2=%08x  s3=%08x  s4=%08x  s5=%08x\n", // fmt
        context^.S0, context^.S1, context^.S2, context^.S3, context^.S4,
        context^.S5,
    )

    RtlPrint (
        " s6=%08x  s7=%08x  s8=%08x  s9=%08x s10=%08x s11=%08x\n", // fmt
        context^.S6, context^.S7, context^.S8, context^.S9, context^.S10,
        context^.S11,
    )

    RtlPrint (
        "s12=%08x s13=%08x s14=%08x s15=%08x s16=%08x s17=%08x\n", // fmt
        context^.S12, context^.S13, context^.S14, context^.S15, context^.S16,
        context^.S17,
    )

    RtlPrint (
        " tp=%08x  sp=%08x  lr=%08x epc=%08x  rs=%08x\n", // fmt
        context^.Tp, context^.Sp, context^.Lr, context^.Epc, context^.Rs,
    )
END

FN DbgTraceContext (
    IN context : ^OsContext,
)

    DbgTrace (
        CAST context^.Sp TO ^VOID, // sp
        CAST context^.Epc TO ^VOID, // pc
        CAST context^.Lr TO ^VOID, // lr
    )
END

FN DbgTraceThreadContext (
    IN context : ^OsContext,
)

    DbgTrace (
        context + SIZEOF OsContext, // sp
        CAST context^.Lr TO ^VOID, // pc
        CAST context^.Lr TO ^VOID, // lr
    )
END