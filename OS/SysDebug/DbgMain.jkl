//
// Initialization for the MINTIA system debugger.
//

#INCLUDE "Dbg.hjk"

DbgStackTraceCallback : KeCrashCallback

PUBLIC DbgCauseCrash := FALSE

FN (KeDebuggerEntryF) DbgEntrypoint (
    IN context : ^OsContext,
)

    old := HalSetDebugConsole ( TRUE )

#IF BLD_MP
    KiFreezeOtherProcessors ()
#END

    // All other processors on the system have been frozen.

    NOTHING KI_CURRENT_PRB_LOCAL^.FrozenContext = context

    DbgCurrentContext = context

    DbgPrompt ()

#IF BLD_MP
    KiUnfreezeOtherProcessors ()
#END

    HalSetDebugConsole ( old )

    IF DbgCauseCrash THEN
        KeCrash ( "Debug\n" )
    END
END

EXPORT FN DbgMain ()
    
    // First register the crash callbacks.

    KeRegisterCrashCallback (
        &DbgStackTraceCallback, // callback
        &DbgStackTrace, // callbackfunc
        TRUE, // doesprint
        FALSE, // horizontal
    )

    // Now register the debugger prompt entry.

    KeDebuggerEntry = &DbgEntrypoint

    // Announce ourselves.

    RtlPrint ( "DbgMain(): Debugger installed\n" )
END