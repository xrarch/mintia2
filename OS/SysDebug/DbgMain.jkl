//
// Initialization for the MINTIA system debugger.
//

#INCLUDE "Dbg.hjk"

DbgStackTraceCallback : KeCrashCallback

PUBLIC DbgCauseCrash := FALSE

DbgEntryCount : ULONG

FN (KeuDebuggerEntryF) DbgEntrypoint (
    IN context : ^OsContext,
)

    // Assumes interrupts are disabled on entry.

    cnt := KeIncrementUlong (
        &DbgEntryCount, // ptr
        1, // inc
    )

    IF cnt != 0 THEN
        // Nested entry. Ignore.

        KeIncrementUlong (
            &DbgEntryCount, // ptr
            0xFFFFFFFF, // inc
        )

        LEAVE
    END

    old := HaluSetDebugConsole ( TRUE )

#IF BLD_MP
    KepFreezeOtherProcessors ()
#END

    DbgCurrentPrb = KEP_CURRENT_PRB

    IF DbgCurrentPrb^.FrozenContext THEN
        DbgCurrentContext = DbgCurrentPrb^.FrozenContext
    ELSE
        DbgCurrentContext = context
    END

    DbgPrompt ()

#IF BLD_MP
    KepUnfreezeOtherProcessors ()
#END

    HaluSetDebugConsole ( old )

    KeIncrementUlong (
        &DbgEntryCount, // ptr
        0xFFFFFFFF, // inc
    )

    IF DbgCauseCrash THEN
        KeCrash ( "Debug\n" )
    END
END

EXPORT FN DbgMain ()
    
    // First register the crash callbacks.

    KeRegisterCrashCallback (
        &DbgStackTraceCallback, // callback
        &DbgStackTrace, // callbackfunc
        TRUE, // doesprint
        FALSE, // horizontal
    )

    // Now register the debugger prompt entry.

    KeuDebuggerEntry = &DbgEntrypoint

    // Announce ourselves.

    RtlPrint ( "DbgMain: Debugger installed\n" )
END