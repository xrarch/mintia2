//
// Graphical helper routines for the fireworks test.
//

#INCLUDE "Fwt.hjk"
#INCLUDE "../../Loader/Headers/Loader.hjk"

FwtPixBuffer : ^UBYTE

FwtPixPitch : UWORD

FwtPixWidth : UWORD
FwtPixHeight : UWORD

FwtViewX : UWORD
FwtViewY : UWORD

PUBLIC FwtViewWidth : UWORD
PUBLIC FwtViewHeight : UWORD

#DEFINE FWT_VIEW_WIDTH 320
#DEFINE FWT_VIEW_HEIGHT 240

#IF ( STRCMP PLATFORM "fox32" )

#MACRO FwtPixelPointer ( x, y ) [
    NOTHING (CAST FwtPixBuffer + (y) * FwtPixPitch + (x) * 4 TO ^ULONG)
]

#ELSEIF ( STRCMP PLATFORM "XRstation" )

#MACRO FwtPixelPointer ( x, y ) [
    NOTHING (CAST FwtPixBuffer + (y) * FwtPixPitch + (x) TO ^UBYTE)
]

#END

FN FwtFillScreen (
    IN color : UWORD,
)
    
    // Fill the screen with the given color.

    y := 0

    WHILE y < FwtViewHeight DO
        x := 0

        WHILE x < FwtViewWidth DO
            FwtSetPixel (
                color, // color
                x, // x
                y, // y
            )

            x += 1
        END

        y += 1
    END
END

FN FwtSetPixel (
    IN color : UWORD,
    IN x : WORD,
    IN y : WORD,
)

    // Set the given pixel on the screen.

    IF x < 0 OR y < 0 OR x >= FwtViewWidth OR y >= FwtViewHeight THEN
        LEAVE
    END

    x += FwtViewX
    y += FwtViewY

    FwtPixelPointer ( x, y ) [0] = color
END

FN FwtInitGraphics ()

    // Initialize graphics for the fireworks test.

    FwtPixBuffer = CAST KeLoaderBlock.BootFbBase TO ^UBYTE
    FwtPixWidth = KeLoaderBlock.BootFbWidth
    FwtPixHeight = KeLoaderBlock.BootFbHeight

    // Set up the view we do the fireworks through.

    FwtViewWidth = FWT_VIEW_WIDTH

    IF FwtPixWidth < FWT_VIEW_WIDTH THEN
        FwtViewWidth = FwtPixWidth
    END

    FwtViewHeight = FWT_VIEW_HEIGHT

    IF FwtPixHeight < FWT_VIEW_HEIGHT THEN
        FwtViewHeight = FwtPixHeight
    END

    FwtViewX = (FwtPixWidth - FwtViewWidth) / 2
    FwtViewY = (FwtPixHeight - FwtViewHeight) / 2

#IF ( STRCMP PLATFORM "fox32" )
    FwtPixPitch = FwtPixWidth * 4

#ELSEIF ( STRCMP PLATFORM "XRstation" )
    FwtPixPitch = FwtPixWidth
#END

END