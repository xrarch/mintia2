#INCLUDE "Fwt.hjk"
#INCLUDE "../../Loader/Headers/Loader.hjk"

PixBuff : ^UBYTE
PixPitch : UWORD
PixWidth : UWORD
PixHeight : UWORD
ViewX : UWORD
ViewY : UWORD
PUBLIC ViewWidth : UWORD
PUBLIC ViewHeight : UWORD

#DEFINE VIEW_WIDTH 320
#DEFINE VIEW_HEIGHT 240

#IF ( STRCMP PLATFORM "fox32" )

#MACRO PixelPointer ( x, y ) [NOTHING (CAST PixBuff + (y) * PixPitch + (x) * 4 TO ^ULONG)]

#ELSEIF ( STRCMP PLATFORM "XRstation" )

#MACRO PixelPointer ( x, y ) [NOTHING (CAST PixBuff + (y) * PixPitch + (x) TO ^UBYTE)]

#END

FN FillScreen(IN Color : UWORD)
    y := 0

    WHILE y < ViewHeight DO
        x := 0

        WHILE x < ViewWidth DO
            SetPixel(Color, x, y)

            x += 1
        END

        y += 1
    END
END

FN SetPixel(IN Color : UWORD, IN x : WORD, IN y : WORD)
    IF x < 0 OR y < 0 OR x >= ViewWidth OR y >= ViewHeight THEN
        LEAVE
    END

    x += ViewX
    y += ViewY

    PixelPointer(x, y)[0] = Color
END

FN InitGraphics()

    PixBuff   = CAST KeLoaderBlock.BootFbBase TO ^UBYTE
    PixWidth  = KeLoaderBlock.BootFbWidth
    PixHeight = KeLoaderBlock.BootFbHeight

    // Set up the view we do the fireworks through.

    ViewWidth = VIEW_WIDTH

    IF PixWidth < VIEW_WIDTH THEN
        ViewWidth = PixWidth
    END

    ViewHeight = VIEW_HEIGHT

    IF PixHeight < VIEW_HEIGHT THEN
        ViewHeight = PixHeight
    END

    ViewX = (PixWidth - ViewWidth) / 2
    ViewY = (PixHeight - ViewHeight) / 2
    
    // KeLoaderBlock doesn't provide a pitch, so it seems we'll have to assume the
    // pitch is equal to the width.

#IF ( STRCMP PLATFORM "fox32" )
    PixPitch  = PixWidth * 4

#ELSEIF ( STRCMP PLATFORM "XRstation" )
    PixPitch  = PixWidth
#END

END
