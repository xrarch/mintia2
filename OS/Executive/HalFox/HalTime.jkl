//
// Implements time management for the fox32 HAL.
//

#INCLUDE "Halp.hjk"

#DEFINE HALU_RTC_PORT 0x80000700
#DEFINE HALU_RTC_YEAR 0
#DEFINE HALU_RTC_MONTH 1
#DEFINE HALU_RTC_DAY 2
#DEFINE HALU_RTC_HOUR 3
#DEFINE HALU_RTC_MIN 4
#DEFINE HALU_RTC_SEC 5
#DEFINE HALU_RTC_UPTME 6
#DEFINE HALU_RTC_ISDST 7

HalpBootTimeSeconds : ULONG
HalpBootUptimeMs : ULONG

FN HalpMakeUnixTime ()

    // Make the initial unix timestamp that we base our time calculations from.

    // The fox32 RTC presents the date in a split up format that we need to
    // convert to milliseconds since 1970.

    year := HaluFox32In ( HALU_RTC_PORT | HALU_RTC_YEAR )

    month := HaluFox32In ( HALU_RTC_PORT | HALU_RTC_MONTH )

    IF month <= 2 THEN
        month += 12
        year -= 1
    END

    day := HaluFox32In ( HALU_RTC_PORT | HALU_RTC_DAY )

    hour := HaluFox32In ( HALU_RTC_PORT | HALU_RTC_HOUR )

    min := HaluFox32In ( HALU_RTC_PORT | HALU_RTC_MIN )

    secpart := HaluFox32In ( HALU_RTC_PORT | HALU_RTC_SEC )

    isdst := HaluFox32In ( HALU_RTC_PORT | HALU_RTC_ISDST )

    // Years to days

    day += (year * 365) + (year / 4) - (year / 100) + (year / 400)

    // Months to days

    day += (month * 30) + (((month + 1) * 3) / 5)

    // Unix epoch

    day -= 719561

    secpart += (day * 86400) + (hour * 3600) + (min * 60)

    IF isdst THEN
        // Subtract an hour worth of seconds to normalize against daylight
        // savings time.

        secpart -= 3600
    END

    HalpBootTimeSeconds = secpart

    HalpBootUptimeMs = HaluFox32In ( HALU_RTC_PORT | HALU_RTC_UPTME )
END

FN HaluQueryTime (
    IN time : ^RtlUquad,
)

    // Query the time in milliseconds. This is called from the timer interrupt.

    uptime := HaluFox32In ( HALU_RTC_PORT | HALU_RTC_UPTME )

    uptime -= HalpBootUptimeMs

    sec := HalpBootTimeSeconds + (uptime / 1000)
    ms := uptime % 1000

    // Perform a 32x16->48 multiply of the seconds part by 1000.

    part1 := (sec & 0xFFFF) * 1000
    part2 := (sec >> 16) * 1000

    // Upper 16 bits of part2 become low 16 bits of high part of 64 bit result.

    time^.High = part2 >> 16

    // Low 16 bits of part2 become high 16 bits of low part.

    time^.Low = part2 << 16

    // Add part1 and milliseconds.

    part1 += ms

    RtlAddUlongToUquad (
        time, // quad
        part1, // val
    )
END