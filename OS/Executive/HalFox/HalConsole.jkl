//
// Implementation of a simple boot console.
//

#INCLUDE "Halp.hjk"

HalpUseFramebuffer := FALSE
HalpDebugConsole := FALSE
HalpFramebufferBytes := 0
PUBLIC HalpFont : ^HaluConsoleFont
HalpCursorX := 0
HalpCursorY := 0
HalpWidth := 0
HalpHeight := 0

#DEFINE HALU_CONSOLE_FG 0xFFFFFFFF
#DEFINE HALU_CONSOLE_BG 0xFF674764

FN HalpDrawCharacter (
    IN c : UWORD,
    IN x : UWORD,
    IN y : UWORD,
    IN base : ^ULONG,
    IN width : UWORD,
    IN fg : UWORD,
    IN bg : UWORD,
)

    font := HalpFont
    w := font^.Width
    h := font^.Height

    j : UWORD
    byte : UWORD
    left : UWORD

    dest : ^ULONG = base + (x + (y * width)) * 4

    mod := (width - w) * 4

    bmp := CAST font + SIZEOF HaluConsoleFont TO ^UBYTE
    bmp += ((w + 7) >> 3) * h * c

    IF font^.Flags & HCF_FLAG_REVERSE == 0 THEN
        WHILE h DO
            left = w >> 3

            WHILE left DO
                byte = bmp^

                IF byte & 128 THEN
                    dest[0] = fg
                ELSE
                    dest[0] = bg
                END

                IF byte & 64 THEN
                    dest[1] = fg
                ELSE
                    dest[1] = bg
                END

                IF byte & 32 THEN
                    dest[2] = fg
                ELSE
                    dest[2] = bg
                END

                IF byte & 16 THEN
                    dest[3] = fg
                ELSE
                    dest[3] = bg
                END

                IF byte & 8 THEN
                    dest[4] = fg
                ELSE
                    dest[4] = bg
                END

                IF byte & 4 THEN
                    dest[5] = fg
                ELSE
                    dest[5] = bg
                END

                IF byte & 2 THEN
                    dest[6] = fg
                ELSE
                    dest[6] = bg
                END

                IF byte & 1 THEN
                    dest[7] = fg
                ELSE
                    dest[7] = bg
                END

                dest += 8 * 4
                left -= 1
                bmp += 1
            END

            left = w & 7

            IF left THEN
                byte = bmp^
                bmp += 1
                
                IF w >> 3 THEN
                    j = 128

                ELSE
                    j = 1 << left
                END

                WHILE left DO
                    IF byte & j THEN
                        dest^ = fg
                    ELSE
                        dest^ = bg
                    END

                    dest += 4
                    j >>= 1
                    left -= 1
                END
            END

            dest += mod
            h -= 1
        END

    ELSE
        WHILE h DO
            left = w >> 3

            WHILE left DO
                byte = bmp^

                IF byte & 128 THEN
                    dest[7] = fg
                ELSE
                    dest[7] = bg
                END

                IF byte & 64 THEN
                    dest[6] = fg
                ELSE
                    dest[6] = bg
                END

                IF byte & 32 THEN
                    dest[5] = fg
                ELSE
                    dest[5] = bg
                END

                IF byte & 16 THEN
                    dest[4] = fg
                ELSE
                    dest[4] = bg
                END

                IF byte & 8 THEN
                    dest[3] = fg
                ELSE
                    dest[3] = bg
                END

                IF byte & 4 THEN
                    dest[2] = fg
                ELSE
                    dest[2] = bg
                END

                IF byte & 2 THEN
                    dest[1] = fg
                ELSE
                    dest[1] = bg
                END

                IF byte & 1 THEN
                    dest[0] = fg
                ELSE
                    dest[0] = bg
                END

                dest += 8 * 4
                left -= 1
                bmp += 1
            END

            left = w & 7

            IF left THEN
                byte = bmp^
                bmp += 1
                j = 1

                WHILE left DO
                    IF byte & j THEN
                        dest^ = fg
                    ELSE
                        dest^ = bg
                    END

                    dest += 4
                    j <<= 1
                    left -= 1
                END
            END

            dest += mod
            h -= 1
        END
    END
END

FN HalpClearScreen ()

    // Fill the framebuffer with blue pixels.

    RtlFillMemoryWithUlong (
        KeuLoaderBlock.BootFbBase, // ptr
        HalpFramebufferBytes, // sz
        HALU_CONSOLE_BG, // ulong
    )

    HalpCursorX = 0
    HalpCursorY = 0
END

FN HalpScrollScreen ()

    // Scroll the framebuffer up by 1 line. This is REALLY slow but the HAL
    // console isn't a performance concern (if it shows up in runtime, the
    // system is either being debugged, or something broke) so simplicity beats
    // speed here. The primary system console used during runtime is *not* this
    // one.

    font := HalpFont

    RtlCopyBytes (
        KeuLoaderBlock.BootFbBase, // dest
        KeuLoaderBlock.BootFbBase +
        KeuLoaderBlock.BootFbWidth * font^.Height * 4, // src
        KeuLoaderBlock.BootFbWidth *
        (KeuLoaderBlock.BootFbHeight - font^.Height) * 4, // sz
    )

    // Clear the last line.

    RtlFillMemoryWithUlong (
        KeuLoaderBlock.BootFbBase +
        KeuLoaderBlock.BootFbWidth *
        (HalpHeight - 1) * font^.Height * 4, // ptr
        KeuLoaderBlock.BootFbWidth * font^.Height * 4, // sz
        HALU_CONSOLE_BG, // ulong
    )
END

FN HaluPutCharacter (
    IN byte : UBYTE,
)

    oldstate := KepDisableInterrupts ()

    IF NOT HalpDebugConsole AND HalpUseFramebuffer THEN
        // Write to the framebuffer console.

        IF byte == '\n' OR HalpCursorX >= HalpWidth THEN
            HalpCursorX = 0
            HalpCursorY += 1
        END

        IF HalpCursorY >= HalpHeight THEN
            HalpCursorX = 0
            HalpCursorY = HalpHeight - 1

            HalpScrollScreen ()
        END

        IF byte != '\n' THEN
            HalpDrawCharacter (
                byte, // c
                HalpCursorX * HalpFont^.Width, // x
                HalpCursorY * HalpFont^.Height, // y
                KeuLoaderBlock.BootFbBase, // base
                KeuLoaderBlock.BootFbWidth, // width
                HALU_CONSOLE_FG, // fg
                HALU_CONSOLE_BG, // bg
            )

            HalpCursorX += 1
        END

    ELSE
        // Write to debugger window.

        HaluDebugWrite ( byte )
    END

    KepRestoreInterrupts ( oldstate )
END

FN HaluCrashConsole ()

    // We assume the system is in a globally single-threaded uniprocessor state
    // right now (since KeCrash has disabled interrupts and made any other
    // processors quiescent). Don't bother taking locks.

    IF HalpUseFramebuffer AND NOT HalpDebugConsole THEN
        // Set the console cursor to x=0 y=0.

        HalpCursorX = 0
        HalpCursorY = 0

        // Draw a cool shutter effect.

        addr := CAST KeuLoaderBlock.BootFbBase TO ^ULONG

        height := KeuLoaderBlock.BootFbHeight

        WHILE height DO
            width := KeuLoaderBlock.BootFbWidth

            off := height & 1

            WHILE width >= 2 DO
                addr[off] = 0

                width -= 2
                addr += 8
            END

            height -= 1
        END
    END
END

FN HaluQueryConsoleSize (
    OUT width : UWORD,
    OUT height : UWORD,
)

    IF HalpUseFramebuffer THEN
        width = HalpWidth
        height = HalpHeight

    ELSE
        // Assume VT terminal.

        width = 80
        height = 24
    END
END

FN HaluQueryConsolePosition (
    OUT x : UWORD,
    OUT y : UWORD,
)

    IF HalpUseFramebuffer THEN
        x = HalpCursorX
        y = HalpCursorY

    ELSE
        x = 0
        y = 0
    END
END

FN HaluSetConsolePosition (
    IN x : UWORD,
    IN y : UWORD,
)

    IF x >= HalpWidth THEN
        x = HalpWidth - 1
    END

    IF y >= HalpHeight THEN
        y = HalpHeight - 1
    END

    HalpCursorX = x
    HalpCursorY = y
END

FN HaluCenterPrint (
    IN str : ^UBYTE,
)

    len := RtlMeasureString ( str )

    IF str[len - 1] == '\n' THEN
        len -= 1
    END

    pad := (HalpWidth - len) / 2

    WHILE pad DO
        RtlPrint ( " " )

        pad -= 1
    END

    RtlPrint ( "%s", str )
END

#SECTION "INITtext"
FN HalpInitializeConsole ()

    // First see if we got a boot framebuffer.

    IF KeuLoaderBlock.BootFbBase THEN
        // We did! Try to find the HAL console font resource.

        rsrc := KeFindResource (
            "HaluFont", // name
            OUT HalpFont, // data
        )

        IF rsrc AND HalpFont^.Magic == HCF_MAGIC THEN
            KePinResource ( rsrc )

            HalpUseFramebuffer = TRUE

            HalpFramebufferBytes =
                KeuLoaderBlock.BootFbWidth * KeuLoaderBlock.BootFbHeight * 4

            HalpWidth = KeuLoaderBlock.BootFbWidth / HalpFont^.Width
            HalpHeight = KeuLoaderBlock.BootFbHeight / HalpFont^.Height

            // Draw the background.

            HalpClearScreen ()
        END
    END
END

EXPORT FN HaluSetDebugConsole (
    IN new : UWORD,
) : UWORD

    oldstate := KepDisableInterrupts ()

    old := HalpDebugConsole

    IF new != old THEN
        // Enable the debugger overlay.

        HalpDebugConsole = new

        HalpSetDbgOverlay ( new )
    END

    KepRestoreInterrupts ( oldstate )

    RETURN old
END