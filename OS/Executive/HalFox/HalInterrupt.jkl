//
// Implements interrupt support for the fox32 HAL.
//

#INCLUDE "Halp.hjk"

FN HaluRaiseHardwareIpl (
    IN ipl : UWORD,
) : UWORD

    // This isn't part of KepRaiseIpl since that is written in a tightly
    // optimized manner around the most common usage (changing between software
    // IPLs, which need not reprogram the hardware interrupt masks).

    prb := KEP_CURRENT_PRB_LOCAL

    oldipl := prb^.Ipl

    IF ipl > KEP_IPL_DPC THEN
        KepDisableInterrupts ()
    END

    prb^.Ipl = ipl

    RETURN oldipl
END

FN HaluLowerHardwareIpl (
    IN ipl : UWORD,
)

    // This isn't part of KepLowerIpl since that is written in a tightly
    // optimized manner around the most common usage (changing between software
    // IPLs, which need not reprogram the hardware interrupt masks).

    prb := KEP_CURRENT_PRB_LOCAL
    prb^.Ipl = ipl

    IF ipl <= KEP_IPL_DPC THEN
        KepEnableInterrupts ()
    END

    // We don't need to check this in the disabled interrupts section because if
    // we get a stale PendingSoftwareInterrupts value from another processor, we
    // will harmlessly call KepDispatchSoftwareInterrupts which does nothing. And
    // we can't "miss" any since all events that switch us to another processor
    // will cause software interrupt dispatch anyway.

    IF prb^.PendingSoftwareInterrupts >> ipl THEN
        KepDispatchSoftwareInterrupts ( ipl )
    END
END

FN (KeInterruptF) HaluHandleDeviceInterrupt (
    IN interrupt : ^KeuInterrupt,
    IN context : ^OsContext,
)

    // Default handler for non-special interrupts.
    // This is unused on fox32 (we just directly call the routine) but is
    // necessary to link the kernel.
END

FN HaluInterrupt (
    IN context : ^OsContext,
    IN vector : UWORD,
)

    // Called by architecture-specific code in Ke when an interrupt is detected.

    prb := KEP_CURRENT_PRB

    int := prb^.IrqDispatchTable[vector]

    // Capture old IPL and set new in Prb.

    oldipl := prb^.Ipl
    prb^.Ipl = int^.Ipl

    context^.OldIpl = oldipl

    // Call the routine.
    // We leave interrupts disabled for all hardware interrupts on fox32 as
    // there are no priority levels or per-interrupt masking on that platform.

    int^.Routine (
        int, // interrupt
        context, // context
    )

    // Reset the Prb IPL.

    prb^.Ipl = oldipl
END