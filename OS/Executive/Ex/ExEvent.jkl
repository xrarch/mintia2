//
// Implements the executive Event object.
//

#INCLUDE "Exp.hjk"

#INCLUDE "<inc>/Obu.hjk"
#INCLUDE "<inc>/Nsu.hjk"
#INCLUDE "<inc>/Psu.hjk"

#INCLUDE "<ll>/System/OsObject.hjk"
#INCLUDE "<ll>/System/OsNamespace.hjk"

STRUCT ExpEventContext
    Notification : UBYTE,
    SignalState : UBYTE,
END

#SECTION "PAGEtext"
FN (ObuTypeInitializeF) ExpInitializeEventObject (
    IN object : ^VOID,
    IN context : ^VOID,
) : OsStatus

    // We've been called to initialize a new event object, so do that.

    event := CAST object TO ^ExuEvent

    ctx := CAST context TO ^ExpEventContext

    KeInitializeEvent (
        &event^.Event, // event
        "ExuEventObject", // name
        ctx^.Notification, // notification
        ctx^.SignalState, // signalstate
    )
END

PUBLIC ExuEventType : ObuType = {
    [Name] = "Event", // name

    [Initialize] = &ExpInitializeEventObject,

    [WaitOffset] = OFFSETOF ExuEvent.Event,
    [TypeIdentifier] = OS_EVENT_TYPE,
    [Tag] = 'Evnt',

    [TypicalBodySize] = SIZEOF ExuEvent,

    [IsPaged] = FALSE,
}

#SECTION "PAGEtext"
FN ExuCreateEventObject (
    IN params : ^NsOpenParameters,
    IN notification : UWORD,
    IN signalstate : UWORD,
    OUT event : ^ExuEvent,
) : OsStatus

    // Create an event object.

    ctx : ExpEventContext

    ctx.Notification = notification
    ctx.SignalState = signalstate

    obparams : ObuParameters

    NsInitializeObjectParameters (
        &obparams, // obparams
        params, // params
        &ExuEventType, // type
        &ctx, // context
        SIZEOF ExuEvent, // bodysize
        0, // pagedbodysize
        0, // npquotabias
        0, // pgquotabias
    )

    RETURN NsuCreateObject (
        params, // params
        &obparams, // obparams
        OUT event, // object
    )
END

#SECTION "PAGEtext"
FN ExuCreateEvent (
    IN params : ^NsOpenParameters,
    IN notification : UWORD,
    IN signalstate : UWORD,
    OUT handle : UWORD,
) : OsStatus

    // Create an event object handle.

    object : ^ExuEvent

    status := ExuCreateEventObject (
        params, // params
        notification, // notification
        signalstate, // signalstate
        OUT object, // object
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    status = ObuInsertObject (
        PsuCurrentProcess (), // process
        object, // object
        params^.Captured.Access, // access
        OUT handle, // handle
    )

    IF OsError ( status ) THEN
        ObuUnreferenceObject ( object )
    END

    RETURN status
END

#SECTION "PAGEtext"
FN OsCreateEvent (
    IN params : ^OsOpenParameters,
    IN notification : UWORD,
    IN signalstate : UWORD,
    OUT handle : UWORD,
) : OsStatus

    // Create an event object, system service.

    sysparams : NsOpenParameters

    status := NsuCaptureOpenParameters (
        params, // userparams
        &sysparams, // sysparams
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    status = ExuCreateEvent (
        &sysparams, // params
        notification, // notification
        signalstate, // signalstate
        OUT handle, // handle
    )

    NsuFreeOpenParameters ( &sysparams )

    RETURN status
END

#SECTION "PAGEtext"
FN OsResetEvent (
    IN handle : UWORD,
    OUT signaled : UWORD,
) : OsStatus

    event : ^ExuEvent
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &ExuEventType, // type
        OUT event, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_READ != access THEN
        // Not a subset of the required access.

        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    signaled = KeResetEvent ( &event^.Event )

@Exit

    ObuUnreferenceObject ( event )

    RETURN status
END

#SECTION "PAGEtext"
FN OsSignalEvent (
    IN handle : UWORD,
    OUT signaled : UWORD,
) : OsStatus

    event : ^ExuEvent
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &ExuEventType, // type
        OUT event, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_EXEC != access THEN
        // Not a subset of the required access.

        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    signaled = KeSignalEvent (
        &event^.Event, // event
        0, // priorityboost
    )

@Exit

    ObuUnreferenceObject ( event )

    RETURN status
END

#SECTION "PAGEtext"
FN OsReadEvent (
    IN handle : UWORD,
    OUT signaled : UWORD,
) : OsStatus

    event : ^ExuEvent
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &ExuEventType, // type
        OUT event, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_READ != access THEN
        // Not a subset of the required access.

        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    signaled = event^.Event.Header.SignalCount

@Exit

    ObuUnreferenceObject ( event )

    RETURN status
END

#SECTION "INITtext"
FN ExpInitializeEvents ()

    // Initialize the event object type.

    ObuInitializeType ( &ExuEventType )
END