//
// Implements system work queue threads.
//

#INCLUDE "Exp.hjk"
#INCLUDE "../../Loader/Headers/Loader.hjk"

FN (KeStartThreadF) ExpWorkQueueLoop (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // This is the main loop for all of the worker threads.
    // My priority was already set by ExpInitializeWorkQueues.
    // I will spin waiting for work forever.

    queue := CAST context1 TO ^KeBalancedQueue

    WHILE TRUE DO
        entry : ^RtlListEntry

        status := KeRemoveFromBalancedQueue (
            queue, // queue
            KE_KERNEL_MODE, // waitmode
            FALSE, // alertable
            NULLPTR, // timeout
            OUT entry, // item
        )

        KeAssert ( NOT OsError ( status ) )

        item := CONTAINEROF entry TO ExWorkItem.QueueEntry

        // Got a work item. Call the routine.

        item^.Routine (
            item^.Context1, // context1
            item^.Context2, // context2
        )
    END
END

EXPORT FN ExEnqueueWorkItem (
    IN item : ^ExWorkItem,
    IN priority : ExWorkQueuePriority,
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // Enqueue a work item to the appropriate thread.
    //
    // Custody of the work item is passed off the the worker thread pool - its
    // lifetime must be guaranteed by the caller. Custody is returned when the
    // work routine is called, at which point it may be freed.

    item^.Context1 = context1
    item^.Context2 = context2

    KeInsertIntoBalancedQueue (
        &PsCurrentNode()^.Exp^.WorkQueues[priority], // queue
        &item^.QueueEntry, // item
        FALSE, // head
        0, // priorityboost
    )
END

EXPORT FN ExEnqueueTimedWorkItem (
    IN item : ^ExTimedWorkItem,
    IN priority : ExWorkQueuePriority,
    IN interval : ^RtlUquad,
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // Enqueue the timer embedded in the work item.

    item^.Priority = priority

    KeEnqueueTimer (
        &item^.Timer, // timer
        interval, // interval
        context1, // context1
        context2, // context2
    )
END

FN (KeDpcF) ExpTimedWorkItemDpc (
    IN dpc : ^KeDpc,
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // The KeTimer embedded in a timed work item expired.
    // We have to enqueue the work item for real now.

    item := CONTAINEROF dpc TO ExTimedWorkItem.Dpc

    ExEnqueueWorkItem (
        &item^.Item, // item
        item^.Priority, // priority
        context1, // context1
        context2, // context2
    )
END

EXPORT FN ExInitializeTimedWorkItem (
    IN item : ^ExTimedWorkItem,
    IN routine : ExWorkItemF,
)

    // Initialize a timed work item.

    ExInitializeWorkItem (
        &item^.Item, // item
        routine, // routine
    )

    KeInitializeDpc (
        &item^.Dpc, // dpc
        &ExpTimedWorkItemDpc, // func
    )

    KeInitializeTimer (
        &item^.Timer, // timer
        &item^.Dpc, // dpc
        "TimedWorkItem", // name
    )
END

#ENTERSECTION "INITtext"

ExpWorkPriorities : UBYTE[EX_MAX_WORK_PRIORITY] = {
    [EX_LOW_WORK_PRIORITY] = OS_PRIORITY_LOW_REALTIME,
    [EX_HIGH_WORK_PRIORITY] = OS_PRIORITY_MED_REALTIME,
    [EX_NONPAGED_WORK_PRIORITY] = OS_PRIORITY_MED_REALTIME,
}

ExpWorkVmPrivileged : UBYTE[EX_MAX_WORK_PRIORITY] = {
    [EX_NONPAGED_WORK_PRIORITY] = TRUE,
}

FN ExpInitializeWorkQueuesForNode (
    IN kenode : ^KeuNode,
)

    // Initialize the system work queues.

    expnode : ^ExpNode = KeuAllocateNodeSpace (
        kenode, // kenode
        SIZEOF ExpNode, // bytes
    )

    KeAssert ( expnode != NULLPTR )

    kenode^.Exp = expnode

    i := 0

    WHILE i < EX_MAX_WORK_PRIORITY DO
        // Initialize the work queue for this priority.
        // maxthreads == 0 causes it to be equal to the number of processors in
        // the system.

        KeInitializeBalancedQueue (
            &expnode^.WorkQueues[i], // queue
            "SysQ", // name
            0, // maxthreads
        )

        i += 1
    END

    // Compute how many threads of each priority there should be.
    // We base this off the system size.

    workthreads : UBYTE[EX_MAX_WORK_PRIORITY]
    
    systemsize := MmGetNodeSize ( kenode )

    IF systemsize <= MM_TINY_SYSTEM THEN
        workthreads[EX_NONPAGED_WORK_PRIORITY] = 1
        workthreads[EX_HIGH_WORK_PRIORITY] = 1
        workthreads[EX_LOW_WORK_PRIORITY] = 1

    ELSEIF systemsize <= MM_SMALL_SYSTEM THEN
        workthreads[EX_NONPAGED_WORK_PRIORITY] = 1
        workthreads[EX_HIGH_WORK_PRIORITY] = 2
        workthreads[EX_LOW_WORK_PRIORITY] = 3

    ELSEIF systemsize <= MM_MEDIUM_SYSTEM THEN
        workthreads[EX_NONPAGED_WORK_PRIORITY] = 2
        workthreads[EX_HIGH_WORK_PRIORITY] = 3
        workthreads[EX_LOW_WORK_PRIORITY] = 5

    ELSE
        workthreads[EX_NONPAGED_WORK_PRIORITY] = 2
        workthreads[EX_HIGH_WORK_PRIORITY] = 4
        workthreads[EX_LOW_WORK_PRIORITY] = 7
    END

    // Initialize a name string for the threads.

    name : RtlString
    RtlInitializeString ( &name, "WorkerThread" )

    // Create the worker threads.

    j := 0

    WHILE j < EX_MAX_WORK_PRIORITY DO
        i = 0

        threads := workthreads[j]

        WHILE i < threads DO
            thread : ^PsuThread

            status := PsCreateExecutiveThread (
                &name, // name
                &ExpWorkQueueLoop, // startfunc
                &expnode^.WorkQueues[j], // context1
                0, // context2
                0, // flags
                OUT thread, // thread
            )

            IF OsError ( status ) THEN
                KeCrash ( "Failed to create workers (%x)\n", status )
            END

            // Set priority here instead of in the context of the thread itself
            // so that we have a guarantee that the worker threads have their
            // correct priority by the time we leave this function. Otherwise
            // there could be a window of time where user programs could perform
            // priority-based DOS attacks if these threads don't happen to get
            // scheduled in by the time we initialize usermode.

            KeSetBasePriorityThread (
                &thread^.Tcb, // thread
                ExpWorkPriorities[j], // priority
                TRUE, // setcurrentpriority
            )

            thread^.VmPrivileged = ExpWorkVmPrivileged[j]

            // Remove the reference we got by virtue of creating the thread.

            ObUnreferenceObject ( thread )

            i += 1
        END

        j += 1
    END
END

FN ExpInitializeWorkQueues ()

    // Initialize all system work queues.

    i := 0
    max := KeQueryNodeCount ()

    WHILE i < max DO
        kenode := KeNumaNodeById ( i )

        ExpInitializeWorkQueuesForNode ( kenode )

        i += 1
    END
END

#LEAVESECTION