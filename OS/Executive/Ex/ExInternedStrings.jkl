//
// Management for string internment for the MINTIA Executive.
//

#INCLUDE "Exp.hjk"

#DEFINE EXP_NAME_BUCKETS 128

STRUCT ExpNameBucket
    TreeRoot : RtlAvlNode,
    Lock : KeLock,
END

ExpNameBuckets : ExpNameBucket[EXP_NAME_BUCKETS]

#SECTION "INITtext"
FN ExpInitializeNames ()

    // Initialize the interned name buckets.

    i := 0

    WHILE i < EXP_NAME_BUCKETS DO
        RtlInitializeAvl ( &ExpNameBuckets[i].TreeRoot )
        KeInitializeLock ( &ExpNameBuckets[i].Lock )

        i += 1
    END
END

#SECTION "PAGEtext"
FN (RtlAvlLessThanF) ExpCompareNameNodes (
    IN a : ^RtlAvlNode,
    IN b : ^RtlAvlNode,
) : UWORD

    // Return whether A < B, lexicographically.

    RETURN CAST RtlCompareString (
        ExNameToString ( CONTAINEROF a TO ExName.Entry ), // str1
        ExNameToString ( CONTAINEROF b TO ExName.Entry ), // str2
    ) TO WORD < 0
END

#SECTION "PAGEtext"
FN ExpHash (
    IN str : ^UBYTE,
    OUT len : UWORD,
) : UWORD

    // Simple hash for a string.

    hash := 0
    len = 0

    WHILE str^ DO
        hash += str^
        hash += hash << 10
        hash $= hash >> 6

        str += 1
        len += 1
    END

    hash += hash << 3
    hash $= hash >> 11
    hash += hash << 15

    RETURN hash
END

#SECTION "PAGEtext"
FN ExFindName (
    IN str : ^UBYTE,
) : ^ExName

    // Find a name in the string internment buckets.

    node : ^ExName

    // Hash the name.

    len : UWORD

    hash := ExpHash (
        str, // name
        OUT len, // len
    )

    // Get the hash bucket.

    bucket := &ExpNameBuckets[hash & (EXP_NAME_BUCKETS - 1)]

    // Lock the bucket.

    KeAcquireLockExclusive ( &bucket^.Lock )

    // Look up the name in the bucket, which is an AVL tree keyed by string and
    // ordered lexicographically.

    avlnode := bucket^.TreeRoot.Right

    WHILE TRUE DO
        IF NOT avlnode THEN
            // Didn't find it, we'll need to create it.

            BREAK
        END

        node = CONTAINEROF avlnode TO ExName.Entry

        res := CAST RtlCompareString (
            str, // str1
            ExNameToString ( node ), // str2
        ) TO WORD

        IF res == 0 THEN
            // Found it! Unlock the bucket and return the node.

            node^.References += 1

            KeReleaseLock ( &bucket^.Lock )

            RETURN node
        END

        IF res < 0 THEN
            avlnode = avlnode^.Left
        ELSE
            avlnode = avlnode^.Right
        END
    END

    // Add space for a null terminator.

    len += 1

    // Round up name length.

    len = (len + RTL_MACHINE_WORD_SIZE - 1) & ~(RTL_MACHINE_WORD_SIZE - 1)

    // Create a node.

    node = MmAllocatePool (
        MM_PAGED_POOL, // poolindex
        SIZEOF ExName + len, // bytes
        'Name', // tag
        TRUE, // wait
    )

    IF NOT node THEN
        // Failed to allocate the name.

        KeReleaseLock ( &bucket^.Lock )

        RETURN NULLPTR
    END

    // Initialize the name.

    node^.References = 1
    node^.BucketIndex = hash & (EXP_NAME_BUCKETS - 1)

    RtlCopyString (
        ExNameToString ( node ), // dest
        str, // src
        -1, // bufsize
    )

    // Insert into the tree.

    RtlInsertAvl (
        &bucket^.TreeRoot, // root
        &node^.Entry, // node
        &ExpCompareNameNodes, // comparisonfunc
    )

    // Unlock bucket and return node.

    KeReleaseLock ( &bucket^.Lock )

    RETURN node
END

#SECTION "PAGEtext"
FN ExUnreferenceName (
    IN name : ^ExName,
)

    // Unreference the name node.

    bucket := &ExpNameBuckets[name^.BucketIndex]

    // Lock the bucket.

    KeAcquireLockExclusive ( &bucket^.Lock )

    // Decrement the reference count.

    oldcount := name^.References
    name^.References = oldcount - 1

    IF oldcount == 1 THEN
        // Remove from tree.

        RtlRemoveAvl (
            &bucket^.TreeRoot, // root
            &name^.Entry, // node
        )

        // Free.

        MmFreePool (
            name, // ptr
            'Name', // tag
        )
    END

    // Unlock the bucket.

    KeReleaseLock ( &bucket^.Lock )
END

#SECTION "PAGEtext"
FN ExReferenceName (
    IN name : ^ExName,
)

    // Reference the name node.

    bucket := &ExpNameBuckets[name^.BucketIndex]

    // Lock the bucket.

    KeAcquireLockExclusive ( &bucket^.Lock )

    // Increment the reference count.

    oldcount := name^.References
    name^.References = oldcount + 1

    // Unlock the bucket.

    KeReleaseLock ( &bucket^.Lock )
END