//
// Implements support for dynamic modules.
//

#INCLUDE "Exp.hjk"

PUBLIC ExuExecutiveModule : ^ExuLoadedModule

ExpModuleLoadingLock : KeLock = KE_INITIAL_LOCK

ExpKnownModuleLookupTree : RtlAvlNode

#SECTION "PAGEtext"
FN ExpLookupKnownModule (
    IN name : ^RtlString,
) : ^ExuKnownModule

    // Look up a known module by name. Assumes the module loading lock is held.

    // Look up the name in the tree, which is an AVL tree keyed by string and
    // ordered lexicographically.

    avlnode := ExpKnownModuleLookupTree.Right

    WHILE TRUE DO
        IF NOT avlnode THEN
            // Didn't find it.

            RETURN NULLPTR
        END

        knownmodule := CONTAINEROF avlnode TO ExuKnownModule.Entry

        res := CAST RtlCompareRtlString (
            name, // str1
            &knownmodule^.Name, // str2
        ) TO WORD

        IF res == 0 THEN
            // Found it! Return it.

            RETURN knownmodule
        END

        IF res < 0 THEN
            avlnode = avlnode^.Left
        ELSE
            avlnode = avlnode^.Right
        END
    END

    KeAssert ( FALSE )
END

#SECTION "PAGEtext"
FN (RtlAvlLessThanF) ExpCompareKnownModuleNames (
    IN a : ^RtlAvlNode,
    IN b : ^RtlAvlNode,
) : UWORD

    // Compare the names.

    RETURN CAST RtlCompareRtlString (
        &(CONTAINEROF a TO ExuKnownModule.Entry)^.Name, // str1
        &(CONTAINEROF b TO ExuKnownModule.Entry)^.Name, // str2
    ) TO WORD < 0
END

#SECTION "PAGEtext"
FN ExuLookupKnownModule (
    IN name : ^RtlString,
    IN create : UWORD,
) : ^ExuKnownModule

    // Look up a known module, or register it.
    //
    // If create is FALSE, a return value of NULLPTR means it wasn't found.
    // If create is TRUE, a return value of NULLPTR means insufficient memory.

    KeAcquireLockExclusive ( &ExpModuleLoadingLock )

    knownmodule := ExpLookupKnownModule ( name )

    IF knownmodule OR NOT create THEN
        // Found it, or we didn't find it and don't wanna create it.
        // In either case just return what we have.

        GOTO Exit
    END

    knownmodule = MmAllocatePool (
        MM_PAGED_POOL, // poolindex
        SIZEOF ExuKnownModule, // bytes
        'KnMo', // tag
        TRUE, // wait
    )

    IF NOT knownmodule THEN
        GOTO Exit
    END

    // Initialize the name.

    status := ExDuplicateString (
        &knownmodule^.Name, // outputstring
        name, // inputstring
        MM_PAGED_POOL, // poolindex
        'KnMs', // tag
    )

    IF OsError ( status ) THEN
        // Failed to duplicate the name string.

        MmFreePool (
            knownmodule, // ptr
            'KnMo', // tag
        )

        knownmodule = NULLPTR

        GOTO Exit
    END

    // Insert it into the lookup tree.

    RtlInsertAvl (
        &ExpKnownModuleLookupTree, // root
        &knownmodule^.Entry, // node
        &ExpCompareKnownModuleNames, // comparisonfunc
    )

    // Initialize as not having an associated loaded module.

    knownmodule^.LoadedModule = NULLPTR

@Exit

    KeReleaseLock ( &ExpModuleLoadingLock )

    RETURN knownmodule
END

#SECTION "PAGEtext"
FN (ObuTypeDeleteF) ExpDeleteModuleObject (
    IN object : ^VOID,
) : UWORD

    // The module's refcount hit zero, which means none of its classes are in
    // use. We should unload it now.

    // ...

    RETURN TRUE
END

STRUCT ExpModuleContext
    KnownModule : ^ExuKnownModule,
END

#SECTION "PAGEtext"
FN (ObuTypeInitializeF) ExpInitializeModuleObject (
    IN object : ^VOID,
    IN context : ^VOID,
) : OsStatus

    // Initialize an ExuLoadedModule object.

    module := CAST object TO ^ExuLoadedModule
    ctx := CAST context TO ^ExpModuleContext

    // Set the known module that this loaded module will be associated with.

    module^.KnownModule = ctx^.KnownModule

    module^.Flags = 0

    RETURN OS_STATUS_SUCCESS
END

PUBLIC ExuModuleType : ObuType = {
    [Name] = "Module",

    [Delete] = &ExpDeleteModuleObject,

    [Initialize] = &ExpInitializeModuleObject,

    [TypeIdentifier] = OS_MODULE_TYPE,
    [Tag] = 'Modu',

    [TypicalBodySize] = SIZEOF ExuLoadedModule,

    [IsPaged] = FALSE,
}

#SECTION "PAGEtext"
FN ExpCreateModule (
    IN knownmodule : ^ExuKnownModule,
    OUT module : ^ExuLoadedModule,
) : OsStatus

    // Create an ExuLoadedModule object.

    ctx : ExpModuleContext

    ctx.KnownModule = knownmodule

    obparams : ObuParameters

    ObuInitializeParameters (
        &obparams, // obparams
        &ExuModuleType, // type
        &ctx, // context
        PsuSystemCredentials (), // cred
        OS_ACCESS_OWNER_ALL, // access
        0, // flags
        SIZEOF ExuLoadedModule, // bodysize
        0, // pagedbodysize
        0, // npquotabias
        0, // pgquotabias
    )

    RETURN ObuAllocateObject (
        &obparams, // obparams
        OUT module, // object
    )
END

#ENTERSECTION "INITtext"

FN ExpInitializeModules ()

    // Initialize the loadable modules package.

    // Initialize the module type.

    ObuInitializeType ( &ExuModuleType )

    // Create a "known module" for the kernel itself.

    kernelname : RtlString

    RtlInitializeString ( &kernelname, "mpx.sys" )

    knownmodule := ExuLookupKnownModule (
        &kernelname, // name
        TRUE, // create
    )

    IF NOT knownmodule THEN
        KeCrash ( "%s: failed to create known module\n", "ExpInitializeModules" )
    END

    // Now create a special ExuLoadedModule to represent the executive binary
    // and IO classes implemented therein.

    module : ^ExuLoadedModule

    status := ExpCreateModule (
        knownmodule, // knownmodule
        OUT module, // module
    )

    IF OsError ( status ) THEN
        KeCrash ( "%s: failed to create module\n", "ExpInitializeModules" )
    END

    // Set the known module to point here.

    knownmodule^.LoadedModule = module

    // Set the special executive module pointer.

    ExuExecutiveModule = module
END

#LEAVESECTION