//
// Implements event counters, which are little refcounted memory blocks
// containing event objects.
//

#INCLUDE "Exp.hjk"

#SECTION "INITtext"
FN ExpInitializeEventCountersForNode (
    IN expnode : ^ExpNode,
)

    // Initialize the event counter list.

    expnode^.EventCounterCache = MmCreatePoolCache (
        MmCurrentNode (), // node
        "Event Counters", // name
        SIZEOF ExuEventCounter, // size
        MM_NONPAGED_POOL, // poolindex
        'EvCn', // tag
    )

#ENTERSECTION "INITtext"
    IF NOT expnode^.EventCounterCache THEN
        KeCrash ( "ExpInitializeEventCounters: failed to create cache\n" )
    END
#LEAVESECTION

END

#SECTION "INITtext"
FN ExpInitializeEventCounters ()

    // Initialize the event counter list.

    i := 0
    nodes := KeQueryNodeCount ()

    WHILE i < nodes DO
        expnode := KeNumaNodeById ( i )^.Exp

        ExpInitializeEventCountersForNode ( expnode )

        i += 1
    END
END

EXPORT FN ExAllocateEventCounter (
    IN wait : UWORD,
) : ^ExuEventCounter

    // Allocate an event counter.

    expnode := PsCurrentNode ()^.Exp

    counter : ^ExuEventCounter = MmAllocateFromPoolCache (
        expnode^.EventCounterCache, // cache
        wait, // wait
    )

    IF NOT counter THEN
        RETURN NULLPTR
    END

    KeInitializeEvent (
        &counter^.Event, // event
        "Collision Counter", // name
        TRUE, // notification
        FALSE, // signalstate
    )

    counter^.References = 1

    RETURN counter
END

EXPORT FN ExReferenceEventCounter (
    IN counter : ^ExuEventCounter,
)

    // Increment the refcount of the counter.

    oldcount := KeIncrementUlong (
        &counter^.References, // ptr
        1, // inc
    )

    KeAssert ( oldcount != 0 )
END

EXPORT FN ExUnreferenceEventCounter (
    IN counter : ^ExuEventCounter,
)

    // Decrement the refcount of the counter.

    oldcount := KeIncrementUlong (
        &counter^.References, // ptr
        0xFFFFFFFF, // inc
    )

    KeAssert ( oldcount != 0 )

    IF oldcount == 1 THEN
        // This was the last user of this event counter. Put it on the list.

        expnode := KeNumaNodeFromSpacePointer ( counter )^.Exp

        MmFreeToPoolCache (
            expnode^.EventCounterCache, // cache
            counter, // ptr
        )
    END
END