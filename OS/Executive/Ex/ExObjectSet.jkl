//
// An implementation of a dynamic array of pointers.
//

#INCLUDE "Exp.hjk"

#DEFINE EXP_ARRAY_EXTENSION 6

FN ExuInitializeSet (
    IN set : ^ExuObjectSet,
    IN tag : UWORD,
    IN poolindex : UWORD,
)

    // Initialize a set.

    set^.Index = 0
    set^.Capacity = EXU_SET_INITIAL_BUFFER
    set^.Buffer = &set^.InitialBuffer[0]
    set^.Tag = tag
    set^.PoolIndex = poolindex
END

FN ExuAppendSet (
    IN set : ^ExuObjectSet,
    IN object : ^VOID,
) : OsStatus

    // Append an object to the set.

    index := set^.Index

    IF index < set^.Capacity THEN
@Insert

        set^.Buffer[index] = object

        set^.Index = index + 1

        RETURN OS_STATUS_SUCCESS
    END

    // Have to extend.

    // XXX Doubling the size is optimal - unfortunately in kernel land we can't
    //     be that casual with memory. I'm also aware that a realloc would be
    //     cleaner or faster. Unfortunately I don't have one.

    newcapacity := set^.Capacity + EXP_ARRAY_EXTENSION

    newbuffer := MmuAllocatePool (
        set^.PoolIndex, // poolindex
        newcapacity * RTL_MACHINE_WORD_SIZE, // bytes
        set^.Tag, // tag
        TRUE, // wait
    )

    IF NOT newbuffer THEN
        // Failed.

        RETURN OS_STATUS_NO_MEMORY
    END

    // Copy the old buffer into the new.

    RtlCopyBytes (
        newbuffer, // dest
        set^.Buffer, // src
        index * RTL_MACHINE_WORD_SIZE, // sz
    )

    // Free the old buffer.

    IF set^.Buffer != &set^.InitialBuffer[0] THEN
        MmuFreePool (
            set^.Buffer, // ptr
            set^.Tag, // tag
        )
    END

    // Set the new buffer and capacity.

    set^.Buffer = newbuffer
    set^.Capacity = newcapacity

    // Insert the object.

    GOTO Insert
END

FN ExuFindObjectInSet (
    IN set : ^ExuObjectSet,
    IN object : ^VOID,
) : UWORD

    // Find an object in the set and return its index. Returns -1 if not found.

    i := 0
    index := set^.Index
    buf := set^.Buffer

    WHILE i < index DO
        IF buf[i] == object THEN
            RETURN i
        END

        i += 1
    END

    RETURN -1
END

FN ExuRemoveObjectFromSetByIndex (
    IN set : ^ExuObjectSet,
    IN index : UWORD,
)

    // Remove an object from the set by index.

    KeAssert ( index < set^.Index )

    // Shift the contents of the buffer left by one.

    buf := set^.Buffer

    i := index + 1
    size := set^.Index

    WHILE i < size DO
        buf[i - 1] = buf[i]

        i += 1
    END

    size -= 1

    set^.Index = size

    // If the size of the set is now less than half the capacity, shrink the
    // buffer.

    newcapacity := set^.Capacity / 2

    IF size >= newcapacity OR
        buf == &set^.InitialBuffer[0] THEN

        // Size not less than half the capacity, or we're still using the
        // inlined initial buffer.

        LEAVE
    END

    newbuffer : ^^VOID

    IF newcapacity <= EXU_SET_INITIAL_BUFFER THEN
        // Use the inlined initial buffer again.

        newcapacity = EXU_SET_INITIAL_BUFFER

        newbuffer = &set^.InitialBuffer[0]

    ELSE
        // Allocate a smaller buffer.

        newbuffer = MmuAllocatePool (
            set^.PoolIndex, // poolindex
            newcapacity * RTL_MACHINE_WORD_SIZE, // bytes
            set^.Tag, // tag
            FALSE, // wait
        )

        IF NOT newbuffer THEN
            // Nevermind.

            LEAVE
        END
    END

    // Copy the buffer contents.

    RtlCopyBytes (
        newbuffer, // dest
        buf, // src
        size * RTL_MACHINE_WORD_SIZE, // size
    )

    // Free the old buffer.
    // Note we ruled out the case where this is still the initial buffer above,
    // so we can just free to pool unconditionally.

    MmuFreePool (
        buf, // ptr
        set^.Tag, // tag
    )

    // Set the new buffer.

    set^.Buffer = newbuffer
    set^.Capacity = newcapacity
END

FN ExuRemoveObjectFromSet (
    IN set : ^ExuObjectSet,
    IN object : ^VOID,
) : UWORD

    // Remove the specified object from the set. Returns TRUE if removed, FALSE
    // if not found.

    index := ExuFindObjectInSet (
        set, // set
        object, // object
    )

    IF index == -1 THEN
        RETURN FALSE
    END

    ExuRemoveObjectFromSetByIndex (
        set, // set
        index, // index
    )

    RETURN TRUE
END

FN ExuEnumerateSet (
    IN set : ^ExuObjectSet,
    IN enumfunc : ExuEnumerateSetF,
    IN context : ^VOID,
) : OsStatus

    // Enumerate an object set and call the function for each object.

    i := 0
    size := set^.Index
    buf := set^.Buffer

    WHILE i < size DO
        status := enumfunc (
            set, // set
            buf[i], // object
            context, // context
        )

        IF OsError ( status ) THEN
            RETURN status
        END

        i += 1
    END

    RETURN OS_STATUS_SUCCESS
END

FN ExuEmptySet (
    IN set : ^ExuObjectSet,
    IN enumfunc : ExuEnumerateSetF,
    IN context : ^VOID,
)

    // Empty an object set.

    IF enumfunc THEN
        ExuEnumerateSet (
            set, // set
            enumfunc, // enumfunc
            context, // context
        )
    END

    IF set^.Buffer != &set^.InitialBuffer[0] THEN
        MmuFreePool (
            set^.Buffer, // ptr
            set^.Tag, // tag
        )
    END

    // Reinitialize the set.

    ExuInitializeSet (
        set, // set
        set^.Tag, // tag
        set^.PoolIndex, // poolindex
    )
END