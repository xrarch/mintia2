//
// Contains the self-initialization routines for the MINTIA Executive.
//

#INCLUDE "Exp.hjk"
#INCLUDE "<inc>/Psu.hjk"
#INCLUDE "<inc>/Iou.hjk"
#INCLUDE "../../Loader/Headers/Loader.hjk"
#INCLUDE "<ll>/System/OsBootFlags.hjk"

FNPTR KtMainF ()

ExuBootFlags : ULONG = 0

#SECTION "INITtext"
FN ExpGetExistenceArg (
    IN str : ^UBYTE,
) : UWORD

    // Check if an argument matching the provided string was passed.

    argcount := KeuLoaderBlock.ArgCount
    argtable := KeuLoaderBlock.ArgTable

    i := 0

    WHILE i < argcount DO
        IF RtlCompareString ( str, argtable[i] ) == 0 THEN
            RETURN TRUE
        END

        i += 1
    END

    RETURN FALSE
END

#SECTION "INITtext"
FN ExpGetValueArg (
    IN key : ^UBYTE,
) : ^UBYTE

    // Return a pointer to the value part of a key=value argument.
    // This is a pointer into the argument string itself and shouldn't
    // be manipulated. If the key does not exist, NULLPTR is returned.

    argcount := KeuLoaderBlock.ArgCount
    argtable := KeuLoaderBlock.ArgTable

    i := 0

    WHILE i < argcount DO
        str := argtable[i]
        j := 0
        name : UBYTE[16]

        WHILE str^ DO
            IF str^ == '=' THEN
                name[j] = 0

                IF RtlCompareString ( key, &name[0] ) == 0 THEN
                    RETURN str + 1
                END

                BREAK
            END

            IF j == 15 THEN
                BREAK
            END

            name[j] = str^

            j += 1
            str += 1
        END

        i += 1
    END

    RETURN NULLPTR
END

#SECTION "INITtext"
FN ExpInitializeBootFlags ()

    // Initialize all of the boot flags.
    // Currently we don't move the arguments out of loader space before we free
    // it, so we can only check them during early initialization.

    IF ExpGetExistenceArg ( "-s" ) THEN
        // Boot the system in single user mode.

        ExuBootFlags |= OS_BOOT_SINGLE_USER
    END

    IF ExpGetExistenceArg ( "-nonpagedexec" ) THEN
        // Force the paged executive to be nonpaged.

        ExuBootFlags |= OS_BOOT_NONPAGED_EXECUTIVE
    END

    IF ExpGetExistenceArg ( "-nonpagedpool" ) THEN
        // Force the paged pool to be nonpaged.

        ExuBootFlags |= OS_BOOT_NONPAGED_POOL
    END
END

#SECTION "INITtext"
FN (KeStartThreadF) ExuStartSystemProcess (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // Perform stage 2 initialization of the Executive. We are now inside the
    // system process which means we have a proper container for kernel virtual
    // memory. This thread will become the balance manager.

    // Start the general worker threads.

    ExpInitializeWorkQueues ()

    // Initialize virtual memory.
    // Starts the worker threads for each node (balance set manager, zero page
    // worker, etc) among other things.

    // MmuInitializeStage2 ()

    // Initialize modules.

    ExpInitializeModules ()

    // Initialize I/O.

    IouInitialize ()

    // Initialize IPC.

    // IpcInitialize ()

    // Initialize the executive video terminals.

    // ExuInitializeVideoTerminals ()

    // Jettison unused boot resources.

    MmuJettisonUnusedResources ()

    // Initialize the swap worker.

    // MmuInitializeSwapWorker ()

    // Perform kernel tests.

    IF KeuLoaderBlock.KtMain THEN
        ktmain := CAST KeuLoaderBlock.KtMain TO KtMainF

        ktmain ()
    END

    // Initialize OSDLL and the SystemInit.exe process.

    PsuInitializeStage2 ()

    // Reuse this thread as the balance manager for node zero.

    MmuBalanceManager ()
END

#SECTION "INITtext"
FN ExuStartExecutive ()

    // Called by the Kernel (Ke) after it finishes its own self-initialization.
    // Responsible for kick-starting the initialization of the rest of
    // the Executive.
    //
    // Performs stage 1 initialization of various components, which is not
    // allowed to block. Then creates the balance manager thread for the boot
    // node which will first perform stage 2 initialization in its context.

    i := 0
    nodes := KeQueryNodeCount ()

    WHILE i < nodes DO
        kenode := KeNumaNodeById ( i )

        expnode : ^ExpNode = KeuAllocateNodeSpace (
            kenode, // kenode
            SIZEOF ExpNode, // bytes
        )

        KeAssert ( expnode != NULLPTR )

        kenode^.Exp = expnode

        i += 1
    END

    // First check for and initialize the boot flags.

    ExpInitializeBootFlags ()

    // Initialize the object manager.

    ObuInitialize ()

    // Initialize physical memory allocation.

    MmuInitializeStage1 ()

    // Initialize event counters.

    ExpInitializeEventCounters ()

    // Initialize name internment.

    ExpInitializeNames ()

    // Initialize the event object.

    ExpInitializeEvents ()

    // Initialize the namespace manager.

    NsuInitialize ()

    // Initialize the system process and create the balance manager, in whose
    // context we will finish initialization.

    PsuInitializeStage1 ()

    // Return and become the idle loop for the boot processor.
END