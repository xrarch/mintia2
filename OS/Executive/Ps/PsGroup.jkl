//
// Implementation of POSIX-compatible process groups.
//

#INCLUDE "Psp.hjk"

#SECTION "PAGEtext"
FN PspInheritProcessGroup (
    IN child : ^PsuProcess,
    IN parent : ^PsuProcess,
)

    // Inherit the process group of the parent into the child.

    parentpaged := parent^.Paged
    childpaged := child^.Paged

    KeAcquireLockExclusive ( &parentpaged^.GroupLock )

    IF parentpaged^.SessionGroup THEN
        // If there's a session group then there's a process group too.
        // Reference both and inherit in the child.

        group := parentpaged^.ProcessGroup
        sidgroup := parentpaged^.SessionGroup

        ObReferenceObject ( sidgroup )
        ObReferenceObject ( group )

        childpaged^.SessionGroup = sidgroup
        childpaged^.ProcessGroup = group

        // Add the child to the process group's list.

        KeAcquireLockExclusive ( &group^.Lock )

        RtlInsertAtTailList (
            &group^.ProcessListHead, // head
            &childpaged^.GroupEntry, // entry
        )

        KeReleaseLock ( &group^.Lock )

    ELSE
        childpaged^.SessionGroup = NULLPTR
        childpaged^.ProcessGroup = NULLPTR
    END

    KeReleaseLock ( &parentpaged^.GroupLock )
END

#SECTION "PAGEtext"
FN PspRemoveProcessGroup (
    IN process : ^PsuProcess,
)

    // Remove the process from its process group.
    // Synchronization is taken care of by the caller.

    paged := process^.Paged
    group := paged^.ProcessGroup

    // Remove the child from the group list.

    KeAcquireLockExclusive ( &group^.Lock )

    RtlRemoveEntryList ( &paged^.GroupEntry )

    KeReleaseLock ( &group^.Lock )

    // Unreference the group.

    ObUnreferenceObject ( group )

    // Unreference the session.

    ObUnreferenceObject ( paged^.SessionGroup )
END

#SECTION "PAGEtext"
FN (ObuTypeDeleteF) PspDeleteGroupObject (
    IN object : ^VOID,
) : UWORD

    // Delete a group object.

    group := CAST object TO ^PsuProcessGroup

    IF group^.LeaderDied THEN
        // If the leader died then we're currently occupying a position in the
        // PID table, which we need to get rid of now.

        PspDeleteId ( group^.LeaderPid )
    END

    RETURN TRUE
END

#SECTION "PAGEtext"
FN (ObuTypeInitializeF) PspInitializeGroupObject (
    IN object : ^VOID,
    IN context : ^VOID,
) : OsStatus

    // Initialize a group object.

    group := CAST object TO ^PsuProcessGroup

    KeInitializeLock ( &group^.Lock )

    group^.LeaderDied = FALSE

    RtlInitializeList ( &group^.ProcessListHead )

    RETURN OS_STATUS_SUCCESS
END

PUBLIC PsuGroupType : ObuType = {
    [Name] = "Group",

    [Delete] = &PspDeleteGroupObject,
    [Initialize] = &PspInitializeGroupObject,

    [WaitOffset] = OBU_TYPE_NO_WAIT_OFFSET,
    [TypeIdentifier] = OS_GROUP_TYPE,
    [Tag] = 'Grup',

    [TypicalBodySize] = SIZEOF PsuProcessGroup,

    [IsPaged] = TRUE,
}

#SECTION "PAGEtext"
FN PspCreateGroup (
    IN leader : ^PsuProcess,
    OUT group : ^PsuProcessGroup,
) : OsStatus

    // Create a new group object.

    obparams : ObParameters

    ObInitializeParameters (
        &obparams, // obparams
        &PsuGroupType, // type
        NULLPTR, // context
        PsuCurrentCredentials (), // cred
        OS_ACCESS_OWNER_ALL, // access
        0, // flags
        SIZEOF PsuProcessGroup, // bodysize
        0, // pagedbodysize
        0, // npquotabias
        0, // pgquotabias
    )

    RETURN ObuAllocateObject (
        &obparams, // obparams
        OUT group, // object
    )
END

#SECTION "PAGEtext"
FN PsuSetGroupProcess (
    IN process : ^PsuProcess,
    IN group : ^PsuProcessGroup,
    IN newsession : UWORD,
) : OsStatus

    // Set the group of the process to the specified group.

    status := OS_STATUS_SUCCESS

    paged := process^.Paged

    newgroup := NOT group

    IF newgroup THEN
        // No group was supplied, so make one.

        status = PspCreateGroup (
            process, // leader
            OUT group, // group
        )

        IF OsError ( status ) THEN
            RETURN status
        END

    ELSE
        // Add a reference to the group to reflect the new member.

        ObReferenceObject ( group )
    END

    // Acquire the process's group lock.

    KeAcquireLockExclusive ( &paged^.GroupLock )

    oldgroup := paged^.ProcessGroup

    // We should only encounter a process without a group when a system session
    // is being created.

    KeAssert ( (oldgroup != NULLPTR) OR
        (process == process^.Pcb.Node^.SystemProcess) )

    IF oldgroup THEN
        // We already belong to a group and session, so check that this is a
        // valid operation.

        IF oldgroup^.LeaderPid == paged^.Pid THEN
            // This is a group leader. Group leaders can't switch groups.

            status = OS_STATUS_IS_GROUP_LEADER

            GOTO Exit
        END

        IF oldgroup == group THEN
            // No change.

            status = OS_STATUS_SUCCESS

            GOTO Exit
        END
    END

    sessiongroup := paged^.SessionGroup

    IF newgroup THEN
        // Initialize the new group.

        IF newsession THEN
            group^.LeaderSid = paged^.Pid
        ELSE
            group^.LeaderSid = oldgroup^.LeaderSid
        END

        group^.LeaderPid = paged^.Pid

    ELSEIF oldgroup^.LeaderSid != group^.LeaderSid THEN
        // Can't switch to a group outside our session.

        status = OS_STATUS_NOT_IN_SESSION

        GOTO Exit
    END

    IF oldgroup THEN
        // Remove the process from the old group's list.

        KeAcquireLockExclusive ( &oldgroup^.Lock )

        RtlRemoveEntryList ( &paged^.GroupEntry )

        KeReleaseLock ( &oldgroup^.Lock )
    END

    // Add the process to the new group's list.

    paged^.ProcessGroup = group

    IF newsession THEN
        // Set our session group as well.

        paged^.SessionGroup = group

        // Apply an extra reference to the new session group to reflect the fact
        // we are a member of the session.

        ObReferenceObject ( group )
    END

    KeAcquireLockExclusive ( &group^.Lock )

    RtlInsertAtTailList (
        &group^.ProcessListHead, // head
        &paged^.GroupEntry, // entry
    )

    KeReleaseLock ( &group^.Lock )

    KeReleaseLock ( &paged^.GroupLock )

    IF oldgroup THEN
        // Unreference the old group, and the session group if we switched out
        // of it.

        ObUnreferenceObject ( oldgroup )

        IF newsession THEN
            ObUnreferenceObject ( sessiongroup )
        END
    END

    RETURN OS_STATUS_SUCCESS

@Exit

    // Release the process's group lock, unreference the group, and return.

    KeReleaseLock ( &paged^.GroupLock )

    ObUnreferenceObject ( group )

    RETURN status
END

#SECTION "PAGEtext"
FN OsSetGroupProcess (
    IN processhandle : UWORD,
    IN grouphandle : UWORD,
) : OsStatus

    // Set the group of a process, system service.

    process : ^PsuProcess
    access : UWORD

    status := ObuReferenceByHandle (
        processhandle, // handle
        &PsuProcessType, // type
        OUT process, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_WRITE != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit1
    END

    group : ^PsuProcessGroup = NULLPTR

    IF grouphandle != OS_NULL_HANDLE THEN
        status = ObuReferenceByHandle (
            grouphandle, // handle
            &PsuGroupType, // type
            OUT group, // object
            OUT access, // access
        )

        IF OsError ( status ) THEN
            GOTO Exit1
        END

        IF access | OS_ACCESS_WRITE != access THEN
            status = OS_STATUS_ACCESS_DENIED

            GOTO Exit2
        END
    END

    status = PsuSetGroupProcess (
        process, // process
        group, // group
        FALSE, // newsession
    )

@Exit2

    IF group THEN
        ObUnreferenceObject ( group )
    END

@Exit1

    ObUnreferenceObject ( process )

    RETURN status
END

#SECTION "PAGEtext"
FN OsOpenGroupByPid (
    IN pid : UWORD,
    IN access : UWORD,
    OUT handle : UWORD,
) : OsStatus

    // Open a group object by PID, system service.

    group : ^PsuProcessGroup

    // Get a referenced pointer to the group object.

    status := PspReferenceById (
        pid, // pid
        &PsuGroupType, // type
        OUT group, // object
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    // Check for appropriate access rights to the group.

    IF NOT ObCheckAccessForCurrent (
        group, // object
        access, // access
    ) THEN

        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    // Insert the group object into the handle table.

    status = ObuInsertObject (
        PsCurrentProcess (), // process
        group, // object
        access, // access
        OUT handle, // handle
    )

    IF OsError ( status ) THEN
        GOTO Exit
    END

    RETURN status

@Exit

    ObUnreferenceObject ( group )

    RETURN status
END

#SECTION "PAGEtext"
FN OsOpenMyGroup (
    IN access : UWORD,
    OUT handle : UWORD,
) : OsStatus

    // Open my own group object, system service.

    // Get a referenced pointer to my group object.

    currentpaged := PsCurrentProcess()^.Paged

    KeAcquireLockExclusive ( &currentpaged^.GroupLock )

    group := currentpaged^.ProcessGroup

    IF NOT group THEN
        KeReleaseLock ( &currentpaged^.GroupLock )

        RETURN OS_STATUS_INVALID_ARGUMENT
    END

    ObReferenceObject ( group )

    KeReleaseLock ( &currentpaged^.GroupLock )

    status : OsStatus

    // Check for appropriate access rights to the group.

    IF NOT ObCheckAccessForCurrent (
        group, // object
        access, // access
    ) THEN

        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    // Insert the group object into the handle table.

    status = ObuInsertObject (
        PsCurrentProcess (), // process
        group, // object
        access, // access
        OUT handle, // handle
    )

    IF OsError ( status ) THEN
        GOTO Exit
    END

    RETURN status

@Exit

    ObUnreferenceObject ( group )

    RETURN status
END

#SECTION "PAGEtext"
FN PsuSignalGroup (
    IN group : ^PsuProcessGroup,
    IN signal : UWORD,
)

    // Signal all the processes in the group.

    listhead := &group^.ProcessListHead

    KeAcquireLockExclusive ( &group^.Lock )

    listentry := listhead^.Next

    WHILE listentry != listhead DO
        process := (
            CONTAINEROF listentry TO PsuPagedProcess.GroupEntry
        )^.Process

        KeSignalProcess (
            &process^.Pcb, // process
            signal, // signal
        )

        listentry = listentry^.Next
    END

    KeReleaseLock ( &group^.Lock )
END

#SECTION "PAGEtext"
FN OsSignalGroup (
    IN handle : UWORD,
    IN signal : UWORD,
) : OsStatus

    // Signal a group, system service.

    IF KeuInvalidSignal ( signal ) THEN
        RETURN OS_STATUS_INVALID_ARGUMENT
    END

    group : ^PsuProcessGroup
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &PsuGroupType, // type
        OUT group, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_EXEC != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    PsuSignalGroup (
        group, // group
        signal, // signal
    )

@Exit

    ObUnreferenceObject ( group )

    RETURN status
END

#SECTION "PAGEtext"
FN OsNewSessionProcess (
    IN processhandle : UWORD,
) : OsStatus

    // Put a process into a new session, system service.

    process : ^PsuProcess
    access : UWORD

    status := ObuReferenceByHandle (
        processhandle, // handle
        &PsuProcessType, // type
        OUT process, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_WRITE != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    status = PsuSetGroupProcess (
        process, // process
        NULLPTR, // group
        TRUE, // newsession
    )

@Exit

    ObUnreferenceObject ( process )

    RETURN status
END