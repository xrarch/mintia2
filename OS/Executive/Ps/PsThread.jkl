//
// Implements executive-level thread objects.
//

#INCLUDE "Psp.hjk"

STRUCT PspThreadContext
    Name : ^RtlString,
    StartFunc : KeStartThreadF,
    Context1 : UWORD,
    Context2 : UWORD,
    Process : ^PsuProcess,
    Flags : ULONG,
END

PUBLIC PspTurnstileCache : ^MmpPoolCache

#MACRO PspAllocateTurnstile () [
    MmAllocateFromPoolCache (
        PspTurnstileCache, // cache
        TRUE, // wait
    )
]

#MACRO PspFreeTurnstile ( turnstile ) [
    MmFreeToPoolCache (
        PspTurnstileCache, // cache
        turnstile, // ptr
    )
]

#SECTION "PAGEtext"
FN PspPerformEagerThreadCleanup (
    IN thread : ^PsuThread,
)

    // Perform the parts of thread cleanup that can be done "eagerly" after the
    // thread terminates and before the thread object's refcount hits zero. This
    // is anything that isn't needed by holders of handles to the thread object.

    paged := thread^.Paged

    IF paged^.CleanedUp THEN
        LEAVE
    END

    paged^.CleanedUp = TRUE

    // Free the kernel stack.

    MmuFreeKernelStack (
        CONTAINEROF thread^.Tcb.Process TO PsuProcess.Pcb, // process
        thread^.Tcb.KernelStackTop -
        (KEU_STACK_PAGES << RTL_PAGE_SHIFT), // kstack
    )

    // Free the turnstile.

    PspFreeTurnstile ( thread^.Tcb.Turnstile )

    IF paged^.IpcBlock THEN
        // Free the IPC block.

        MmFreePool (
            paged^.IpcBlock, // ptr
            'Ipcb', // tag
        )
    END
END

#SECTION "PAGEtext"
FN (ObuTypeDeleteF) PspDeleteThreadObject (
    IN object : ^VOID,
) : UWORD

    // Delete a thread object.

    thread := CAST object TO ^PsuThread

    paged := thread^.Paged

    process := CONTAINEROF thread^.Tcb.Process TO PsuProcess.Pcb

    // Delete the TID.

    PspDeleteId ( paged^.Tid )

    // Unreference the interned name.

    ExUnreferenceName ( paged^.Name )

    // Perform eager cleanup if it hasn't been done.

    PspPerformEagerThreadCleanup ( thread )

    // Unreference the process.

    ObUnreferenceObject ( process )

    RETURN TRUE
END

#SECTION "PAGEtext"
EXPORT FN PsExitThread ()

    // This thread has reached the end of its life. Usermode will never regain
    // control.

    thread := PsCurrentThread ()

    process := PsCurrentProcess ()

    // Make our priority the lowest real-time priority to expedite the releasing
    // of this thread's resources.

    KeSetPriorityThread (
        &thread^.Tcb, // thread
        OS_PRIORITY_LOW_REALTIME, // priority
    )

    // Ignore events so nothing weird happens while we're cleaning up.

    KeuIgnoreEvents ()

    // If there are any asynchronous I/O operations in-flight, try to cancel
    // them and wait for completion.

    IF NOT RtlEmptyList ( &thread^.IopListHead ) THEN
        // It's safe to check this list in this manner because the current
        // thread is the only one who will ever enqueue items to this list,
        // and we ain't doing that right now. Although, I/O completion APCs
        // can asynchronously remove items from it in our context, but that
        // doesn't matter when we're just checking for emptiness.

        IouRundownThread ( thread )
    END

    // Uninitialize the thread with respect to Mm.

    MmuUninitializeThread (
        process, // process
        thread, // thread
    )

    // Uninitialize the thread with respect to Ke.

    IF KeuUninitializeThread ( &thread^.Tcb ) THEN
        // Ke marked the thread's process terminated. Threads can no longer be
        // created within the process and early exit processing should be
        // performed.
        //
        // Decrement the rundown deferment count and possibly cause process
        // rundown.

        IF thread^.Paged^.TerminationStatus != OS_STATUS_SUCCESS THEN
            process^.Paged^.TerminationStatus = thread^.Paged^.TerminationStatus
        END

        PsuUnblockRundownProcess ( process )
    END

    // Now exit the thread. This does not return.
    // The thread object will be enqueued to the reaper thread.

    KeuExitThread ( &thread^.Tcb )
END

#SECTION "PAGEtext"
FN (KeStartThreadF) PspUserThreadEntry (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // Entry point for all usermode threads.

    thread := PsCurrentThread ()

    // Build the TEB.

    // ...

    // Copy the TEB to userspace.

    // ...

    // Enter usermode for the first time, in some architecture-dependent way.

    // ...

    KeCrash ( "NYI PspUserThreadEntry\n" )
END

#SECTION "PAGEtext"
FN (ObuTypeInitializeF) PspInitializeThreadObject (
    IN object : ^VOID,
    IN context : ^VOID,
) : OsStatus

    thread := CAST object TO ^PsuThread
    ctx := CAST context TO ^PspThreadContext

    status : OsStatus

    // Initialize the thread object.

    flags := ctx^.Flags
    process := ctx^.Process

    paged := CAST ObuFindPagedPart ( thread ) TO ^PsuPagedThread

    thread^.Paged = paged

    // Intern the name.

    paged^.Name = ExFindName ( ctx^.Name )

    IF NOT paged^.Name THEN
        RETURN OS_STATUS_NO_MEMORY
    END

    // Allocate the kernel stack.

    kstack : ^VOID

    status = MmuCreateKernelStack (
        process, // process
        OUT kstack, // kstack
    )

    IF OsError ( status ) THEN
        GOTO Error2
    END

    // Create a TID.

    status = PspCreateId (
        thread, // object
        OUT paged^.Tid, // id
    )

    IF OsError ( status ) THEN
        GOTO Error3
    END

    paged^.TerminationStatus = OS_STATUS_SUCCESS

    paged^.CleanedUp = FALSE

    // Allocate a turnstile.

    turnstile := PspAllocateTurnstile ()

    IF NOT turnstile THEN
        status = OS_STATUS_NO_MEMORY

        GOTO Error4
    END

    // Use the start function to determine whether this is a usermode or kernel
    // thread.

    IF ctx^.StartFunc == &PspUserThreadEntry THEN
        paged^.Mode = KE_USER_MODE
    ELSE
        paged^.Mode = KE_KERNEL_MODE
    END

    // Initialize the thread for usage by Mm.

    status = MmuInitializeThread (
        process, // process
        thread, // thread
        paged^.Mode, // mode
    )

    IF OsError ( status ) THEN
        GOTO Error5
    END

    // Initialize the thread for usage by Ob.

    paged^.DeletingObject = FALSE
    paged^.DeferredObjectHead = NULLPTR

    // Initialize the thread for usage by Io.

    RtlInitializeList ( &thread^.IopListHead )

    RtlInitializeList ( &thread^.DeferredRequestListHead )

    RtlInitializeList ( &thread^.DeferredCompletionListHead )

    thread^.CurrentTrampoline = NULLPTR
    thread^.UpwardFlowingPacket = FALSE

    // Initialize for usage by Ipc.

    paged^.IpcBlock = NULLPTR

    // Initialize the thread for usage by Ke.
    // The thread should be initialized enough that its visibility to the
    // process via the thread list is safe, since it's added to that by Ke here.

    // Attach to the process to ensure it is resident.

    KeuAttachProcess (
        &process^.Pcb, // process
        FALSE, // try
    )

    IF NOT KeuInitializeThread (
        &process^.Pcb, // process
        &thread^.Tcb, // thread
        ctx^.Name^.Data, // name
        kstack, // kstack
        KEU_STACK_PAGES << RTL_PAGE_SHIFT, // kstacksize
        turnstile, // turnstile
        ctx^.StartFunc, // startfunc
        ctx^.Context1, // context1
        ctx^.Context2, // context2
    ) THEN
        // The process was terminated already.

        KeuDetachProcess ( &process^.Pcb )

        status = OS_STATUS_WAS_TERMINATED

        GOTO Error6
    END

    KeuDetachProcess ( &process^.Pcb )

    // Inherit the signal mask from the current thread.

    current := PsCurrentThread ()

    thread^.Tcb.SignalAcceptMask[0] |= current^.Tcb.SignalAcceptMask[0]
    thread^.Tcb.SignalAcceptMask[1] |= current^.Tcb.SignalAcceptMask[1]

    // Bias the reference count of the thread object.
    // This reflects an active execution context existing, which means its
    // kernel stack and other resources should remain in existence. This bias is
    // removed by the reaper thread after the thread terminates.

    ObReferenceObject ( thread )

    // Bias the reference count of the process object.
    // This reflects an active thread existing within it. This bias is removed
    // when the thread object is deleted.

    ObReferenceObject ( process )

    IF flags & OS_THREAD_SUSPENDED THEN
        // Suspend the thread. It will block on the suspension event upon return
        // to usermode.

        KeSuspendThread ( &thread^.Tcb )
    END

    // Set the thread a-runnin'.
    //
    // KeuReadyThread ( &thread^.Tcb )
    //
    // Nevermind, the caller will do that. Basically there might still be outer
    // failure modes and we can't know at this stage whether the thread should
    // be set running yet.

    RETURN OS_STATUS_SUCCESS

@Error6

    MmuUninitializeThread (
        process, // process
        thread, // thread
    )

@Error5

    PspFreeTurnstile ( turnstile )

@Error4

    PspDeleteId ( paged^.Tid )

@Error3

    MmuFreeKernelStack (
        process, // process
        kstack, // kstack
    )

@Error2

    ExUnreferenceName ( paged^.Name )

    RETURN status
END

#SECTION "PAGEtext"
FN (KeuProcessCustodyListF) PspReapThread (
    IN list : ^KeuCustodyList,
    IN object : ^VOID,
    IN context : ^VOID,
)

    // Called to reap a thread as a KeuProcessCustodyList callback.

    // Ke uses the ready queue link of the thread as the custody list link.
    // This is fine since it is useless at this point in the thread's lifetime.

    thread := CONTAINEROF object TO KeuThread.ReadyEntry.Next
    psthrd := CONTAINEROF thread TO PsuThread.Tcb

    threadlisthead := CAST context TO ^^KeuThread

#IF BLD_MP
    // Execute the reaper barrier to make sure the thread has fully switched off
    // its stack.

    KeuReaperBarrier ( thread )
#END

    // Perform eager cleanup of the thread object to rid ourselves of resources
    // no longer needed that it's best to free as early as we can.

    PspPerformEagerThreadCleanup ( psthrd )

    // Add the thread to the list for final unreferencing.

    thread^.ReadyEntry.Next = CAST threadlisthead^ TO ^VOID
    threadlisthead^ = thread
END

#SECTION "PAGEtext"
FN (ExWorkItemF) PsuReaperWorker (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // Implements the thread reaper.

    kenode := CAST context1 TO ^KeuNode

    threadlisthead : ^KeuThread[1]
    threadlisthead[0] = NULLPTR

    KeuProcessCustodyList (
        kenode^.ReaperCustodyList, // list
        &PspReapThread, // callback
        &threadlisthead[0], // context
    )

    // Unreference the thread objects as a second step. We don't do this inline
    // with the PspPerformEagerThreadCleanup()s that PspReapThread does because
    // we really want to free all the kernel stacks we can before we try to
    // unreference the objects, to minimize the risk of contending (or even
    // deadlocking) with threads holding locks that we need to take to delete
    // this thread object, that block for free pages while holding those locks.

    thread := threadlisthead[0]

    WHILE thread DO
        next := CAST thread^.ReadyEntry.Next TO ^KeuThread
        psthrd := CONTAINEROF thread TO PsuThread.Tcb

        // Remove the reference from the thread object that signified that there
        // was an active execution context that depended on it. There no longer
        // is (and its last act was to place itself on our reaper list), so
        // unreference it.

        ObUnreferenceObject ( psthrd )

        thread = next
    END
END

FN (KeuCustodyListActivateF) PspActivateReaper (
    IN list : ^KeuCustodyList,
)

    // Called by Ke when the reaper list transitions to non-empty.
    // Enqueue the reaper timer. Fires in 100ms to trigger thread reaping.
    // PspActivateReaperDpc will be invoked, which will enqueue the work item.

    kenode := CAST list^.Context TO ^KeuNode

    interval : RtlUquad

    RtlSetUquadToUlong ( &interval, 100 )

    ExEnqueueTimedWorkItem (
        &kenode^.Psp^.ReaperTimedWorkItem, // item
        EX_HIGH_WORK_PRIORITY, // priority
        &interval, // interval
        kenode, // context1
        0, // context2
    )
END

#SECTION "PAGEtext"
FN PsuTerminateThreadObject (
    IN thread : ^PsuThread,
    IN status : OsStatus,
)

    // Terminate the thread object.

    IF status != OS_STATUS_SUCCESS THEN
        thread^.Paged^.TerminationStatus = status
    END

    KeSignalThread (
        &thread^.Tcb, // thread
        OS_SIGNAL_KILL, // signal
    )
END

#SECTION "PAGEtext"
FN OsTerminateThread (
    IN handle : UWORD,
    IN status : OsStatus,
) : OsStatus

    // Terminate a thread, system service.

    thread : ^PsuThread
    access : UWORD

    retstatus := ObuReferenceByHandle (
        handle, // handle
        &PsuThreadType, // type
        OUT thread, // object
        OUT access, // access
    )

    IF OsError ( retstatus ) THEN
        RETURN retstatus
    END

    IF access | OS_ACCESS_EXEC != access THEN
        retstatus = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    PsuTerminateThreadObject (
        thread, // thread
        status, // status
    )

@Exit

    ObUnreferenceObject ( thread )

    RETURN retstatus
END

#SECTION "PAGEtext"
FN OsSuspendThread (
    IN handle : UWORD,
) : OsStatus

    // Suspend a thread, system service.

    thread : ^PsuThread
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &PsuThreadType, // type
        OUT thread, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_EXEC != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    KeSuspendThread ( &thread^.Tcb )

@Exit

    ObUnreferenceObject ( thread )

    RETURN status
END

#SECTION "PAGEtext"
FN OsUnsuspendThread (
    IN handle : UWORD,
    IN force : UWORD,
) : OsStatus

    // Unsuspend a thread, system service.

    thread : ^PsuThread
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &PsuThreadType, // type
        OUT thread, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_EXEC != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    KeUnsuspendThread (
        &thread^.Tcb, // thread
        force, // force
    )

@Exit

    ObUnreferenceObject ( thread )

    RETURN status
END

#SECTION "PAGEtext"
FN PsuReadStatusThreadObject (
    IN thread : ^PsuThread,
) : OsStatus

    // Read the termination status of a thread.

    IF NOT thread^.Tcb.Terminated THEN
        RETURN OS_STATUS_THREAD_IS_ALIVE
    END

    RETURN thread^.Paged^.TerminationStatus
END

#SECTION "PAGEtext"
FN OsReadStatusThread (
    IN handle : UWORD,
) : OsStatus

    // Read the termination status of a thread, system service.

    thread : ^PsuThread
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &PsuThreadType, // type
        OUT thread, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_READ != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    status = PsuReadStatusThreadObject ( thread )

@Exit

    ObUnreferenceObject ( thread )

    RETURN status
END

#SECTION "PAGEtext"
FN PsuQueryThreadObject (
    IN thread : ^PsuThread,
    IN query : ^OsThreadQuery,
)

    // Query information about a thread object.

    RtlFillMemoryWithUlong (
        query, // dest
        SIZEOF OsThreadQuery, // sz
        0, // ulong
    )

    RtlMoveUquad (
        &query^.UserTimeMs, // destquad
        &thread^.Tcb.UserTimeMs, // srcquad
    )

    RtlMoveUquad (
        &query^.SystemTimeMs, // destquad
        &thread^.Tcb.SystemTimeMs, // srcquad
    )

    RtlMoveUquad (
        &query^.DpcTimeMs, // destquad
        &thread^.Tcb.DpcTimeMs, // srcquad
    )

    query^.Status = thread^.Tcb.Status
    query^.Terminated = thread^.Tcb.Terminated
    query^.ExitStatus = thread^.Paged^.TerminationStatus
    query^.Priority = thread^.Tcb.Priority
    query^.BasePriority = thread^.Tcb.BasePriority
END

#SECTION "PAGEtext"
FN PsuQueryThread (
    IN handle : UWORD,
    IN query : ^OsThreadQuery,
) : OsStatus

    // Query information about a thread handle.

    thread : ^PsuThread
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &PsuThreadType, // type
        OUT thread, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_READ != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    PsuQueryThreadObject (
        thread, // thread
        query, // query
    )

@Exit

    ObUnreferenceObject ( thread )

    RETURN status
END

#SECTION "PAGEtext"
FN OsQueryThread (
    IN handle : UWORD,
    IN query : ^OsThreadQuery,
) : OsStatus

    // Query information about a thread, system service.

    sysquery : OsThreadQuery

    status := PsuQueryThread (
        handle, // handle
        &sysquery, // query
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    // Copy the information to userspace.

    RETURN MmSafeCopyOut (
        query, // dest
        &sysquery, // src
        SIZEOF OsThreadQuery, // size
    )
END

#SECTION "PAGEtext"
FN OsSignalThread (
    IN handle : UWORD,
    IN signal : UWORD,
) : OsStatus

    // Signal a thread, system service.

    IF KeuInvalidSignal ( signal ) THEN
        RETURN OS_STATUS_INVALID_ARGUMENT
    END

    thread : ^PsuThread
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &PsuThreadType, // type
        OUT thread, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_EXEC != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    IF signal == OS_SIGNAL_KILL AND
        thread^.Tcb.ActualProcess != KeCurrentProcess () THEN

        // Kill the thread's whole process.
        // We don't permit single threads to be involuntarily terminated at the
        // kernel level, other than by the process itself.

        KeSignalProcess (
            thread^.Tcb.ActualProcess, // process
            signal, // signal
        )

    ELSE
        KeSignalThread (
            &thread^.Tcb, // thread
            signal, // signal
        )
    END

@Exit

    ObUnreferenceObject ( thread )

    RETURN status
END

#SECTION "PAGEtext"
FN OsMaskSignalThread (
    IN handle : UWORD,
    IN signal : UWORD,
    IN enable : UWORD,
) : OsStatus

    // Mask a signal in a thread, system service.

    IF KeuInvalidSignal ( signal ) THEN
        RETURN OS_STATUS_INVALID_ARGUMENT
    END

    thread : ^PsuThread
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &PsuThreadType, // type
        OUT thread, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_WRITE != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    KeuMaskSignalThread (
        &thread^.Tcb, // thread
        signal, // signal
        enable, // enable
    )

@Exit

    ObUnreferenceObject ( thread )

    RETURN status
END

#SECTION "PAGEtext"
FN OsDeliverOnWaitSignalThread (
    IN handle : UWORD,
    IN signal : UWORD,
    IN enable : UWORD,
) : OsStatus

    // Deliver-on-wait a signal in a thread, system service.

    IF KeuInvalidSignal ( signal ) THEN
        RETURN OS_STATUS_INVALID_ARGUMENT
    END

    thread : ^PsuThread
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &PsuThreadType, // type
        OUT thread, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_WRITE != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    KeuDeliverOnWaitSignalThread (
        &thread^.Tcb, // thread
        signal, // signal
        enable, // enable
    )

@Exit

    ObUnreferenceObject ( thread )

    RETURN status
END

#SECTION "PAGEtext"
FN OsGetFaultStatusThread (
    IN handle : UWORD,
) : OsStatus

    // Get the status for the last fault that the thread incurred.

    thread : ^PsuThread
    access : UWORD

    status := ObuReferenceByHandle (
        handle, // handle
        &PsuThreadType, // type
        OUT thread, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_READ != access THEN
        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    status = thread^.Tcb.LastFaultStatus

@Exit

    ObUnreferenceObject ( thread )

    RETURN status
END

#SECTION "PAGEtext"
FN OsSleepThread (
    IN interval : ^RtlUquad,
    IN alertable : UWORD,
) : OsStatus

    // Put the current thread to sleep for the specified number of milliseconds.

    IF KeuInvalidAlertability ( alertable ) THEN
        RETURN OS_STATUS_INVALID_ARGUMENT
    END

    sysinterval : RtlUquad

    status := MmSafeCopyIn (
        &sysinterval, // dest
        interval, // src
        SIZEOF RtlUquad, // size
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    RETURN KeSleep (
        &sysinterval, // interval
        KE_USER_MODE, // waitmode
        alertable, // alertable
    )
END

PUBLIC PsuThreadType : ObuType = {
    [Name] = "Thread",

    [Delete] = &PspDeleteThreadObject,

    [Initialize] = &PspInitializeThreadObject,

    [WaitOffset] = OFFSETOF PsuThread.Tcb.TerminationEvent,
    [TypeIdentifier] = OS_THREAD_TYPE,
    [Tag] = 'Thrd',

    [TypicalBodySize] = SIZEOF PsuThread,
    [TypicalPgBodySize] = SIZEOF PsuPagedThread,

    [IsPaged] = FALSE,
}

#SECTION "PAGEtext"
FN PsuCreateThreadObject (
    IN params : ^NsOpenParameters,
    IN name : ^RtlString,
    IN startfunc : KeStartThreadF,
    IN context1 : UWORD,
    IN context2 : UWORD,
    IN process : ^PsuProcess,
    IN flags : UWORD,
    OUT thread : ^PsuThread,
) : OsStatus

    // Create a thread object.

    ctx : PspThreadContext

    ctx.Name = name
    ctx.StartFunc = startfunc
    ctx.Context1 = context1
    ctx.Context2 = context2
    ctx.Process = process
    ctx.Flags = flags

    obparams : ObParameters

    NsInitializeObjectParametersWithNode (
        &obparams, // obparams
        params, // params
        &PsuThreadType, // type
        &ctx, // context
        SIZEOF PsuThread, // bodysize
        SIZEOF PsuPagedThread, // pagedbodysize
        0, // npquotabias
        0, // pgquotabias
        process^.Pcb.Node, // node
    )

    RETURN NsuCreateObject (
        params, // params
        &obparams, // obparams
        OUT thread, // object
    )
END

#SECTION "PAGEtext"
EXPORT FN PsCreateExecutiveThread (
    IN name : ^RtlString,
    IN startfunc : KeStartThreadF,
    IN context1 : UWORD,
    IN context2 : UWORD,
    IN flags : UWORD,
    OUT thread : ^PsuThread,
) : OsStatus

    // Helper function for creating a thread that only executes code in the
    // system executive.

    params : NsOpenParameters

    params.Path.Data = NULLPTR

    params.Captured.Flags = 0
    params.Captured.ObFlags = 0
    params.Captured.Permissions = OS_ACCESS_OWNER_ALL

    status := PsuCreateThreadObject (
        &params, // params
        name, // name
        startfunc, // startfunc
        context1, // context1
        context2, // context2
        PsuSystemProcess, // process
        0, // flags
        OUT thread, // thread
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF flags & OS_THREAD_SUSPENDED == 0 THEN
        // Set the thread running.

        KeuReadyThread ( &thread^.Tcb )
    END

    RETURN status
END

#SECTION "PAGEtext"
FN PsuCreateThread (
    IN params : ^NsOpenParameters,
    IN name : ^RtlString,
    IN startfunc : KeStartThreadF,
    IN context1 : UWORD,
    IN context2 : UWORD,
    IN processhandle : UWORD,
    IN flags : UWORD,
    OUT handle : UWORD,
) : OsStatus

    // Create a thread object - handle interface.

    process : ^PsuProcess
    access : UWORD

    status := ObuReferenceByHandle (
        processhandle, // handle
        &PsuProcessType, // type
        OUT process, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF access | OS_ACCESS_WRITE != access THEN
        // Not a subset of the required access.

        status = OS_STATUS_ACCESS_DENIED

        GOTO Exit
    END

    thread : ^PsuThread

    status = PsuCreateThreadObject (
        params, // params
        name, // name
        startfunc, // startfunc
        context1, // context1
        context2, // context2
        process, // process
        flags, // flags
        OUT thread, // thread
    )

    IF OsError ( status ) THEN
        GOTO Exit
    END

    status = ObuInsertObject (
        PsCurrentProcess (), // process
        thread, // object
        params^.Captured.Access, // access
        OUT handle, // handle
    )

    IF OsError ( status ) THEN
        IF params^.ResultFlags & NS_RESULT_CREATED THEN
            // We created the thread object successfully, but we failed to
            // insert it into the handle table (quota limit reached or some
            // such). Set the thread terminated and ready it so that it kills
            // itself.

            PsuTerminateThreadObject (
                thread, // thread
                status, // status
            )

            KeuReadyThread ( &thread^.Tcb )
        END

        ObUnreferenceObject ( thread )

    ELSEIF params^.ResultFlags & NS_RESULT_CREATED THEN
        // Ready the thread because PsuCreateThreadObject didn't do it yet.
        // We need to do this after a successful handle table insertion so that
        // the thread is only set loose when we know the caller will receive a
        // successful status. Otherwise we could get a situation where a user
        // process is very confused when a thread executed some stuff even
        // though it got an error back from the thread creation call.

        KeuReadyThread ( &thread^.Tcb )
    END

@Exit

    ObUnreferenceObject ( process )

    RETURN status
END

#SECTION "PAGEtext"
FN OsCreateThread (
    IN params : ^OsOpenParameters,
    IN name : ^RtlString,
    IN startfunc : OsStartThreadF,
    IN context : UWORD,
    IN processhandle : UWORD,
    IN flags : UWORD,
    OUT handle : UWORD,
) : OsStatus

    // Create a thread object, system service.

    sysname : RtlString

    status := MmCaptureString (
        name, // string
        &sysname, // output
        OS_NAME_MAX, // maxlen
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    sysparams : NsOpenParameters

    status = NsuCaptureOpenParameters (
        params, // userparams
        &sysparams, // sysparams
    )

    IF OsError ( status ) THEN
        GOTO Exit
    END

    status = PsuCreateThread (
        &sysparams, // params
        &sysname, // name
        &PspUserThreadEntry, // startfunc
        CAST startfunc TO UWORD, // context1
        context, // context2
        processhandle, // processhandle
        flags, // flags
        OUT handle, // handle
    )

    NsuFreeOpenParameters ( &sysparams )

@Exit

    MmFreeCapturedString ( &sysname )

    RETURN status
END