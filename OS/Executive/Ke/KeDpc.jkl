//
// Implements support for DPC objects.
//

#INCLUDE "Ki.hjk"

#IF BLD_MP

#MACRO KiAcquireDpcQueue () [
    KiDisableInterrupts ()
    prb := KI_CURRENT_PRB_LOCAL
    KiAcquireSpinlock ( &prb^.DpcQueueLock )
]

#MACRO KiReleaseDpcQueue ( oldstate ) [
    prb := KI_CURRENT_PRB_LOCAL
    KiReleaseSpinlock ( &prb^.DpcQueueLock )
    KiRestoreInterrupts ( oldstate )
]

#MACRO KiAcquireOtherDpcQueue ( prb ) [
    KiDisableInterrupts ()
    KiAcquireSpinlock ( &(prb)^.DpcQueueLock )
]

#MACRO KiReleaseOtherDpcQueue ( prb, oldstate ) [
    KiReleaseSpinlock ( &(prb)^.DpcQueueLock )
    KiRestoreInterrupts ( oldstate )
]

#ELSE

#MACRO KiAcquireDpcQueue () [
    KiDisableInterrupts ()
]

#MACRO KiReleaseDpcQueue ( oldstate ) [
    KiRestoreInterrupts ( oldstate )
]

#MACRO KiAcquireOtherDpcQueue ( prb ) [
    KiDisableInterrupts ()
]

#MACRO KiReleaseOtherDpcQueue ( prb, oldstate ) [
    KiRestoreInterrupts ( oldstate )
]

#END

EXPORT FN KeInitializeDpc (
    IN dpc : ^KeDpc,
    IN func : KeDpcF,
)

    // Initialize a DPC object.

    dpc^.Function = func
    dpc^.EnqueuedTo = NULLPTR
END

EXPORT FN KeEnqueueDpc (
    IN dpc : ^KeDpc,
    IN context1 : UWORD,
    IN context2 : UWORD,
) : UWORD

    // Enqueue a DPC object to the current processor's queue.
    // NOTE: No attempt is made whatsoever to synchronize against multiple
    //       concurrent enqueues of the same DPC from multiple processors.

    // Lock our DPC queue.

    ipl := KiAcquireDpcQueue ()

    IF dpc^.EnqueuedTo THEN
        // Already enqueued.

        KiReleaseDpcQueue ( ipl )

        RETURN FALSE
    END

    dpc^.Context1 = context1
    dpc^.Context2 = context2

    // Capture the Prb into the DPC.

    prb := KI_CURRENT_PRB
    dpc^.EnqueuedTo = prb

    // Insert into the queue.

    RtlInsertAtTailList (
        &prb^.DpcListHead, // head
        &dpc^.Entry, // entry
    )

    // Set the DPC interrupt pending.

    // Note that we subtract 1 from the IPL shift so that we can do a right
    // shift by IPL to check for pending bits later (which is the hotter path).
    // Also this assumes interrupts disabled.

#IF BLD_MP
    KiSoftInterruptSelf ( prb, KI_IPL_DPC )

#ELSE
    // On UP this can be a direct read-modify-write since we already have
    // interrupts disabled and no other processors can touch it.

    prb^.PendingSoftwareInterrupts |= KiPendingIpl ( KI_IPL_DPC )
#END

    // Release the DPC queue lock.
    // Note that we simply re-enable interrupts and therefore may not actually
    // dispatch the DPC here.
    //
    // The expectation is that critical DPCs will be enqueued from interrupt
    // context. They will therefore be dispatched on exit from the exception
    // handler. It is not anticipated that DPCs will ever be enqueued from a
    // lower IPL, but if they are, they will get dispatched next timer
    // interrupt.

    KiReleaseDpcQueue ( ipl )

    RETURN TRUE
END

EXPORT FN KeDequeueDpc (
    IN dpc : ^KeDpc,
) : UWORD

    // Dequeue a DPC.

@Retry

    // Get the Prb it was enqueued to.

    trgprb := dpc^.EnqueuedTo

    IF NOT trgprb THEN
        // It wasn't enqueued to a Prb.

        RETURN FALSE
    END

    // Lock the Prb's DPC queue.

    ipl := KiAcquireOtherDpcQueue ( trgprb )

    IF dpc^.EnqueuedTo != trgprb THEN
        // It changed while we were checking.
        // Retry.

        KiReleaseOtherDpcQueue ( trgprb, ipl )

        GOTO Retry
    END

    // Remove the DPC from the list.

    RtlRemoveEntryList ( &dpc^.Entry )

    // Set dequeued.

    dpc^.EnqueuedTo = NULLPTR

    // Release the DPC queue lock.

    KiReleaseOtherDpcQueue ( trgprb, ipl )

    RETURN TRUE
END

FN KiDispatchDpcQueue (
    IN prb : ^KiPrb,
)

    // Dispatch the DPC queue for this processor.
    // Note that we're already at DPC level on entry to this function.

    prb^.DpcActive = TRUE

    // Pop items from the list until it's emptied.

    head := &prb^.DpcListHead

    WHILE TRUE DO
        ipl := KiAcquireDpcQueue ()

        listentry := head^.Next

        IF listentry == head THEN
            // Queue's empty.

            KiReleaseDpcQueue ( ipl )

            BREAK
        END

        dpc := CONTAINEROF head^.Next TO KeDpc.Entry

        // Remove from the list.

        RtlRemoveEntryList ( &dpc^.Entry )
        dpc^.EnqueuedTo = NULLPTR

        // Capture the context before unlocking the queue, or the DPC could get
        // re-enqueued again from an interrupt or another processor, destroying
        // the context.

        context1 := dpc^.Context1
        context2 := dpc^.Context2

        // Unlock the queue.

        KiReleaseDpcQueue ( ipl )

        // Call the DPC function.

        dpc^.Function (
            dpc, // dpc
            context1, // context1
            context2, // context2
        )
    END

    prb^.DpcActive = FALSE
END