//
// Implements support for DPC objects.
//

#INCLUDE "Kep.hjk"

#IF BLD_MP

#MACRO KepAcquireDpcQueue () [
    KepDisableInterrupts ()
    prb := KEP_CURRENT_PRB_LOCAL
    KepAcquireSpinlock ( &prb^.DpcQueueLock )
]

#MACRO KepReleaseDpcQueue ( oldstate ) [
    prb := KEP_CURRENT_PRB_LOCAL
    KepReleaseSpinlock ( &prb^.DpcQueueLock )
    KepRestoreInterrupts ( oldstate )
]

#MACRO KepAcquireOtherDpcQueue ( prb ) [
    KepDisableInterrupts ()
    KepAcquireSpinlock ( &(prb)^.DpcQueueLock )
]

#MACRO KepReleaseOtherDpcQueue ( prb, oldstate ) [
    KepReleaseSpinlock ( &(prb)^.DpcQueueLock )
    KepRestoreInterrupts ( oldstate )
]

#ELSE

#MACRO KepAcquireDpcQueue () [
    KepDisableInterrupts ()
]

#MACRO KepReleaseDpcQueue ( oldstate ) [
    KepRestoreInterrupts ( oldstate )
]

#MACRO KepAcquireOtherDpcQueue ( prb ) [
    KepDisableInterrupts ()
]

#MACRO KepReleaseOtherDpcQueue ( prb, oldstate ) [
    KepRestoreInterrupts ( oldstate )
]

#END

EXPORT FN KeInitializeDpc (
    IN dpc : ^KeDpc,
    IN func : KeDpcF,
)

    // Initialize a DPC object.

    dpc^.Function = func
    dpc^.EnqueuedTo = NULLPTR
END

EXPORT FN KeEnqueueDpc (
    IN dpc : ^KeDpc,
    IN context1 : UWORD,
    IN context2 : UWORD,
) : UWORD

    // Enqueue a DPC object to the current processor's queue.
    // NOTE: No attempt is made whatsoever to synchronize against multiple
    //       concurrent enqueues of the same DPC from multiple processors.

    // Lock our DPC queue.

    ipl := KepAcquireDpcQueue ()

    IF dpc^.EnqueuedTo THEN
        // Already enqueued.

        KepReleaseDpcQueue ( ipl )

        RETURN FALSE
    END

    dpc^.Context1 = context1
    dpc^.Context2 = context2

    // Capture the Prb into the DPC.

    prb := KEP_CURRENT_PRB
    dpc^.EnqueuedTo = prb

    // Insert into the queue.

    RtlInsertAtTailList (
        &prb^.DpcListHead, // head
        &dpc^.Entry, // entry
    )

    // Set the DPC interrupt pending.

#IF BLD_MP
    KepSoftInterruptSelf ( prb, KEP_IPL_DPC )
#ELSE
    prb^.PendingSoftwareInterrupts |= KepPendingIpl ( KEP_IPL_DPC )
#END

    // Release the DPC queue lock.
    
    // Note that we simply re-enable interrupts and therefore may not actually
    // dispatch the DPC here.
    //
    // The expectation is that critical DPCs will be enqueued from interrupt
    // context. They will therefore be dispatched on exit from the exception
    // handler. It is not anticipated that DPCs will ever be enqueued from a
    // lower IPL, but if they are, they will get dispatched next timer
    // interrupt.

    KepReleaseDpcQueue ( ipl )

    RETURN TRUE
END

EXPORT FN KeDequeueDpc (
    IN dpc : ^KeDpc,
) : UWORD

    // Dequeue a DPC.

@Retry

    // Get the Prb it was enqueued to.

    trgprb := dpc^.EnqueuedTo

    IF NOT trgprb THEN
        // It wasn't enqueued to a Prb.

        RETURN FALSE
    END

    // Lock the Prb's DPC queue.

    ipl := KepAcquireOtherDpcQueue ( trgprb )

    IF dpc^.EnqueuedTo != trgprb THEN
        // It changed while we were checking. This could include the DPC being
        // dequeued, so we need to check this even on UP builds.

        // Retry.

        KepReleaseOtherDpcQueue ( trgprb, ipl )

        GOTO Retry
    END

    // Remove the DPC from the list.

    RtlRemoveEntryList ( &dpc^.Entry )

    // Set dequeued.

    dpc^.EnqueuedTo = NULLPTR

    // Release the DPC queue lock.

    KepReleaseOtherDpcQueue ( trgprb, ipl )

    RETURN TRUE
END

FN KepDispatchDpcQueue (
    IN prb : ^KepPrb,
)

    // Dispatch the DPC queue for this processor.
    // Note that we're already at DPC level on entry to this function.

    prb^.DpcActive = TRUE

    // Pop items from the list until it's emptied.

    head := &prb^.DpcListHead

    WHILE TRUE DO
        ipl := KepAcquireDpcQueue ()

        listentry := head^.Next

        IF listentry == head THEN
            // Queue's empty.

            KepReleaseDpcQueue ( ipl )

            BREAK
        END

        dpc := CONTAINEROF head^.Next TO KeDpc.Entry

        // Remove from the list.

        RtlRemoveEntryList ( &dpc^.Entry )
        dpc^.EnqueuedTo = NULLPTR

        // Capture the context before unlocking the queue, or the DPC could get
        // re-enqueued again from an interrupt, destroying the context.

        context1 := dpc^.Context1
        context2 := dpc^.Context2

        // Unlock the queue.

        KepReleaseDpcQueue ( ipl )

        // Call the DPC function.

        dpc^.Function (
            dpc, // dpc
            context1, // context1
            context2, // context2
        )
    END

    prb^.DpcActive = FALSE
END