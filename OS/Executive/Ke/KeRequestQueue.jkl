//
// Implements a spinlock-guarded request queue. Intended for devices.
//

#INCLUDE "Kep.hjk"

#IF BLD_MP

#MACRO KepAcquireRequestQueue ( queue ) [
    KepAcquireSpinlockRaise ( &(queue)^.Spinlock )
]

#MACRO KepReleaseRequestQueue ( queue, oldipl ) [
    KepReleaseSpinlockLower (
        &(queue)^.Spinlock, // spinlock
        oldipl, // oldipl
    )
]

#ELSE

#MACRO KepAcquireRequestQueue ( queue ) [
    KepRaiseIpl ( KEP_IPL_DPC )
]

#MACRO KepReleaseRequestQueue ( queue, oldipl ) [
    KepLowerIpl ( oldipl )
]

#END

EXPORT FN KeInitializeRequestQueue (
    IN queue : ^KeRequestQueue,
    IN sortpolicy : UWORD,
    IN startfunc : KeRequestStartF,
    IN completefunc : KeRequestCompleteF,
    IN maximum : UWORD,
)

    // Initialize a request queue.

    IF sortpolicy != KE_REQUEST_QUEUE_FIFO THEN
        // Initialize the roots of the pairing heaps.

        queue^.Head.Sorted.Roots[0] = NULLPTR
        queue^.Head.Sorted.Roots[1] = NULLPTR
        queue^.CurrentQueueIndex = 0

    ELSE
        // Initialize the linked list.

        RtlInitializeList ( &queue^.Head.List )
    END

    queue^.SortPolicy = sortpolicy
    queue^.Current = 0
    queue^.Maximum = maximum
    queue^.StartFunction = startfunc
    queue^.CompleteFunction = completefunc

#IF BLD_MP
    KepInitializeSpinlock ( &queue^.Spinlock )
#END

END

FN (RtlLessThanF) KepCompareRequests (
    IN a : ^RtlHeapEntry,
    IN b : ^RtlHeapEntry,
) : UWORD

    request1 := CONTAINEROF a TO KeRequestEntry.Entry.Sorted
    request2 := CONTAINEROF b TO KeRequestEntry.Entry.Sorted

    RETURN request1^.SortKey < request2^.SortKey
END

EXPORT FN KeInsertRequestQueue (
    IN queue : ^KeRequestQueue,
    IN request : ^KeRequestEntry,
)

    // Insert a request into the queue, and start it if possible.

    ipl := KepAcquireRequestQueue ( queue )

    IF queue^.Current + request^.Cost <= queue^.Maximum THEN
        // Not busy, set it busy.

        queue^.Current += request^.Cost
        queue^.Pivot = request^.SortKey

        request^.Status = KEP_REQUEST_CURRENT

        // Start the request.

        queue^.StartFunction ( request )

    ELSEIF queue^.SortPolicy THEN
        // This is a sorted request queue.

        // The queue is busy, so insert the request into the appropriate
        // priority queue.

        current := queue^.CurrentQueueIndex

        IF request^.SortKey <= queue^.Pivot THEN
            // We are currently processing entries with a key larger than the
            // current pivot. Since we're less or equal, toggle the index to
            // insert us into the "next" queue.

            current $= 1
        END

        RtlInsertIntoHeap (
            &KepCompareRequests, // comparisonfunc
            &queue^.Head.Sorted.Roots[current], // rootptr
            &request^.Entry.Sorted, // entry
        )

        // Save which heap we stored it in.

        request^.HeapIndex = current

        // Set inserted.

        request^.Status = KEP_REQUEST_ENQUEUED

    ELSE
        // This is a non-sorted request queue.

        // The queue is busy, so insert the request at the tail of the list.

        RtlInsertAtTailList (
            &queue^.Head.List, // head
            &request^.Entry.List, // entry
        )

        // Set inserted.

        request^.Status = KEP_REQUEST_ENQUEUED
    END

    KepReleaseRequestQueue ( queue, ipl )
END

FN KepStartRequests (
    IN queue : ^KeRequestQueue,
)

    // Start requests from the queue until the maximum cost is reached. Queue
    // lock is held.

@Again

    request : ^KeRequestEntry = NULLPTR

    IF queue^.SortPolicy == KE_REQUEST_QUEUE_FIFO AND
        NOT RtlEmptyList ( &queue^.Head.List ) THEN

        // Check the FIFO queue.

        request = CONTAINEROF queue^.Head.List.Next TO KeRequestEntry.Entry.List

    ELSE
        // Check the current queue.

        current := queue^.CurrentQueueIndex

        root := queue^.Head.Sorted.Roots[current]

        IF root THEN
            request = CONTAINEROF root TO KeRequestEntry.Entry.Sorted

        ELSE
            // None in current queue. Switch queues and check the other one.

            current $= 1

            queue^.CurrentQueueIndex = current

            root = queue^.Head.Sorted.Roots[current]

            IF root THEN
                request = CONTAINEROF root TO KeRequestEntry.Entry.Sorted
            END
        END
    END

    IF request AND queue^.Current + request^.Cost <= queue^.Maximum THEN
        IF queue^.SortPolicy == KE_REQUEST_QUEUE_FIFO THEN
            // Remove from FIFO list.

            RtlRemoveEntryList ( &request^.Entry.List )

        ELSE
            // Remove from heap.

            RtlRemoveFromHeap (
                &KepCompareRequests, // comparisonfunc
                &queue^.Head.Sorted.Roots[request^.HeapIndex], // rootptr
                &request^.Entry.Sorted, // entry
            )
        END

        // Start the next request.

        queue^.Current += request^.Cost
        queue^.Pivot = request^.SortKey

        request^.Status = KEP_REQUEST_CURRENT

        // Start the request.

        queue^.StartFunction ( request )

        // Find another one.

        GOTO Again
    END
END

EXPORT FN KeCompleteRequestQueue (
    IN queue : ^KeRequestQueue,
    IN request : ^KeRequestEntry,
) : UWORD

    // Complete the given request (if it hasn't already been cancelled) and
    // start any that can now be started. Returns TRUE if completed, FALSE
    // otherwise.

    completed := FALSE

    ipl := KepAcquireRequestQueue ( queue )

    KeAssert ( (request^.Status == KEP_REQUEST_CURRENT) OR
        (request^.Status == KEP_REQUEST_CANCELLED) )

    IF request^.Status == KEP_REQUEST_CURRENT THEN
        completed = TRUE

        // Set as completed.

        request^.Status = KEP_REQUEST_COMPLETED

        // The request is still current, so decrement the current cost.

        queue^.Current -= request^.Cost

        // Invoke callback to free up any structures associated with this
        // entry's cost.

        queue^.CompleteFunction (
            request, // request
            FALSE, // cancelled
        )

        // Try to start any requests that can be started.

        KepStartRequests ( queue )
    END

    KepReleaseRequestQueue ( queue, ipl )

    RETURN completed
END

EXPORT FN KeCancelRequestQueue (
    IN queue : ^KeRequestQueue,
    IN request : ^KeRequestEntry,
) : UWORD

    // Cancel the given request. Returns TRUE if cancelled, FALSE otherwise.

    cancelled := FALSE

    ipl := KepAcquireRequestQueue ( queue )

    KeAssert ( (request^.Status == KEP_REQUEST_ENQUEUED) OR
        (request^.Status == KEP_REQUEST_CURRENT) OR
        (request^.Status == KEP_REQUEST_COMPLETED) )

    status := request^.Status

    IF status != KEP_REQUEST_COMPLETED THEN
        // Hasn't been completed yet, which means its still cancellable.

        cancelled = TRUE

        request^.Status = KEP_REQUEST_CANCELLED

        IF status == KEP_REQUEST_ENQUEUED THEN
            // It was enqueued, so we have to remove it from the queue
            // structure.

            IF queue^.SortPolicy == KE_REQUEST_QUEUE_FIFO THEN
                // Remove from the FIFO list.

                RtlRemoveEntryList ( &request^.Entry.List )

            ELSE
                // Remove from the appropriate min heap.

                RtlRemoveFromHeap (
                    &KepCompareRequests, // comparisonfunc
                    &queue^.Head.Sorted.Roots[request^.HeapIndex], // rootptr
                    &request^.Entry.Sorted, // entry
                )
            END

        ELSE
            // Invoke callback to free up any structures associated with this
            // entry's cost.

            // XXX Maybe have this return a value saying whether it's okay to
            //     decrement the cost and start more requests - certain things
            //     won't be able to do that until the device naturally completes
            //     the pending request. In that case we'd want cleaning up the
            //     cost and starting the next requests to be its own API.
            //
            // XXX That's exactly what it does now - comment above left for
            //     posterity.

            IF queue^.CompleteFunction (
                request, // request
                TRUE, // cancelled
            ) THEN

                // Decrement the cost.

                queue^.Current -= request^.Cost

                // Try to start any requests that can be started.

                KepStartRequests ( queue )
            END
        END
    END

    KepReleaseRequestQueue ( queue, ipl )

    RETURN cancelled
END

EXPORT FN KeFinishCancelRequestQueue (
    IN queue : ^KeRequestQueue,
    IN request : ^KeRequestEntry,
)

    // Finish cancelling the given request. This just decrements the current
    // cost and attempts to start more requests.

    ipl := KepAcquireRequestQueue ( queue )

    KeAssert ( request^.Status == KEP_REQUEST_CANCELLED )

    // Decrement the cost.

    queue^.Current -= request^.Cost

    // Try to start any requests that can be started.

    KepStartRequests ( queue )

    KepReleaseRequestQueue ( queue, ipl )
END