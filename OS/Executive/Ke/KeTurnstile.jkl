//
// Implementation of "turnstiles" (invented by Solaris) for the MINTIA Kernel.
//
// Turnstiles are a locking mechanism whereby a single pointer-sized datum can
// be used as a mutex or rwlock, by storing a block in a hash table keyed by
// the address of the lock upon contention.
//
// NT "pushlocks" accomplish similar goals but have a thread-local block stored
// on the stack of each contending thread. This decentralization makes it easier
// on cache (each thread can do adaptive spinning in its own stack), but makes
// it a little harder to accomplish priority inheritance. Since priority
// inheritance is of great importance to the robustness of the MINTIA Executive
// (which is almost completely preemptible aside from the scheduler), the
// original usage of pushlocks in MINTIA was quickly replaced with turnstiles.
// An "autoboost"-like mechanism could have been implemented for pushlocks but
// this would have taken more time than I was willing to spend.
//

#INCLUDE "Kep.hjk"

#IF BLD_MP

#MACRO KepAcquireChain ( chain ) [
    KepAcquireSpinlockRaise ( &(chain)^.Spinlock )
]

#MACRO KepReleaseChain ( chain, ipl ) [
    KepReleaseSpinlockLower ( &(chain)^.Spinlock, ipl )
]

#MACRO KepAcquireChainElevated ( chain ) [
    KepAcquireSpinlock ( &(chain)^.Spinlock )
]

#MACRO KepReleaseChainElevated ( chain ) [
    KepReleaseSpinlock ( &(chain)^.Spinlock )
]

#MACRO KepAcquireTurnstileElevated ( turnstile ) [
    KepAcquireSpinlock ( &(turnstile)^.Spinlock )
]

#MACRO KepReleaseTurnstileElevated ( turnstile ) [
    KepReleaseSpinlock ( &(turnstile)^.Spinlock )
]

#MACRO KepReleaseTurnstileAndThreadElevated ( turnstile, thread ) [
    KepReleaseTwoSpinlocks ( &(turnstile)^.Spinlock, &(thread)^.Spinlock )
]

#ELSE

#MACRO KepAcquireChain ( chain ) [
    KepRaiseIpl ( KEP_IPL_DPC )
]

#MACRO KepReleaseChain ( chain, ipl ) [
    KepLowerIpl ( ipl )
]

#MACRO KepAcquireChainElevated ( chain ) []

#MACRO KepReleaseChainElevated ( chain ) []

#MACRO KepAcquireTurnstileElevated ( turnstile ) []

#MACRO KepReleaseTurnstileElevated ( turnstile ) []

#MACRO KepReleaseTurnstileAndThreadElevated ( turnstile, thread ) []

#END

#IF ( == BLD_BITS 32 )

#DEFINE KEP_TURNSTILE_HASH_SHIFT 2

#ELSE

#DEFINE KEP_TURNSTILE_HASH_SHIFT 3

#END

#MACRO KepTurnstileHash ( address ) [
    ((address >> KEP_TURNSTILE_HASH_SHIFT) & (KEP_TURNSTILE_CHAINS - 1))
]

#DEFINE KEP_SPIN_CYCLES 400

#SECTION "INITtext"
FN KepInitializeTurnstiles (
    IN kepnode : ^KepNode,
)

    // Initialize the turnstile package.

    // Initialize the chains.

    i := 0

    WHILE i < KEP_TURNSTILE_CHAINS DO
        RtlInitializeList ( &kepnode^.TurnstileChains[i].Head )

        i += 1
    END
END

FN (RtlLessThanF) KepCompareThreads (
    IN a : ^RtlHeapEntry,
    IN b : ^RtlHeapEntry,
) : UWORD

    // Compare the bestowed priority of two threads for insertion into the
    // turnstile's maxheap. Since it's a maxheap, we will return whether a>b so
    // that higher priority threads end up at the root.

    // Interactive threads are always greater than noninteractive threads.
    // If interactivity is the same, it's determined by the priority.

    thread1 := CONTAINEROF a TO KeuThread.TurnstileEntry
    thread2 := CONTAINEROF b TO KeuThread.TurnstileEntry

    IF thread1^.BestowedInteractivity OR
        thread1^.BestowedPriority >= OS_PRIORITY_LOW_REALTIME THEN

        IF thread2^.BestowedInteractivity OR
            thread2^.BestowedPriority >= OS_PRIORITY_LOW_REALTIME THEN

            RETURN thread1^.BestowedPriority > thread2^.BestowedPriority

        ELSE
            RETURN TRUE
        END

    ELSEIF thread2^.BestowedInteractivity OR
        thread2^.BestowedPriority >= OS_PRIORITY_LOW_REALTIME THEN

        RETURN FALSE

    ELSE
        RETURN thread1^.BestowedPriority > thread2^.BestowedPriority
    END
END

FN KepBoostPriority (
    IN owner : ^KeuThread,
    IN interactive : UWORD,
    IN priority : UWORD,
) : UWORD

    // As required, boost the priority of the thread and all of the threads upon
    // which it depends to make progress.

    original := owner

    WHILE TRUE DO
        // Boost the owner. His lock is held on entry, released on exit.

        boosted := KepRaisePriorityFloorThread (
            owner, // thread
            interactive, // interactive
            priority, // priority
        )

        // We have to follow the chain of lock owners and boost them all.
        // Each blocked thread is linked to its turnstile by BlockedOnTurnstile
        // which is guarded with its thread lock. Each turnstile is linked to
        // its current owner by the Owner field. Therefore we can find all
        // threads that are indirectly depended upon by this thread to make
        // progress; all owners of locks being waited for, by owners of locks
        // being waited for, by owners of locks being waited for, etc, by this
        // thread.

        turnstile := owner^.BlockedOnTurnstile

        IF NOT turnstile THEN
            // Not blocked on a turnstile, return.

            BREAK
        END

        IF boosted THEN
            // Move the waiter in the turnstile's maxheap to reflect a new
            // bestowed priority.

            KepAcquireTurnstileElevated ( turnstile )

            RtlRemoveFromHeap (
                &KepCompareThreads, // comparisonfunc
                &turnstile^.WaiterHeapRoot, // rootptr
                &owner^.TurnstileEntry, // entry
            )

            // Set new bestowed.

            owner^.BestowedPriority = owner^.Priority
            owner^.BestowedInteractivity = owner^.InteractiveBits

            // Re-insert.

            RtlInsertIntoHeap (
                &KepCompareThreads, // comparisonfunc
                &turnstile^.WaiterHeapRoot, // rootptr
                &owner^.TurnstileEntry, // entry
            )

            KepReleaseTurnstileElevated ( turnstile )
        END

        // NOTE: Previously we set an owner^.Boosted flag here which indicated
        //       to the owner that if he was in the middle of performing
        //       boosting on another processor, he needs to redo everything.
        //       This was unnecessary because we are definitely "behind" him
        //       in boosting the waiter chain he lies along; he necessarily got
        //       there first, and if we ever caught up we would contend with him
        //       on a thread or chain spinlock and be forced to remain behind
        //       him. So, the boosting will always happen correctly.
        //
        // owner^.Boosted = TRUE

        IF turnstile^.Event.Header.SignalCount THEN
            // The turnstile was already awoken and this thread just hasn't made
            // its way out of KepBlockOnLock yet. Note that we aren't holding
            // any lock that guards this field so this is just a hint; it could
            // change from FALSE to TRUE at any moment after this (the reverse,
            // however, could not happen).

            BREAK
        END

#IF BLD_MP
        kenode := KeNumaNodeFromArbitraryPointer ( turnstile^.Key )

        chain := &kenode^.Kep^.TurnstileChains[KepTurnstileHash(turnstile^.Key)]

        // Try to acquire the chain spinlock. Note that this is out of order
        // with respect to the thread lock, so we only try-acquire it and retry
        // the entire lock operation if we fail. Also note that try-acquire
        // spins many times rather than trying once and just giving up, just in
        // case the holder holds it along a codepath that never contends on the
        // out of order spinlock we're currently holding (which is most common).

        IF NOT KepTryAcquireSpinlock ( &chain^.Spinlock ) THEN
            // Failed to acquire it.

            KepReleaseThread ( owner, KEP_IPL_APC )

            // Indicate to caller that a retry is needed.

            RETURN TRUE
        END

        IF turnstile^.Event.Header.SignalCount THEN
            // The turnstile was awoken before we could acquire the chain
            // spinlock.

            KepReleaseChainElevated ( chain )

            BREAK
        END
#END

        KepReleaseThreadElevated ( owner )

        // Acquire the spinlock of the current owning thread of this next
        // turnstile.

        owner = turnstile^.Owner

        IF owner == original THEN
            // A cycle in the lock dependency chain indicates deadlock.
            // Crash here or else we'll spin forever.

            KeCrash ( "Turnstile waiter cycle (deadlock!)\n" )
        END

        KepAcquireThreadElevated ( owner )

        KepReleaseChainElevated ( chain )
    END

    KepReleaseThread ( owner, KEP_IPL_APC )

    // Indicate to caller that no retry is necessary.

    RETURN FALSE
END

EXTERN FN MmuWriteFaultOnLockWord (
    IN lockptr : ^VOID,
)

FN KepBlockOnLock (
    IN lock : ^KeLock,
    IN current : UWORD,
)

    // Block on the lock.

    turnstile : ^KepTurnstile
    otherturnstile : ^KepTurnstile

    // First acquire a pointer to the current thread.

    thread := KeCurrentThread ()

    // Get a pointer to the owner, if the lock is held exclusive.

    owner : ^KeuThread = NULLPTR

    IF current & KEP_LOCK_WRITE_LOCKED THEN
        owner = CAST current & KEP_LOCK_BITS TO ^KeuThread
    END

    // Lock the turnstile chain.

    kenode := KeNumaNodeFromArbitraryPointer ( lock )

    chain := &kenode^.Kep^.TurnstileChains[KepTurnstileHash(lock)]

    ipl := KepAcquireChain ( chain )

    // Set that there are waiters now. This will cause releasing threads to
    // signal the turnstile event.

    // We do this CAS even if the waiters bit is already set because we want to
    // also make sure that the owner (or the state of not having one; i.e.
    // being held shared) did not change before we were able to set the WAITERS
    // bit. This is because we need to be absolutely sure that whatever owner we
    // see in our "current" variable (which captures the lock word during a
    // certain timeframe) will be the exact thread to awaken this turnstile at
    // some point *after* this CAS is performed.

    aborted : UWORD

    IF KeCompareSwapPtrAbortOnFault (
        &lock^.Ptr, // ptr
        current | KEP_LOCK_WAITERS, // newvalue
        current, // expectedvalue
        OUT aborted, // aborted
    ) != current THEN

        // Something changed.

        // We also may have taken a page fault on the lock word. We cannot
        // handle this at this moment because our turnstile is tied up and the
        // page fault handler has to take turnstile-backed locks. The page
        // fault will be serviced when the caller tries again to claim the lock.
        // Also we're at an elevated IPL holding a spinlock so that's another
        // reason.

        KepReleaseChain ( chain, ipl )

        IF aborted THEN
            // We were aborted because we faulted on the lock word.
            // Call Mm directly to try to satisfy this page fault.
            // An upcall from Ke to Mm is technically a layering violation but
            // there's no other decent way to accomplish this.

            MmuWriteFaultOnLockWord ( lock )
        END

        LEAVE
    END

    // Scan the chain for the relevant turnstile.

    listentry := chain^.Head.Next

    WHILE TRUE DO
        IF listentry == &chain^.Head THEN
            // Reached the end of the chain and didn't find the right turnstile.
            // We'll use our thread's turnstile.

            turnstile = thread^.Turnstile

#IF BLD_CHK
            KeAssert ( turnstile != NULLPTR )
            thread^.Turnstile = NULLPTR
#END

            // Initialize it.

            turnstile^.Key = lock
            turnstile^.Free = NULLPTR
            turnstile^.Owner = owner

#IF BLD_MP
            KepInitializeSpinlock ( &turnstile^.Spinlock )
#END

            RtlInsertAtHeadList (
                &chain^.Head, // head
                &turnstile^.ChainEntry, // entry
            )

            turnstile^.WaiterHeapRoot = NULLPTR

            KeInitializeEvent (
                &turnstile^.Event, // event
                "Turnstile", // name
                TRUE, // notification
                FALSE, // signalstate
            )

            IF owner THEN
                KepAcquireThreadElevated ( owner )

                RtlInsertAtHeadList (
                    &owner^.PiTurnstileListHead, // head
                    &turnstile^.PiEntry, // entry
                )

                KepReleaseThreadElevated ( owner )
            END

            BREAK
        END

        turnstile = CONTAINEROF listentry TO KepTurnstile.ChainEntry

        IF turnstile^.Key == lock AND
            turnstile^.Owner == owner THEN

            // Found a turnstile. Stash ours on the free list for this
            // turnstile. We "donate" it like this so that anybody who wakes
            // up can quickly grab one to replace theirs.

            otherturnstile = thread^.Turnstile

            otherturnstile^.Free = turnstile^.Free
            turnstile^.Free = otherturnstile

#IF BLD_CHK
            thread^.Turnstile = NULLPTR
#END

            BREAK
        END

        listentry = listentry^.Next
    END

    IF owner THEN
        // At this point we know exactly who owns this lock and we know they
        // haven't fully left the lock yet, which means its safe to apply
        // priority inheritance to them since they definitely exist and will
        // continue to do so until we release the chain lock and their thread
        // lock.

#IF BLD_MP
        IF owner^.Status == KE_THREAD_RUNNING THEN
            // The owner exists and is currently running on another processor.
            // Spin for a while to see if we can get the lock without blocking.

            KepReleaseChain ( chain, KEP_IPL_APC )

            i := KEP_SPIN_CYCLES

            WHILE i DO
                IF turnstile^.Event.Header.SignalCount THEN
                    // It was freed!

                    GOTO UnreferenceAndLeave
                END

                // Stop the compiler from optimizing this loop out.

                BARRIER

                KeSpinPause ()

                i -= 1
            END

            KepAcquireChain ( chain )

            IF turnstile^.Event.Header.SignalCount THEN
                // Owner no longer valid.

                GOTO UnreferenceAndLeaveChainHeld
            END
        END
#END

        // We're really going to wait, so do priority boosting.

        KepAcquireThreadElevated ( thread )

        // Capture current priority as the BestowedPriority.
        //
        // NOTE: Currently no attempt is made to re-boost the waiter chain if
        //       this thread's priority is increased after it did its own
        //       boosting; boosting is done purely based on the priority we see
        //       for ourselves right here. It's assumed to be sufficient to just
        //       boost the waiter chain with whatever priority we had when we
        //       blocked, but if that assumption turns out to be faulty, it
        //       wouldn't be difficult to extend KepSetPriorityThread to unblock
        //       a thread blocked on a turnstile if its priority was raised,
        //       with an OS_STATUS_KERNEL_APC, which would cause the lock
        //       acquisition to be performed again and the waiters re-boosted
        //       (by this thread) to the new priority.

        priority := thread^.Priority
        thread^.BestowedPriority = priority

        interactive := thread^.InteractiveBits
        thread^.BestowedInteractivity = interactive

        thread^.BlockedOnTurnstile = turnstile

        // Insert the thread into the turnstile's max heap.

        KepAcquireTurnstileElevated ( turnstile )

        RtlInsertIntoHeap (
            &KepCompareThreads, // comparisonfunc
            &turnstile^.WaiterHeapRoot, // rootptr
            &thread^.TurnstileEntry, // entry
        )

        KepReleaseTurnstileAndThreadElevated ( turnstile, thread )

        // Acquire the owning thread's spinlock and release the chain.

        KepAcquireThreadElevated ( owner )

        KepReleaseChainElevated ( chain )

        IF owner^.PriorityFloor < priority OR
            (interactive AND NOT KepIsInteractiveForFloor ( owner )) THEN

            // Raise the priority floor. Returns with the thread lock released.

#IF BLD_MP
            IF KepBoostPriority (
                owner, // owner
                interactive, // interactive
                priority, // priority
            ) THEN

                // Failed to try-acquire a spinlock. Unwind and retry
                // everything.

                GOTO UnreferenceAndLeave
            END
#ELSE
            KepBoostPriority (
                owner, // owner
                interactive, // interactive
                priority, // priority
            )
#END

        ELSE
            KepReleaseThread ( owner, KEP_IPL_APC )
        END
    
    ELSE
        KepReleaseChain ( chain, KEP_IPL_APC )
    END

    // Wait on the turnstile event.
    // This wait can be cancelled if a kernel APC arrives, but the kernel APC
    // will not be delivered since we're at KEP_IPL_APC. This allows us to
    // clean up our usage of our turnstile, which lets the APC grab turnstile-
    // -backed locks once it is actually delivered when we lower IPL.

    alertable := KE_UNALERTABLE

    IF ipl == KEP_IPL_LOW THEN
        // The lock was acquired at LOW IPL, so we should cancel the wait and
        // clean up if KAPCs arrive.

        alertable = KE_CANCEL_ON_KAPC
    END

    KeWaitForSingleObject (
        KE_KERNEL_MODE, // waitmode
        alertable, // alertable
        NULLPTR, // timeout
        &turnstile^.Event.Header, // object
    )

@UnreferenceAndLeave

    KepAcquireChain ( chain )

@UnreferenceAndLeaveChainHeld

    IF thread^.BlockedOnTurnstile THEN
        KepAcquireThreadElevated ( thread )

        // Remove the thread from the turnstile's maxheap.

        thread^.BlockedOnTurnstile = NULLPTR

        KepAcquireTurnstileElevated ( turnstile )

        RtlRemoveFromHeap (
            &KepCompareThreads, // comparisonfunc
            &turnstile^.WaiterHeapRoot, // rootptr
            &thread^.TurnstileEntry, // entry
        )

        KepReleaseTurnstileAndThreadElevated ( turnstile, thread )
    END

    // Make sure we didn't magically get a turnstile back.

    KeAssert ( thread^.Turnstile == NULLPTR )

    IF turnstile^.Free THEN
        // Take a turnstile from the free list anchored in the lock turnstile.

        otherturnstile = turnstile^.Free
        turnstile^.Free = otherturnstile^.Free

        thread^.Turnstile = otherturnstile

    ELSE
        // Steal the lock turnstile itself.

        thread^.Turnstile = turnstile

        IF turnstile^.Event.Header.SignalCount == 0 THEN
            // It wasn't signaled, which means its still on the chain.
            // This happens in the case where we set up a turnstile, but then
            // decided to back out. Since the free list is empty, we're the only
            // thread with a pointer to this turnstile; nobody can be waiting
            // and no priority inheritance has been applied. Therefore we can
            // just remove the turnstile from the chain here.

            RtlRemoveEntryList ( &turnstile^.ChainEntry )

            IF owner THEN
                KepAcquireThreadElevated ( owner )

                RtlRemoveEntryList ( &turnstile^.PiEntry )

                KepReleaseThreadElevated ( owner )
            END
        END
    END

#IF BLD_NUMA
    IF KeNumaNodeFromSpacePointer ( thread ) !=
        KeNumaNodeFromSpacePointer ( thread^.Turnstile ) THEN

        // We ended up with a turnstile from a remote node. This isn't a
        // correctness issue but will cause long term performance problems. It's
        // not safe to try reallocating this right now, so defer it to a safe
        // moment, which we use the next time we drop below KEP_IPL_APC for.
        // This works since all of the blocking mutexes that turnstile
        // allocation depends upon raise to KEP_IPL_APC, so we won't be holding
        // any such mutexes at that time.

        KepSoftInterruptSelf (
            KEP_CURRENT_PRB, // targetprb
            KEP_IPL_APC, // ipl
        )

        thread^.PendingApcLevelInt = TRUE
    END
#END

    KepReleaseChain ( chain, ipl )

    // Return to retry the lock.
END

FN KepComputeFloor (
    IN thread : ^KeuThread,
)

    // Compute a new priority floor for the thread.

    head := &thread^.PiTurnstileListHead

    ipl := KepAcquireThread ( thread )

    // Scan the turnstile list to find the highest priority.

    listentry := head^.Next
    floor := 0
    interactivityfloor := FALSE

    WHILE listentry != head DO
        turnstile := CONTAINEROF listentry TO KepTurnstile.PiEntry

        KepAcquireTurnstileElevated ( turnstile )

        IF turnstile^.WaiterHeapRoot THEN
            booster :=
                CONTAINEROF turnstile^.WaiterHeapRoot
                TO KeuThread.TurnstileEntry

            IF booster^.BestowedPriority > floor THEN
                floor = booster^.BestowedPriority
            END

            interactivityfloor |= booster^.BestowedInteractivity
        END

        KepReleaseTurnstileElevated ( turnstile )

        listentry = listentry^.Next
    END

    // Apply the new priority floor.

    KepLowerPriorityFloorThread (
        thread, // thread
        interactivityfloor, // interactive
        floor, // priority
    )

    KepReleaseThread ( thread, ipl )
END

FN KepWakeLock (
    IN lock : ^KeLock,
    IN owner : ^KeuThread,
)

    // Wake all waiters on the lock.

    // Lock the turnstile chain.

    kenode := KeNumaNodeFromArbitraryPointer ( lock )

    chain := &kenode^.Kep^.TurnstileChains[KepTurnstileHash(lock)]

    turnstile : ^KepTurnstile
    otherturnstile : ^KepTurnstile

    head := &chain^.Head

    ipl := KepAcquireChain ( chain )

    listentry := head^.Next

    WHILE listentry != head DO
        turnstile = CONTAINEROF listentry TO KepTurnstile.ChainEntry

        IF turnstile^.Key == lock AND
            turnstile^.Owner == owner THEN

            // Found the turnstile.

            // Remove it from the chain.

            RtlRemoveEntryList ( &turnstile^.ChainEntry )

            // Signal the event.

            KeSignalEvent (
                &turnstile^.Event, // event
                0, // priorityboost
            )

            IF owner THEN
                // Remove from our list of turnstiles that gave us a priority
                // boost.

                KepAcquireThreadElevated ( owner )

                RtlRemoveEntryList ( &turnstile^.PiEntry )

                KepReleaseThreadElevated ( owner )
            END

            BREAK
        END

        listentry = listentry^.Next
    END

    KepReleaseChain ( chain, ipl )

    IF owner THEN
        // Compute the new priority inheritance floor.
        // We do this unconditionally even if we didn't find a turnstile, or the
        // turnstile didn't specify an owner, because there are cases where we
        // still may have received a priority boost from it.

        KepComputeFloor ( owner )
    END
END

// The following routines deal with the slow path of KeLocks, if the fast path
// routines (which are prefixed with Ke- and are written in assembly) failed.

FN KepAcquireLockShared (
    IN lock : ^KeLock,
    IN oldipl : UWORD,
) : UWORD

    // Acquire a lock shared.
    // This is structured as a CAS loop.

    current := lock^.Ptr
    new : UWORD

    WHILE TRUE DO
        IF current & KEP_LOCK_C_BITS == 0 THEN
            // The lock is free and has no exclusive waiters.
            // Try incrementing the share count.

            new = KeCompareSwapPtr (
                &lock^.Ptr, // ptr
                current + KEP_LOCK_SHARE_INC, // newvalue
                current, // expectedvalue
            )

            IF new == current THEN
                // Return the same oldipl we were passed.
                // This simplifies the implementation of the fast path wrappers
                // written in assembly language.

                RETURN oldipl
            END

            // Failed to CAS.

            current = new

            CONTINUE
        END

        // Make sure this isn't a recursive lock which is prohibited.

        KeAssert ( (current & KEP_LOCK_WRITE_LOCKED == 0) OR
            (current & KEP_LOCK_BITS != KeCurrentThread () ) )

        // If any of the control bits are set, one of the following is true:
        //
        // - The lock is held exclusively.
        // - There are exclusive waiters.
        //
        // In either case we need to sleep on the turnstile and then retry.

        KepBlockOnLock (
            lock, // lock
            current, // current
        )

        current = lock^.Ptr
    END
END

FN KepAcquireLockExclusive (
    IN lock : ^KeLock,
    IN oldipl : UWORD,
) : UWORD

    // Acquire a lock exclusively.
    // This is structured as a CAS loop.

    current := lock^.Ptr
    new : UWORD

    thread := KeCurrentThread ()

    WHILE TRUE DO
        IF current == 0 THEN
            // The lock is free. Try setting it owned with our thread pointer.

            new = KeCompareSwapPtr (
                &lock^.Ptr, // ptr
                thread | KEP_LOCK_WRITE_LOCKED, // newvalue
                current, // expectedvalue
            )

            IF new == current THEN
                // Return the same oldipl we were passed.
                // This simplifies the implementation of the fast path wrappers
                // written in assembly language.

                RETURN oldipl
            END

            // Failed to CAS.

            current = new

            CONTINUE
        END

        // Make sure this isn't a recursive lock which is prohibited.

        KeAssert ( (current & KEP_LOCK_WRITE_LOCKED == 0) OR
            (current & KEP_LOCK_BITS != KeCurrentThread () ) )

        // If the lock is nonzero, one of the following is true:
        //
        // - The lock is held exclusively.
        // - The lock is held shared.
        //
        // In either case we need to sleep on the turnstile and then retry.

        KepBlockOnLock (
            lock, // lock
            current, // current
        )

        current = lock^.Ptr
    END
END

FN KepReleaseLock (
    IN lock : ^KeLock,
)

    // Release a lock.
    // This is structured as a CAS loop.

    current := lock^.Ptr
    new : UWORD

    WHILE TRUE DO
        IF current & KEP_LOCK_WRITE_LOCKED THEN
            // Locked exclusively.

            KeAssert ( current & KEP_LOCK_BITS == KeCurrentThread () )
            
            // Try to zero out the lock.

            new = KeCompareSwapPtr (
                &lock^.Ptr, // ptr
                0, // newvalue
                current, // expectedvalue
            )

            IF new != current THEN
                // Failed to CAS.

                current = new

                CONTINUE
            END

            IF current & KEP_LOCK_WAITERS THEN
                // Wake the waiters.

                KepWakeLock (
                    lock, // lock
                    CAST current & KEP_LOCK_BITS TO ^KeuThread, // owner
                )
            END

            LEAVE
        END

        // Locked shared.

        KeAssert ( current & KEP_LOCK_BITS != 0 )

        IF current & KEP_LOCK_BITS == KEP_LOCK_SHARE_INC THEN
            // Only I have it locked shared. Try to zero out the lock.

            new = KeCompareSwapPtr (
                &lock^.Ptr, // ptr
                0, // newvalue
                current, // expectedvalue
            )

            IF new != current THEN
                // Failed to CAS.

                current = new

                CONTINUE
            END

            IF current & KEP_LOCK_WAITERS THEN
                // Wake the waiters.

                KepWakeLock (
                    lock, // lock
                    NULLPTR, // owner
                )
            END

            LEAVE
        END

        // Try to decrement the shared count.

        new = KeCompareSwapPtr (
            &lock^.Ptr, // ptr
            current - KEP_LOCK_SHARE_INC, // newvalue
            current, // expectedvalue
        )

        IF new == current THEN
            // Got it.

            LEAVE
        END

        current = new
    END
END

EXPORT FN KeConvertLockToShared (
    IN lock : ^KeLock,
)

    // Convert a lock from exclusive to shared.

    current := lock^.Ptr
    new : UWORD

    WHILE TRUE DO
        KeAssert ( current & KEP_LOCK_WRITE_LOCKED != 0 )
        KeAssert ( current & KEP_LOCK_BITS == KeCurrentThread () )

        // Try to set held shared with a single owner.

        new = KeCompareSwapPtr (
            &lock^.Ptr, // ptr
            KEP_LOCK_SHARE_INC, // newvalue
            current, // expectedvalue
        )

        IF new == current THEN
            // Got it.

            IF current & KEP_LOCK_WAITERS THEN
                // Wake the waiters.

                KepWakeLock (
                    lock, // lock
                    CAST current & KEP_LOCK_BITS TO ^KeuThread, // owner
                )
            END

            LEAVE
        END

        current = new
    END
END

EXPORT FN KeTryConvertLockToExclusive (
    IN lock : ^KeLock,
) : UWORD

    // Try to convert a lock from shared to exclusive. Return TRUE if upgraded,
    // FALSE if still shared.

    current := lock^.Ptr
    new : UWORD

    thread := KeCurrentThread ()

    WHILE TRUE DO
        IF current & KEP_LOCK_WRITE_LOCKED THEN
            // Already exclusive.

            KeAssert ( current & KEP_LOCK_BITS == thread )

            RETURN TRUE
        END

        IF current & KEP_LOCK_BITS != KEP_LOCK_SHARE_INC THEN
            // If there are other shared holders, we can't kick them out of the
            // lock, so return failure.

            RETURN FALSE
        END

        // Try to set exclusive, owned by me.

        new = KeCompareSwapPtr (
            &lock^.Ptr, // ptr
            KEP_LOCK_WRITE_LOCKED | thread, // newvalue
            current, // expectedvalue
        )

        IF new == current THEN
            // Got it.

            IF current & KEP_LOCK_WAITERS THEN
                // Wake the waiters, since they're now operating under the
                // incorrect assumption that the lock is held shared.

                KepWakeLock (
                    lock, // lock
                    NULLPTR, // owner
                )
            END

            RETURN TRUE
        END

        current = new
    END
END