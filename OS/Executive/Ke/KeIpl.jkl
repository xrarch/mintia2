//
// Implements support for software Interrupt Priority Levels (IPLs).
//

#INCLUDE "Ki.hjk"

#MACRO KiAcknowledgeSoftwareInterrupt ( prb, ipl ) [
    KeAssert ( ipl != KI_IPL_LOW )

#IF BLD_MP
    KeMaskUlong (
        &(prb)^.PendingSoftwareInterrupts, // ptr
        ~(KiPendingIpl ( ipl )), // mask
    )
#ELSE
    NOTHING (prb)^.PendingSoftwareInterrupts &= ~(KiPendingIpl ( ipl ))
#END
]

FNPTR KiSoftwareInterruptHandlerF ()

FN (KiSoftwareInterruptHandlerF) KiNoInterrupt ()

    // Called if no software interrupt is pending on entry to
    // KiDispatchSoftwareInterrupts.

    NOTHING
END

FN (KiSoftwareInterruptHandlerF) KiApcInterrupt ()

    // Called if an APC interrupt is pending.
    // Atomically clear the pending APC interrupt.

    prb := KI_CURRENT_PRB_LOCAL

    KiAcknowledgeSoftwareInterrupt (
        prb, // prb
        KI_IPL_APC, // ipl
    )

    current := prb^.CurrentThread

    prb^.Ipl = KI_IPL_APC

    KiEnableInterrupts ()

    KiDispatchKernelApcQueue ( current )

    KiDisableInterrupts ()
END

FN (KiSoftwareInterruptHandlerF) KiDpcInterrupt ()

    // Called if a DPC interrupt is pending.
    // Atomically clear the pending DPC interrupt.

    prb := KI_CURRENT_PRB

    KiAcknowledgeSoftwareInterrupt (
        prb, // prb
        KI_IPL_DPC, // ipl
    )

    prb^.Ipl = KI_IPL_DPC

    KiEnableInterrupts ()

    IF NOT RtlEmptyList ( &prb^.DpcListHead ) THEN
        KiDispatchDpcQueue ( prb )
    END

    IF prb^.CheckInteractivity THEN
        prb^.CheckInteractivity = FALSE

        KiCheckInteractivity ( prb )
    END

    IF prb^.QuantumEnd THEN
        prb^.QuantumEnd = FALSE

        KiQuantumEnd ( prb )
    END

#IF BLD_MP
    IF prb^.WakeStackSwapper THEN
        // A request was made to wake the stack swapper.
        // On MP we do this here because when we noticed we had to, we were deep
        // in some spinlocks. UP does not have this issue because DPC level is
        // DPC level, so we just signal the event at that time instead of here.

        prb^.WakeStackSwapper = FALSE

        KeSignalEvent (
            &KeStackSwapperEvent, // event
            0, // priorityboost
        )
    END
#END

    IF prb^.NextThread THEN
        // A next thread was selected to preempt the current one.
        // Switch to it immediately.

        KiPreemptThread ( prb )
    END

    KiDisableInterrupts ()
END

#SECTION "text"
KiSoftwareInterruptPriorityTable : KiSoftwareInterruptHandlerF[8] = {
    [0] = &KiNoInterrupt,
    [1] = &KiApcInterrupt,
    [2] = &KiDpcInterrupt,
    [3] = &KiDpcInterrupt,
}

FN KiDispatchSoftwareInterrupts (
    IN newipl : UWORD,
)

    // Called when a pending software interrupt is detected either while
    // lowering IPL, or before returning from an interrupt. We need to
    // loop dispatching software interrupts until no more unmasked ones are
    // pending.

    mask := 3 << newipl

    oldstate := KiDisableInterrupts ()

    // Note that there's a race where inbetween the caller noticing unmasked
    // pending soft ints, and disabling interrupts just now, an interrupt may
    // have come in that delivered them, meaning we'll observe a "zero" mask in
    // the pending soft ints mask. We don't check for this and assume it's an
    // uncommon case (although it does happen, observably). The zeroth entry
    // in the dispatch table is a no-op routine in order to catch this
    // harmlessly without wasting branch instructions in the hot path.

    prb := KI_CURRENT_PRB_LOCAL
    pending := prb^.PendingSoftwareInterrupts & mask

    WHILE TRUE DO
        KiSoftwareInterruptPriorityTable[pending] ()

#IF KI_VOLATILE_LOCAL_PRB
        // Recapture the Prb since it might have changed due to activity of the
        // scheduler while interrupts were enabled.

        prb = KI_CURRENT_PRB_LOCAL
#END

        pending = prb^.PendingSoftwareInterrupts & mask

        IF NOT pending THEN
            BREAK
        END
    END

    prb^.Ipl = newipl

    KiRestoreInterrupts ( oldstate )
END