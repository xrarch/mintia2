//
// Implements support for Balanced Queue kernel objects.
//
// These are queues into which items can be inserted and removed.
// There is integration with the dispatcher so that when a thread which is
// dealing with a balanced queue item blocks, another thread is awoken to take
// its place; likewise, when a thread attempts to grab a balanced queue item and
// the maximum number of threads are already running, it will block. This is to
// prevent the processors from being oversubscribed with worker threads.
//

#INCLUDE "Kep.hjk"

#IF BLD_MP

PUBLIC KepQueueRemovalLocks : KepSpinlock[KEP_QUEUE_REMOVAL_LOCKS]

#END

FN KepWakeBalancedQueue (
    IN queue : ^KeBalancedQueue,
)

    // A thread associated with this queue has disassociated from it, or is
    // going to sleep. See if we should wake up another thread. Queue removal
    // lock and object lock are held.

    KeAssert ( queue^.ActiveThreadCount != 0 )

    queue^.ActiveThreadCount -= 1

    IF queue^.ActiveThreadCount < queue^.MaximumThreadCount AND
        queue^.Header.WaiterCount AND
        NOT RtlEmptyList ( &queue^.ItemListHead ) THEN

        // There are more threads remaining, there are threads waiting, and
        // there's at least one item to process, so unblock someone and get
        // him going on it.

        // We wake them with a status of OS_STATUS_KERNEL_APC so that they
        // repeat the wait loop, which will hopefully cause them to pick up this
        // item. If someone else steals it, they'll adjust their timeout and go
        // back to sleep.

        KepSatisfyObject (
            &queue^.Header, // object
            0, // priorityboost
            FALSE, // all
            OS_STATUS_KERNEL_APC, // status
        )
    END
END

FN KepDisassociateFromBalancedQueue (
    IN thread : ^KeuThread,
)

    // Disassociate the thread from any balanced queue it is part of.
    // The thread must be the current thread.

    KeAssert ( KeCurrentThread () == thread )

    oldqueue := thread^.BalancedQueue

    KepComputeQueueRemovalLockAddress ( lock, oldqueue )

    ipl := KepAcquireQueueRemoval ( lock )

    // Since we acquired the queue removal lock, we might have gotten
    // removed from the old queue, so check that we're still associated.

    IF thread^.BalancedQueue THEN
        KepAcquireObjectElevated ( &oldqueue^.Header )

        RtlRemoveEntryList ( &thread^.BalancedQueueEntry )

        KepWakeBalancedQueue ( oldqueue )

        KepReleaseObjectElevated ( &oldqueue^.Header )

        thread^.BalancedQueue = NULLPTR
    END

    KepReleaseQueueRemoval ( lock, ipl )
END

EXPORT FN KeInitializeBalancedQueue (
    IN queue : ^KeBalancedQueue,
    IN name : ^UBYTE,
    IN maxthreads : UWORD,
)

    // Initialize a Balanced Queue object.
    // These have a dispatch header, but it should *never* be waited on
    // directly, only KeRemoveFromBalancedQueue should be used.

    KepInitializeDispatchObject (
        &queue^.Header, // header
        name, // name
        KEP_DISPATCH_BALANCED_QUEUE, // type
    )

    queue^.Header.SignalCount = 0

    RtlInitializeList ( &queue^.ItemListHead )
    RtlInitializeList ( &queue^.ThreadListHead )

    queue^.ActiveThreadCount = 0

    // If a maximum number of active threads was specified, use that. Otherwise,
    // default to the number of processors in the system.

    IF maxthreads THEN
        queue^.MaximumThreadCount = maxthreads

    ELSE
        queue^.MaximumThreadCount = KeuLoaderBlock.ProcessorCount
    END
END

EXPORT FN KeInsertIntoBalancedQueue (
    IN queue : ^KeBalancedQueue,
    IN item : ^RtlListEntry,
    IN head : UWORD,
    IN priorityboost : UWORD,
)

    // Insert an item into the balanced queue at either the head or the tail.

    // Acquire the object spinlock.

    ipl := KepAcquireObject ( &queue^.Header )

    IF queue^.Header.WaiterCount AND
        queue^.ActiveThreadCount < queue^.MaximumThreadCount THEN

        // Satisfy a wait. This may fail to wake anyone.
        // We wake them with a status of OS_STATUS_KERNEL_APC so that they
        // repeat the wait loop, which will hopefully cause them to pick up this
        // item. If someone else steals it, they'll adjust their timeout and go
        // back to sleep.

        KepSatisfyObject (
            &queue^.Header, // object
            priorityboost, // priorityboost
            FALSE, // all
            OS_STATUS_KERNEL_APC, // status
        )
    END

    // Insert the item into the queue.

    IF head THEN
        RtlInsertAtHeadList (
            &queue^.ItemListHead, // head
            item, // entry
        )

    ELSE
        RtlInsertAtTailList (
            &queue^.ItemListHead, // head
            item, // entry
        )
    END

    KepReleaseObject ( &queue^.Header, ipl )
END

EXPORT FN KeRemoveFromBalancedQueue (
    IN queue : ^KeBalancedQueue,
    IN waitmode : UWORD,
    IN alertable : UWORD,
    IN timeout : ^RtlUquad,
    OUT item : ^RtlListEntry,
) : OsStatus

    // Remove an item from the balanced queue. If there are too many threads, or
    // there are no items to remove, we block. 

    // Not allowed at >= KEP_IPL_APC because the QueueItem field of the thread
    // struct could get corrupted if this happens in an APC.

    KeAssert ( KepCurrentIpl () < KEP_IPL_APC )

    thread := KeCurrentThread ()

    ipl := KepRaiseIpl ( KEP_IPL_DPC )

    IF thread^.BalancedQueue != queue THEN
        // Associate the thread with this queue.
        // Note that this queue will not be ran down while we're trying to
        // remove an item, because the Executive is holding a reference to the
        // associated object.

        IF thread^.BalancedQueue THEN
            // Remove the thread from its current queue and insert it into this
            // queue. This may involve waking a waiter on the old queue.
            // The queue removal spinlock is superior to all the dispatcher
            // locks, so we can awaken threads and so on with it held.

            KepDisassociateFromBalancedQueue ( thread )
        END

        thread^.BalancedQueue = queue

        KepAcquireObjectElevated ( &queue^.Header )

        RtlInsertAtTailList (
            &queue^.ThreadListHead, // head
            &thread^.BalancedQueueEntry, // entry
        )

        queue^.ActiveThreadCount += 1

        KepReleaseObjectElevated ( &queue^.Header )
    END

    // The thread is now definitely associated with this queue, and won't become
    // disassociated from it for the duration of this operation as the owner of
    // the queue object has guaranteed its lifetime (for example with an object
    // manager reference).

    IF NOT RtlEmptyList ( &queue^.ItemListHead ) AND
        queue^.ActiveThreadCount <= queue^.MaximumThreadCount THEN

        // Make a quick check for an entry before trying to wait.

        KepAcquireObjectElevated ( &queue^.Header )

        IF NOT RtlEmptyList ( &queue^.ItemListHead ) AND
            queue^.ActiveThreadCount <= queue^.MaximumThreadCount THEN

            // Got one.

            item = queue^.ItemListHead.Next

            RtlRemoveEntryList ( item )

            KepReleaseObject ( &queue^.Header, ipl )

            RETURN OS_STATUS_SUCCESS
        END

        KepReleaseObjectElevated ( &queue^.Header )
    END

    KepLowerIpl ( ipl )

    status := KeWaitForSingleObject (
        waitmode, // waitmode
        alertable, // alertable
        timeout, // timeout
        &queue^.Header, // object
    )

    IF status == 0 THEN
        // Got an item.

        item = thread^.QueueItem

        status = OS_STATUS_SUCCESS
    END

    RETURN status
END

FN KeuRundownBalancedQueue (
    IN queue : ^KeBalancedQueue,
)

    // Uninitialize a balanced queue. Nobody can be waiting on it since that
    // would imply a reference, and as it is being deleted, there must be zero
    // references. We have to disassociate all threads from this queue.

    KepComputeQueueRemovalLockAddress ( lock, queue )

    ipl := KepAcquireQueueRemoval ( lock )

    KepAcquireObjectElevated ( &queue^.Header )

    head := &queue^.ThreadListHead
    listentry := head^.Next

    WHILE listentry != head DO
        thread := CONTAINEROF listentry TO KeuThread.BalancedQueueEntry

        // No need to remove the thread's list entry, just setting the queue
        // pointer to NULLPTR is sufficient.

        thread^.BalancedQueue = NULLPTR

        listentry = listentry^.Next
    END

    KepReleaseObjectElevated ( &queue^.Header )

    KepReleaseQueueRemoval ( lock, ipl )
END