//
// Initialization of the Kernel (Ke) subcomponent of the MINTIA Executive.
//

#INCLUDE "Kep.hjk"
#INCLUDE "<inc>/Psu.hjk"

PUBLIC KeuIdleProcess : PsuProcess

EXPORT KeuLoaderBlock : BlInfoRecord

PUBLIC KeuSystemPageDirectoryPfn : UWORD

FNPTR DbgMainF ()

EXPORT KeSharedUserPage : ^OsSharedUserPage

#IF BLD_CHK

EXPORT KepAssertionMessage : UBYTE[40]

#END

#IF BLD_MP

KepInitializeCount : ULONG

KepAnnounceSpinlock : KepSpinlock = KEP_INITIAL_SPINLOCK

PUBLIC KepWorkBalanceDpc : KeDpc

PUBLIC KepMaximumBalanceIterations : ULONG

#MACRO KepAcquireAnnounce () [
    KepAcquireSpinlockRaise ( &KepAnnounceSpinlock )
]

#MACRO KepReleaseAnnounce ( oldipl ) [
    KepReleaseSpinlockLower ( &KepAnnounceSpinlock, oldipl )
]

#ELSE

#MACRO KepAcquireAnnounce () [
    KepRaiseIpl ( KEP_IPL_DPC )
]

#MACRO KepReleaseAnnounce ( oldipl ) [
    KepLowerIpl ( oldipl )
]

#END

// FORWARD
EXTERN FN (KeStartThreadF) KepThreadedMain (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

FN (KeStartThreadF) KepIdleLoop (
    IN context1 : UWORD,
    IN context2 : UWORD,
)


    ipl := KepAcquireAnnounce ()

    RtlPrint ( "KepIdleLoop: Processor %u idling\n", KepCurrentProcessor () )

    KepReleaseAnnounce ( ipl )

#IF BLD_MP
    prb := KEP_CURRENT_PRB_LOCAL

    // Atomically decrement the initialization count to indicate to the boot
    // processor that we made it into our idle thread.

    KeIncrementUlong (
        &KepInitializeCount, // ptr
        0xFFFFFFFF, // ulong
    )
#END

    WHILE TRUE DO
        KepWaitForInterrupt ()

#IF BLD_MP
        IF prb^.StealWork THEN
            // Time to try stealing work.

            prb^.StealWork = FALSE

            KepStealWork ()
        END
#END
    END
END

#SECTION "INITtext"
FN KepInitializePrb (
    IN prb : ^KepPrb,
    IN bootprocessor : UWORD,
)

    // Initialize this Prb.

    // Loader stashed words in the Prb at the following indices:
    // 0 - Processor ID
    // 1 - Software IDT pointer
    // 2 - Idle stack pointer

    idword := CAST prb TO ^UWORD

    id := idword[0]
    irqdt := idword[1]
    stackbase := idword[2]

    // Clear the Prb.

    RtlFillMemoryWithUlong (
        prb, // ptr
        BL_PRB_SIZE, // sz
        0, // ulong
    )

    // Place the stashed words at their proper place.

    prb^.Id = id
    prb^.IrqDispatchTable = CAST irqdt TO ^VOID
    prb^.IdleStackBase = CAST stackbase TO ^VOID

    // On some architectures, the thing we get from KEP_CURRENT_PRB_LOCAL is not
    // a globally visible address for this Prb.

#IF KEP_VIRTUAL_PRB
    prb^.RealVirtual = prb
#END

    RtlInitializeList ( &prb^.DpcListHead )

    RtlInitializeList ( &prb^.StackSwapCandidateListHead )

#IF BLD_MP
    KepInitializeSpinlock ( &prb^.DpcQueueLock )
    KepInitializeSpinlock ( &prb^.ReadyQueueLock )
    KepInitializeSpinlock ( &prb^.TimerTreeLock )
    KepInitializeSpinlock ( &prb^.StackSwapListLock )

    prb^.StashedCurrentThreadInfo = KepSetIdleInStash ( 0, TRUE )
    prb^.IdleDeferredIpiEvents = 0
    prb^.InIdleLoop = TRUE
#END

    KeInitializeDpc (
        &prb^.TimerExpirationDpc, // dpc
        &KepTimerExpiration, // function
    )

#IF ( == BLD_BITS 64 )
    prb^.EarliestTimerMs.Quad = 0xFFFFFFFFFFFFFFFF

#ELSE
    prb^.EarliestTimerMs.Low = 0xFFFFFFFF
    prb^.EarliestTimerMs.High = 0xFFFFFFFF
#END

    prb^.Ipl = KEP_IPL_LOW

    // Initialize the IRQ dispatch table.

    i := 0

    WHILE i < HALU_VECTOR_MAX DO
        prb^.IrqDispatchTable[i] = &KepUnexpectedInterruptObject

        i += 1
    END

    // Initialize the real time scheduler queues.

    i = 0

    WHILE i < OS_PRIORITY_MAX DO
        RtlInitializeList ( &prb^.RealTimeListHeads[i] )

        i += 1
    END

    // Initialize the timeshared scheduler queues.

    i = 0

    WHILE i < KEP_TIMESHARED_QUEUES DO
        RtlInitializeList ( &prb^.CalendarListHeads[i] )

        i += 1
    END

    // Initialize the idle scheduler queue.

    RtlInitializeList ( &prb^.IdleListHead )

    // Initialize the idle thread.

    startfunc : KeStartThreadF

    IF bootprocessor THEN
        startfunc = &KepThreadedMain

    ELSE
        startfunc = &KepIdleLoop
    END

    KeuInitializeThread (
        &KeuIdleProcess.Pcb, // process
        &prb^.IdleThread, // thread
        "IdleThread", // name
        prb^.IdleStackBase, // kstack
        BL_IDLE_STACK_SIZE, // kstacksize
        KeuAllocateInitialTurnstile (), // turnstile
        startfunc, // startfunc
        0, // context1
        0, // context2
    )

    prb^.IdleThread.Priority = OS_PRIORITY_IDLE
    prb^.IdleThread.BasePriority = OS_PRIORITY_IDLE

#IF BLD_MP
    prb^.IdleThread.CurrentPrb = prb
#END

    // Initialize frozen context to zero so that the debugger doesn't try to
    // read it if we failed to freeze.

    prb^.FrozenContext = NULLPTR

    // Stash the Prb ID.

    KepStashPrbId ( prb, id )
END

#IF BLD_MP

#SECTION "INITtext"
FN (HaluStartProcessorF) KepStartOtherProcessor ()

    // This is the first code that runs in the context of a non-boot processor.
    // We assume any context required to access the Prb has been set up by the
    // Loader. We have to initialize ourselves and then jump into our idle
    // thread to wait for work.

    prb := KEP_CURRENT_PRB

    // Initialize architectural support.

    KepInitializeArchitecture ( prb )

    // Initialize HAL.

    HaluProcessorInitialize ( prb )

    // Become a subject of the dispatcher.

    KepJumpIntoIdleThread ( prb )
END

#END

EXTERN FN ExuStartExecutive ()

#SECTION "INITtext"
FN (KeStartThreadF) KepThreadedMain (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // This is the first code that runs in thread context during the lifetime of
    // the system. We need to perform phase 0 initialization of the Executive.

    ExuStartExecutive ()

    // Go into the idle loop.

    KepIdleLoop (
        context1, // context1
        context2, // context2
    )
END

#SECTION "INITtext"
FN KepFixupLoaderBlockLists ()

    // Fix up the circular lists in the loader block so that they still point to
    // the newly relocated list heads.

    listhead := &KeuLoaderBlock.ResourceListHead

    listhead^.Next^.Prev = listhead
    listhead^.Prev^.Next = listhead

    listhead = &KeuLoaderBlock.DllListHead

    listhead^.Next^.Prev = listhead
    listhead^.Prev^.Next = listhead

    listhead = &KeuLoaderBlock.DescriptorListHead

    listhead^.Next^.Prev = listhead
    listhead^.Prev^.Next = listhead
END

#SECTION "INITtext"
EXPORT FN KeMain (
    IN loaderblock : ^BlInfoRecord,
)

    // At this point:
    //
    // o  We have been transferred to from the Loader.
    // o  We are on a page-sized initial stack.
    // o  We have no thread context - we need to hand-craft one.
    // o  Interrupts are disabled. Paging is enabled.
    // o  The PFN database has been constructed by the Loader for us. Not much
    //    else has been.
    // o  The firmware - no matter what system we are on - is inaccessible, as a
    //    rule, even if we could theoretically use it on some platform. Only
    //    exception are firmware services that *must* be called at certain
    //    points in HAL initialization, and only then.
    //
    // Any pointers from the Loader may or may not be in a spot where they are
    // permanently accessible, since this may or may not be a platform where the
    // Loader is entered with paging enabled. If it wasn't, the Loader ran out
    // of an identity mapping in what is going to become userspace. Therefore
    // anything from loader-space that we need to access during runtime must be
    // be transferred into the Executive between now and the execution of the
    // first usermode process in order to guarantee its safe to touch. Plus, we
    // are going to reclaim all of the Loader's physical memory during Mm init.

#IF BLD_CHK
    // Set this up so that we don't waste space on repeated strings.

    RtlCopyString (
        &KepAssertionMessage[0], // dest
        "ASSERTION FAILED IN %s() AT %s:%d\n%s\n", // src
        40, // bufsize
    )
#END

    // Initialize the KeCrash module.

    KepInitializeCrashing ()

    // Start by copying the loader block into the Executive.

    RtlCopyBytes (
        &KeuLoaderBlock, // dest
        loaderblock, // src
        SIZEOF BlInfoRecord, // sz
    )

    // Fix up loader block lists.

    KepFixupLoaderBlockLists ()

    // Initialize the idle process.

    KeuInitializeProcess (
        &KeuIdleProcess.Pcb, // process
        "Idle", // name
    )

    KeuIdleProcess.Pcb.PageDirectoryPfn = KeuLoaderBlock.SystemPageDirectoryPfn

    // Initialize turnstiles.

    KepInitializeTurnstiles ()

    // Initialize the Prbs.

    prb := KepPrbFromNumber ( 0 )

    initprb := prb
    i := 0

    WHILE i < KeuLoaderBlock.ProcessorCount DO
        KepInitializePrb (
            initprb, // prb
            i == 0, // bootprocessor
        )

        i += 1
        initprb += BL_PRB_SIZE
    END

    // Initialize architectural support.

    KepInitializeArchitecture ( prb )

    // Perform early initialization of the HAL. This will perform baseline
    // initialization of integral platform components such as the interval
    // timer. Also, we get a boot console which is guaranteed to stay sane
    // throughout the lifetime of the system and at any IPL.

    HaluEarlyInitialize ()

    // Now that we have a console, announce ourselves.

    HaluCenterPrint ( "== MINTIA2 BOOT CONSOLE ==\n" )

    // Perform platform-specific initialization for the boot processor.

    KeAssert ( SIZEOF KepPrb <= BL_PRB_SIZE )

    HaluProcessorInitialize ( prb )

    // Initialize the shared user page.

    KeSharedUserPage = KeuLoaderBlock.VirtualSharedUserPage

    KepUpdateTime ( prb )

    // Export the page directory PFN.

    KeuSystemPageDirectoryPfn = KeuLoaderBlock.SystemPageDirectoryPfn

    // Perform early initialization of the debugger.

    IF KeuLoaderBlock.DbgMain THEN
        dbgmain := CAST KeuLoaderBlock.DbgMain TO DbgMainF

        dbgmain ()
    END

    // Initialize the stack swapper event. This is waited upon by a component
    // of Mm which outswaps kernel stacks and processes.

    KeInitializeEvent (
        &KeuStackSwapperEvent, // event
        "StackSwap", // name
        FALSE, // notification
        FALSE, // signalstate
    )

#IF BLD_MP
    // Initialize the work balancing DPC.

    KeInitializeDpc (
        &KepWorkBalanceDpc, // dpc
        &KepBalanceWork, // func
    )

    // Compute the maximum number of balance iterations as the number of
    // possible pairs between processors plus one. This is the same as the
    // number of possible imbalances plus one for good measure.
    //
    // This is calculated as n choose 2 which simplifies to n(n-1)/2 + 1.

    KepMaximumBalanceIterations = (KeuLoaderBlock.ProcessorCount *
        (KeuLoaderBlock.ProcessorCount - 1)) / 2 + 1

    IF KepMaximumBalanceIterations > 64 THEN
        KepMaximumBalanceIterations = 64
    END

    // We're ready to pull the other processors into the system.

    KepInitializeCount = KeuLoaderBlock.ProcessorCount - 1

    // Bring em in.

    HaluStartProcessors ( &KepStartOtherProcessor )

    // Wait for them to signal that they've been initialized.

    WHILE KepInitializeCount DO
        // Language BARRIER to stop this loop from being optimized out.

        BARRIER
    END
#END

    // Become a subject of the dispatcher.

    KepJumpIntoIdleThread ( prb )

    KeCrash ( "KepJumpIntoThread returned\n" )
END

#IF BLD_MP

EXPORT FN KeQueryProcessorCount () : UWORD

    // Query the number of processors in the system.

    RETURN KeuLoaderBlock.ProcessorCount
END

#END