//
// Initialization of the Kernel (Ke) subcomponent of the MINTIA Executive.
//

#INCLUDE "Ki.hjk"

PUBLIC KiIdleProcess : KeProcess

EXPORT ExLoaderBlock : BlInfoRecord

#SECTION "INIT$text"
FN KiFixupLoaderBlockLists ()

    // Fix up the circular lists in the loader block so that they still point to
    // the newly relocated list heads.

    listhead := &ExLoaderBlock.ResourceListHead

    listhead^.Next^.Prev = listhead
    listhead^.Prev^.Next = listhead

    listhead = &ExLoaderBlock.DllListHead

    listhead^.Next^.Prev = listhead
    listhead^.Prev^.Next = listhead

    listhead = &ExLoaderBlock.DescriptorListHead

    listhead^.Next^.Prev = listhead
    listhead^.Prev^.Next = listhead
END

#SECTION "INIT$text"
FN KiInitializePrb (
    IN prb : ^KiPrb,
)

    // Initialize this PRB. It has already been filled with zeroes by the Loader
    // so avoid doing that again for those fields where zeroes (and NULLPTR) are
    // the initial state.

    RtlInitializeList ( &prb^.DpcListHead )

#IF BLD_MP
    KiInitializeSpinlock ( &prb^.DpcQueueLock )
    KiInitializeSpinlock ( &prb^.ReadyQueueLock )
    KiInitializeSpinlock ( &prb^.TimerTreeLock )
#END

    KeInitializeDpc (
        &prb^.TimerExpirationDpc, // dpc
        &KiTimerExpiration, // function
    )

#IF ( == BLD_BITS 64 )
    prb^.EarliestTimerMs.Quad = 0xFFFFFFFFFFFFFFFF

#ELSE
    prb^.EarliestTimerMs.Low = 0xFFFFFFFF
    prb^.EarliestTimerMs.High = 0xFFFFFFFF
#END

    prb^.Ipl = KI_IPL_LOW

    // Initialize the IRQ dispatch table.

    i := 0

    WHILE i < HAL_VECTOR_MAX DO
        prb^.IrqDispatchTable[i] = &KiUnexpectedInterruptObject

        i += 1
    END

    // Initialize the real time scheduler queues.

    i = 0

    WHILE i < OS_PRIORITY_MAX DO
        RtlInitializeList ( &prb^.RealTimeListHeads[i] )

        i += 1
    END

    // Initialize the timeshared scheduler queues.

    i = 0

    WHILE i < OS_PRIORITY_LOW_REALTIME DO
        RtlInitializeList ( &prb^.CalendarListHeads[i] )

        i += 1
    END

    // Initialize the idle scheduler queue.

    RtlInitializeList ( &prb^.IdleListHead )
END

KiTestTimer : KeTimer
KiTestTimer2 : KeTimer
KiTestTimer3 : KeTimer
KiTestTimer4 : KeTimer
KiTestTimer5 : KeTimer
KiTestTimer6 : KeTimer

FN (KeDpcF) KiTestTimerDpc (
    IN dpc : ^KeDpc,
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    IF NOT context1 THEN
        RtlPrint ( "8000 " )

    ELSEIF context1 == 1 THEN
        RtlPrint ( "4000 " )

    ELSEIF context1 == 2 THEN
        RtlPrint ( "2000 " )
    
    ELSEIF context1 == 3 THEN
        RtlPrint ( "1000 " )

    ELSEIF context1 == 4 THEN
        RtlPrint ( "500  " )

    ELSEIF context1 == 5 THEN
        RtlPrint ( "250  " )
    END

    interval : RtlUquad

    interval.High = 0
    interval.Low = 8000

    KeEnqueueTimer (
        &KiTestTimer, // timer
        &interval, // interval
        0, // context1
        0, // context2
    )

    interval.Low = 4000

    KeEnqueueTimer (
        &KiTestTimer2, // timer
        &interval, // interval
        1, // context1
        0, // context2
    )

    interval.Low = 2000

    KeEnqueueTimer (
        &KiTestTimer3, // timer
        &interval, // interval
        2, // context1
        0, // context2
    )

    interval.Low = 1000

    KeEnqueueTimer (
        &KiTestTimer4, // timer
        &interval, // interval
        3, // context1
        0, // context2
    )

    interval.Low = 500

    KeEnqueueTimer (
        &KiTestTimer5, // timer
        &interval, // interval
        4, // context1
        0, // context2
    )

    interval.Low = 250

    KeEnqueueTimer (
        &KiTestTimer6, // timer
        &interval, // interval
        5, // context1
        0, // context2
    )

    KeDequeueTimer ( &KiTestTimer5 )

    KeDequeueTimer ( &KiTestTimer2 )

    KeDequeueTimer ( &KiTestTimer4 )
END

FNPTR DbgMainF ()

#IF BLD_MP

FN (KiIpiF) TestIpi (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    RtlPrint ( "Sup I'm an IPI! %d %d\n", context1, context2 )
END

#END

#SECTION "INIT$text"
EXPORT FN KeMain (
    IN loaderblock : ^BlInfoRecord,
)

    // At this point:
    //
    // o  We have been transferred to from the Loader.
    // o  We are on a page-sized initial stack.
    // o  We have no thread context - we need to hand-craft one.
    // o  Interrupts are disabled. Paging is enabled.
    // o  The PFN database has been constructed by the Loader for us. Not much
    //    else has been.
    // o  The firmware - no matter what system we are on - is inaccessible, as a
    //    rule, even if we could theoretically use it on some platform. Only
    //    exception are firmware services that *must* be called at certain
    //    points in HAL initialization, and only then.
    //
    // Any pointers from the Loader may or may not be in a spot where they are
    // permanently accessible, since this may or may not be a platform where the
    // Loader is entered with paging enabled. If it wasn't, the Loader ran out
    // of an identity mapping in what is going to become userspace. Therefore
    // anything from loader-space that we need to access during runtime must be
    // be transferred into the Executive between now and the execution of the
    // first usermode process in order to guarantee its safe to touch. Plus, we
    // are going to reclaim all of the Loader's physical memory during Mm init.

    // Initialize the KeCrash module.

    KiInitializeCrashing ()

    // Start by copying the loader block into the Executive.

    RtlCopyBytes (
        &ExLoaderBlock, // dest
        loaderblock, // src
        SIZEOF BlInfoRecord, // sz
    )

    // Fix up loader block lists.

    KiFixupLoaderBlockLists ()

    // Initialize the boot processor's PRB.

    prb := KI_CURRENT_PRB

    KiInitializePrb ( prb )

    // Initialize architectural support.

    KiInitializeArchitecture ( prb )

    // Perform early initialization of the HAL. This will perform baseline
    // initialization of integral platform components such as the interval
    // timer. Also, we get a boot console which is guaranteed to stay sane
    // throughout the lifetime of the system and at any IPL.

    HalEarlyInitialize ()

    // Perform early initialization of the debugger.

    IF ExLoaderBlock.DbgMain THEN
        dbgmain := CAST ExLoaderBlock.DbgMain TO DbgMainF

        dbgmain ()
    END

    // Announce ourselves.

    RtlPrint ( "KeMain(): MINTIA is awake!\n" )

    dpc : KeDpc
    dpc2 : KeDpc
    dpc3 : KeDpc
    dpc4 : KeDpc
    dpc5 : KeDpc
    dpc6 : KeDpc

    KeInitializeDpc (
        &dpc, // dpc
        &KiTestTimerDpc, // func
    )

    KeInitializeDpc (
        &dpc2, // dpc
        &KiTestTimerDpc, // func
    )

    KeInitializeDpc (
        &dpc3, // dpc
        &KiTestTimerDpc, // func
    )

    KeInitializeDpc (
        &dpc4, // dpc
        &KiTestTimerDpc, // func
    )

    KeInitializeDpc (
        &dpc5, // dpc
        &KiTestTimerDpc, // func
    )

    KeInitializeDpc (
        &dpc6, // dpc
        &KiTestTimerDpc, // func
    )

    KeInitializeTimer (
        &KiTestTimer, // timer
        &dpc, // dpc
        "TestTimer", // name
    )

    KeInitializeTimer (
        &KiTestTimer2, // timer
        &dpc2, // dpc
        "TestTimer2", // name
    )

    KeInitializeTimer (
        &KiTestTimer3, // timer
        &dpc3, // dpc
        "TestTimer3", // name
    )

    KeInitializeTimer (
        &KiTestTimer4, // timer
        &dpc4, // dpc
        "TestTimer4", // name
    )

    KeInitializeTimer (
        &KiTestTimer5, // timer
        &dpc5, // dpc
        "TestTimer5", // name
    )

    KeInitializeTimer (
        &KiTestTimer6, // timer
        &dpc6, // dpc
        "TestTimer6", // name
    )

    interval : RtlUquad

    interval.High = 0
    interval.Low = 1000

    KeEnqueueTimer (
        &KiTestTimer, // timer
        &interval, // interval
        0, // context1
        0, // context2
    )

#IF BLD_MP
    HalTriggerIpi ( KI_CURRENT_PRB )

    oldipl := KiRaiseIpl ( KI_IPL_DPC )

    KiExecuteOnOtherProcessor (
        KI_CURRENT_PRB, // target
        &TestIpi, // function
        10, // context1
        15, // context2
    )

    KiExecuteOnOtherProcessor (
        KI_CURRENT_PRB, // target
        &TestIpi, // function
        11, // context1
        15, // context2
    )

    KiExecuteOnOtherProcessor (
        KI_CURRENT_PRB, // target
        &TestIpi, // function
        13, // context1
        15, // context2
    )

    KiLowerIpl ( oldipl )

    RtlPrint ( "Cool I sent an IPI" )
#END

    WHILE TRUE DO
        KiWaitForInterrupt ()
    END
END