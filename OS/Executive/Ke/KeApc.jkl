//
// Implements support for APC objects.
//

#INCLUDE "Ki.hjk"

// APCs are functions that can be targeted to run in the context of a particular
// arbitrary thread.
//
// There are three kinds of APCs.
//
// Kernel APCs: These are dispatched whenever IPL is below KI_IPL_APC on a
//              processor. They can take page faults and can acquire pushlocks
//              that have been marked as KAPC-acquirable.
//
// Lazy APCs:   These are dispatched whenever a processor is executing in
//              usermode, upon return to usermode from the kernel, and whenever
//              the kernel takes a KE_USER_MODE wait. They can take page faults
//              and can acquire (almost) any pushlock.
//
// User APCs:   UAPCs are special as they cause code to be executed in usermode
//              instead of kernel mode. They are dispatched only when the kernel
//              takes a KE_USER_MODE wait, upon which time they cause an early
//              exit from the current system service with an OS_STATUS_USER_APC
//              return value. The thread then executes a callback in usermode
//              before returning to the caller of the system service.

#IF BLD_MP

#MACRO KiAcquireApcQueue ( thread ) [
    KiAcquireSpinlockRaise ( &(thread)^.ApcSpinlock )
]

#MACRO KiReleaseApcQueue ( thread, oldipl ) [
    KiReleaseSpinlockLower (
        &(thread)^.ApcSpinlock, // spinlock
        oldipl, // oldipl
    )
]

#MACRO KiAcquireApcQueueElevated ( thread ) [
    KiAcquireSpinlock ( &(thread)^.ApcSpinlock )
]

#MACRO KiReleaseApcQueueElevated ( thread ) [
    KiReleaseSpinlock ( &(thread)^.ApcSpinlock )
]

#ELSE

#MACRO KiAcquireApcQueue ( thread ) [
    KiRaiseIpl ( KI_IPL_DPC )
]

#MACRO KiReleaseApcQueue ( thread, oldipl ) [
    KiLowerIpl ( oldipl )
]

#MACRO KiAcquireApcQueueElevated ( thread ) []

#MACRO KiReleaseApcQueueElevated ( thread ) []

#END

EXPORT FN KeInitializeApc (
    IN apc : ^KeApc,
    IN func : KeApcF,
    IN type : UWORD,
    IN thread : ^KeThread,
)

    apc^.Function = func
    apc^.Type = type
    apc^.Thread = thread
    apc^.Enqueued = FALSE
END

EXPORT FN KeEnqueueKapc (
    IN apc : ^KeApc,
    IN priorityboost : UWORD,
) : UWORD

    // Enqueue the kernel APC to its thread.
    // Return TRUE if enqueued, FALSE otherwise.

#IF BLD_CHK
    IF apc^.Type != KE_APC_KERNEL THEN
        KeCrash ( "KeEnqueueKapc: wrong type %u\n", apc^.Type )
    END
#END

    thread := apc^.Thread

    // Acquire the APC queue.

    ipl := KiAcquireApcQueue ( thread )

    IF apc^.Enqueued THEN
        // Already enqueued.

        KiReleaseApcQueue ( thread, ipl )

        RETURN FALSE
    END

    // Add the APC to the list.

    RtlInsertAtTailList (
        &thread^.KapcListHead, // head
        &apc^.Entry, // entry
    )

    apc^.Enqueued = TRUE

    // Release APC queue but keep IPL elevated.

    KiReleaseApcQueueElevated ( thread )

    // Acquire the thread lock.

    KiAcquireThreadElevated ( thread )

    IF thread^.Status == KI_THREAD_RUNNING THEN
        // Need to trigger an APC interrupt.

        // Acquire a pointer to the current Prb.

        prb := KI_CURRENT_PRB

#IF BLD_MP
        IF thread^.WaitAttempt == KI_THREAD_WAIT_TRY AND
            thread^.WaitIpl == KI_IPL_LOW THEN

            // Abort the wait attempt.

            KiUnwaitThread (
                thread, // thread,
                OS_STATUS_KERNEL_APC, // status
                priorityboost, // priorityboost
            )
        END

        IF thread^.CurrentPrb != prb THEN
            // Trigger on the target processor.

            KiSoftInterruptOther (
                thread^.CurrentPrb, // targetprb
                KI_IPL_APC, // ipl
            )

        ELSE
#END
            // Trigger on the current processor.

            KiSoftInterruptSelf (
                prb, // targetprb
                KI_IPL_APC, // ipl
            )
#IF BLD_MP
        END
#END

    ELSEIF thread^.Status == KI_THREAD_WAITING AND
        thread^.WaitIpl == KI_IPL_LOW THEN

        // Wake the thread.

        KiUnwaitThread (
            thread, // thread,
            OS_STATUS_KERNEL_APC, // status
            priorityboost, // priorityboost
        )
    END

    KiReleaseThread ( thread, ipl )

    RETURN TRUE
END

EXPORT FN KeEnqueueUapc (
    IN apc : ^KeApc,
    IN priorityboost : UWORD,
) : UWORD

    // Enqueue the user APC to its thread.
    // Return TRUE if enqueued, FALSE otherwise.

#IF BLD_CHK
    IF apc^.Type != KE_APC_USER THEN
        KeCrash ( "KeEnqueueUapc: wrong type %u\n", apc^.Type )
    END
#END

    thread := apc^.Thread

    // Acquire the APC queue.

    ipl := KiAcquireApcQueue ( thread )

    IF apc^.Enqueued THEN
        // Already enqueued.

        KiReleaseApcQueue ( thread, ipl )

        RETURN FALSE
    END

    // Add the APC to the list.

    RtlInsertAtTailList (
        &thread^.UapcListHead, // head
        &apc^.Entry, // entry
    )

    apc^.Enqueued = TRUE

    // Release APC queue but keep IPL elevated.

    KiReleaseApcQueueElevated ( thread )

    // Acquire the thread lock.

    KiAcquireThreadElevated ( thread )

    IF (thread^.Status == KI_THREAD_WAITING
#IF BLD_MP
        OR (thread^.Status == KI_THREAD_RUNNING AND
        thread^.WaitAttempt == KI_THREAD_WAIT_TRY)
#END
        ) AND
        thread^.WaitIpl == KI_IPL_LOW AND
        thread^.WaitMode == KE_USER_MODE AND
        thread^.Alertable THEN

        // The thread was in a usermode alertable wait, so trigger user APC
        // delivery.

        thread^.UserApcTriggered = TRUE
        thread^.UserInterrupt = TRUE

        // Wake the thread.

        KiUnwaitThread (
            thread, // thread,
            OS_STATUS_USER_APC, // status
            priorityboost, // priorityboost
        )
    END

    KiReleaseThread ( thread, ipl )

    RETURN TRUE
END

EXPORT FN KeEnqueueLapc (
    IN apc : ^KeApc,
    IN priorityboost : UWORD,
) : UWORD

    // Enqueue the lazy APC to its thread.
    // Return TRUE if enqueued, FALSE otherwise.

#IF BLD_CHK
    IF apc^.Type != KE_APC_LAZY THEN
        KeCrash ( "KeEnqueueLapc: wrong type %u\n", apc^.Type )
    END
#END

    thread := apc^.Thread

    // Acquire the APC queue.

    ipl := KiAcquireApcQueue ( thread )

    IF apc^.Enqueued THEN
        // Already enqueued.

        KiReleaseApcQueue ( thread, ipl )

        RETURN FALSE
    END

    // Add the APC to the list.

    RtlInsertAtTailList (
        &thread^.LapcListHead, // head
        &apc^.Entry, // entry
    )

    apc^.Enqueued = TRUE

    // Release APC queue but keep IPL elevated.

    KiReleaseApcQueueElevated ( thread )

    // Acquire the thread lock.

    KiAcquireThreadElevated ( thread )

    IF (thread^.Status == KI_THREAD_WAITING
#IF BLD_MP
        OR (thread^.Status == KI_THREAD_RUNNING AND
        thread^.WaitAttempt == KI_THREAD_WAIT_TRY)
#END
        ) AND
        thread^.WaitIpl == KI_IPL_LOW AND
        thread^.WaitMode == KE_USER_MODE THEN

        // The thread was in a usermode wait, so trigger lazy APC delivery.

        KiUnwaitThread (
            thread, // thread,
            OS_STATUS_KERNEL_APC, // status
            priorityboost, // priorityboost
        )

#IF BLD_MP
    ELSEIF thread^.Status == KI_THREAD_RUNNING AND
        thread^.CurrentMode == KE_USER_MODE THEN

        // The thread is running in usermode on another processor. Cause an IPI
        // to get him to check his lazy APC queue.

        thread^.UserInterrupt = TRUE

        HalTriggerIpi ( thread^.CurrentPrb )
#END
    END

    KiReleaseThread ( thread, ipl )

    RETURN TRUE
END

EXPORT FN KeDequeueApc (
    IN apc : ^KeApc,
) : UWORD

    // Dequeue an APC of any type and return whether it was dequeued or not.

    thread := apc^.Thread

    // Acquire the APC queue.

    ipl := KiAcquireApcQueue ( thread )

    enqueued := apc^.Enqueued

    IF enqueued THEN
        // Remove from the appropriate list.

        RtlRemoveEntryList ( &apc^.Entry )

        apc^.Enqueued = FALSE
    END

    // Release APC queue.

    KiReleaseApcQueue ( thread, ipl )

    RETURN enqueued
END

FN KiDispatchKernelModeApcs (
    IN thread : ^KeThread,
    IN head : ^RtlListEntry,
)

    // Dispatch the kernel mode APCs on the given list.

    // Pop items from the list until it's emptied.

    WHILE TRUE DO
        ipl := KiAcquireApcQueue ( thread )

        listentry := head^.Next

        IF listentry == head THEN
            // Queue's empty.

            KiReleaseApcQueue ( thread, ipl )

            BREAK
        END

        apc := CONTAINEROF head^.Next TO KeApc.Entry

        // Remove from the list.

        RtlRemoveEntryList ( &apc^.Entry )

        // Capture the context before unlocking the queue, or the APC could get
        // re-enqueued again from an interrupt or another processor, destroying
        // the context.

        context1 := apc^.Context1
        context2 := apc^.Context2

        // Set dequeued.

        apc^.Enqueued = FALSE

        // Unlock the queue.

        KiReleaseApcQueue ( thread, ipl )

        // Call the APC function.

        apc^.Function (
            apc, // apc
            context1, // context1
            context2, // context2
        )
    END
END

FN KiDispatchKernelApcQueue (
    IN current : ^KeThread,
)

    // Here we have to dispatch kernel APCs. We are in the context of the
    // current thread at KI_IPL_APC.

#IF BLD_CHK
    IF KiCurrentIpl () != KI_IPL_APC THEN
        KeCrash ( "KiDispatchKernelApcQueue: not KI_IPL_APC\n" )
    END
#END

    KiDispatchKernelModeApcs (
        current, // thread
        &current^.KapcListHead, // head
    )
END

FN KiDispatchLazyApcQueue (
    IN current : ^KeThread,
)

    // Dispatch the lazy APC queue. This is called upon an attempt to perform a
    // KE_USER_MODE wait, and upon a return to userspace with pending lazy APCs.
    // We are in the context of the current thread at KI_IPL_LOW.

#IF BLD_CHK
    IF KiCurrentIpl () != KI_IPL_LOW THEN
        KeCrash ( "KiDispatchLazyApcQueue: not KI_IPL_LOW\n" )
    END
#END

    KiDispatchKernelModeApcs (
        current, // thread
        &current^.LapcListHead, // head
    )
END

FN KiDispatchUserApcQueue (
    IN current : ^KeThread,
    IN dispatchfunc : ^VOID,
    IN trapframe : ^VOID,
)

    // Dispatch the user APC queue. This is called upon return to usermode after
    // an attempt to perform an alertable KE_USER_MODE wait. We are in the
    // context of the current thread at KI_IPL_LOW.

    ipl := KiAcquireApcQueue ( current )

    IF RtlEmptyList ( &current^.UapcListHead ) THEN
        // None to dispatch.

        KiReleaseApcQueue ( current, ipl )

        LEAVE
    END

    listentry := current^.UapcListHead.Next

    // Remove the user APC from the queue.

    RtlRemoveEntryList ( listentry )

    apc := CONTAINEROF listentry TO KeApc.Entry

    // Capture the context before unlocking the queue, or the APC could get
    // re-enqueued again from an interrupt or another processor, destroying
    // the context.

    context1 := apc^.Context1
    context2 := apc^.Context2

    // Set dequeued.

    apc^.Enqueued = FALSE

    // Unlock the queue.

    KiReleaseApcQueue ( current, ipl )

    // Call the kernel function. It will probably free the APC structure, so we
    // can't touch it after this.

    apc^.Function (
        apc, // apc
        context1, // context1
        context2, // context2
    )

    // Dispatch the APC in usermode.

    KiInterruptUsermode (
        dispatchfunc, // dispatchfunc
        trapframe, // trapframe
        context1, // userfunc
        context2, // context
    )
END