//
// Implements support for APC objects.
//

#INCLUDE "Ki.hjk"

// APCs are functions that can be targeted to run in the context of a particular
// arbitrary thread.
//
// There are three kinds of APCs.
//
// Kernel APCs: KAPCs are dispatched whenever IPL is below KI_IPL_APC for the
//              thread. They can take page faults and can acquire locks that
//              are designed to be KAPC-acquirable (i.e. they're only ever taken
//              with the KeAcquireApcLock family of routines, which
//              automatically raise and lower IPL appropriately).
//
// Lazy APCs:   LAPCs are dispatched whenever a thread is executing in usermode,
//              upon return to usermode from the kernel, and whenever the thread
//              takes a KE_USER_MODE wait. They can take page faults and can
//              acquire (almost) any lock, since a KE_USER_MODE waiter is
//              guaranteed not to be holding the vast majority of internal locks
//              at wait time.
//
// User APCs:   UAPCs are special as they cause code to be executed in usermode
//              instead of kernel mode. They are dispatched only when the kernel
//              takes a KE_USER_MODE wait, upon which time they cause an early
//              exit from the current system service with an OS_STATUS_USER_APC
//              return value. The thread then executes a callback in usermode
//              before returning to the caller of the system service.

EXPORT FN KeInitializeApc (
    IN apc : ^KeApc,
    IN func : KeApcF,
    IN type : UWORD,
    IN thread : ^KeThread,
)

    apc^.Function = func
    apc^.Type = type
    apc^.Thread = thread
    apc^.Enqueued = FALSE
END

EXPORT FN KeEnqueueKapc (
    IN apc : ^KeApc,
    IN priorityboost : UWORD,
    IN context1 : UWORD,
    IN context2 : UWORD,
) : UWORD

    // Enqueue the kernel APC to its thread.
    // Return TRUE if enqueued, FALSE otherwise.

    KeAssert ( apc^.Type == KE_APC_KERNEL )

    thread := apc^.Thread

    // Acquire the thread.

    ipl := KiAcquireThread ( thread )

    IF apc^.Enqueued THEN
        // Already enqueued.

        KiReleaseThread ( thread, ipl )

        RETURN FALSE
    END

    // Add the APC to the list.

    RtlInsertAtTailList (
        &thread^.KapcListHead, // head
        &apc^.Entry, // entry
    )

    thread^.PendingEventsSet |= KI_THREAD_PENDING_KAPC

    apc^.Enqueued = TRUE

    apc^.Context1 = context1
    apc^.Context2 = context2

    IF thread^.Status == KI_THREAD_RUNNING THEN
        // Need to trigger an APC interrupt.

        // Acquire a pointer to the current Prb.

        prb := KI_CURRENT_PRB

#IF BLD_MP
        IF thread^.WaitAttempt == KI_THREAD_WAIT_TRY AND
            (thread^.WaitIpl == KI_IPL_LOW OR
            thread^.Alertable == KE_CANCEL_ON_KAPC) THEN

            // Abort the wait attempt.

            KiUnwaitThread (
                thread, // thread,
                OS_STATUS_KERNEL_APC, // status
                priorityboost, // priorityboost
            )
        END

        IF thread^.CurrentPrb != prb THEN
            // Trigger on the target processor.

            KiSoftInterruptOther (
                thread^.CurrentPrb, // targetprb
                KI_IPL_APC, // ipl
            )

        ELSE
#END
            // Trigger on the current processor.

            KiSoftInterruptSelf (
                prb, // targetprb
                KI_IPL_APC, // ipl
            )
#IF BLD_MP
        END
#END

    ELSEIF thread^.Status == KI_THREAD_WAITING AND
        (thread^.WaitIpl == KI_IPL_LOW OR
        thread^.Alertable == KE_CANCEL_ON_KAPC) THEN

        // Wake the thread.

        KiUnwaitThread (
            thread, // thread,
            OS_STATUS_KERNEL_APC, // status
            priorityboost, // priorityboost
        )
    END

    KiReleaseThread ( thread, ipl )

    RETURN TRUE
END

EXPORT FN KeEnqueueUapc (
    IN apc : ^KeApc,
    IN priorityboost : UWORD,
    IN context1 : UWORD,
    IN context2 : UWORD,
) : UWORD

    // Enqueue the user APC to its thread.
    // Return TRUE if enqueued, FALSE otherwise.

    thread := apc^.Thread

    KeAssert ( apc^.Type == KE_APC_USER )

    // Acquire the thread.

    ipl := KiAcquireThread ( thread )

    IF apc^.Enqueued THEN
        // Already enqueued.

        KiReleaseThread ( thread, ipl )

        RETURN FALSE
    END

    // Add the APC to the list.

    RtlInsertAtTailList (
        &thread^.UapcListHead, // head
        &apc^.Entry, // entry
    )

    thread^.PendingEventsSet |= KI_THREAD_PENDING_UAPC

    apc^.Enqueued = TRUE

    apc^.Context1 = context1
    apc^.Context2 = context2

    IF (thread^.Status == KI_THREAD_WAITING
#IF BLD_MP
        OR (thread^.Status == KI_THREAD_RUNNING AND
        thread^.WaitAttempt == KI_THREAD_WAIT_TRY)
#END
        ) AND thread^.WaitIpl == KI_IPL_LOW AND
        thread^.WaitMode == KE_USER_MODE AND
        thread^.Alertable == KE_ALERTABLE THEN

        // The thread was in a usermode alertable wait, so trigger user APC
        // delivery.

        thread^.UserApcTriggered = TRUE
        thread^.UserInterrupt = TRUE

        // Wake the thread.

        KiUnwaitThread (
            thread, // thread,
            OS_STATUS_USER_APC, // status
            priorityboost, // priorityboost
        )
    END

    KiReleaseThread ( thread, ipl )

    RETURN TRUE
END

EXPORT FN KeEnqueueLapc (
    IN apc : ^KeApc,
    IN priorityboost : UWORD,
    IN context1 : UWORD,
    IN context2 : UWORD,
) : UWORD

    // Enqueue the lazy APC to its thread.
    // Return TRUE if enqueued, FALSE otherwise.

    KeAssert ( apc^.Type == KE_APC_LAZY )

    thread := apc^.Thread

    // Acquire the thread.

    ipl := KiAcquireThread ( thread )

    IF apc^.Enqueued THEN
        // Already enqueued.

        KiReleaseThread ( thread, ipl )

        RETURN FALSE
    END

    // Add the APC to the list.

    RtlInsertAtTailList (
        &thread^.LapcListHead, // head
        &apc^.Entry, // entry
    )

    thread^.PendingEventsSet |= KI_THREAD_PENDING_LAPC

    apc^.Enqueued = TRUE

    apc^.Context1 = context1
    apc^.Context2 = context2

    // Set user interrupt pending to make sure the thread checks his lazy APC
    // queue on the way back out to usermode.

    thread^.UserInterrupt = TRUE

    IF (thread^.Status == KI_THREAD_WAITING
#IF BLD_MP
        OR (thread^.Status == KI_THREAD_RUNNING AND
        thread^.WaitAttempt == KI_THREAD_WAIT_TRY)
#END
        ) AND thread^.WaitIpl == KI_IPL_LOW AND
        thread^.WaitMode == KE_USER_MODE THEN

        // The thread was in a usermode wait, so wake him up with a kernel APC
        // status so that he tries to wait again and dispatches his lazy APC
        // queue at that time.

        KiUnwaitThread (
            thread, // thread,
            OS_STATUS_KERNEL_APC, // status
            priorityboost, // priorityboost
        )

#IF BLD_MP
    ELSEIF thread^.Status == KI_THREAD_RUNNING AND
        thread^.CurrentMode == KE_USER_MODE THEN

        KeMemoryBarrier ()

        // The thread is running in usermode on another processor. Cause an IPI
        // to get him to check his lazy APC queue.

        HalTriggerIpi (
            thread^.CurrentPrb, // target
            KI_IPI_ASYNCHRONOUS, // type
        )
#END
    END

    KiReleaseThread ( thread, ipl )

    RETURN TRUE
END

EXPORT FN KeDequeueApc (
    IN apc : ^KeApc,
) : UWORD

    // Dequeue an APC of any type and return whether it was dequeued or not.

    thread := apc^.Thread

    // Acquire the APC queue.

    ipl := KiAcquireThread ( thread )

    enqueued := apc^.Enqueued

    IF enqueued THEN
        // Remove from the appropriate list.

        RtlRemoveEntryList ( &apc^.Entry )

        apc^.Enqueued = FALSE
    END

    // Release APC queue.

    KiReleaseThread ( thread, ipl )

    RETURN enqueued
END

FN KiDispatchKernelModeApcs (
    IN thread : ^KeThread,
    IN head : ^RtlListEntry,
    IN pendingbit : UWORD,
)

    // Dispatch the kernel mode APCs on the given list.

    // Pop items from the list until it's emptied.

    WHILE TRUE DO
        ipl := KiAcquireThread ( thread )

        listentry := head^.Next

        IF listentry == head THEN
            // Queue's empty.

            thread^.PendingEventsSet &= ~pendingbit

            KiReleaseThread ( thread, ipl )

            BREAK
        END

        apc := CONTAINEROF listentry TO KeApc.Entry

        // Remove from the list.

        RtlRemoveEntryList ( listentry )

        // Capture the context before unlocking the queue, or the APC could get
        // re-enqueued again from an interrupt or another processor, destroying
        // the context.

        context1 := apc^.Context1
        context2 := apc^.Context2

        // Set dequeued.

        apc^.Enqueued = FALSE

        // Unlock the queue.

        KiReleaseThread ( thread, ipl )

        // Call the APC function.

        apc^.Function (
            apc, // apc
            context1, // context1
            context2, // context2
            FALSE, // rundown
        )
    END
END

FN KiDispatchKernelApcQueue (
    IN current : ^KeThread,
)

    // Here we have to dispatch kernel APCs. We are in the context of the
    // current thread at KI_IPL_APC.

    KeAssert ( KiCurrentIpl () == KI_IPL_APC )

    // Dispatch the APCs.

    current^.ExecutingKapc = TRUE

    KiDispatchKernelModeApcs (
        current, // thread
        &current^.KapcListHead, // head
        KI_THREAD_PENDING_KAPC, // pendingbit
    )

    current^.ExecutingKapc = FALSE
END

FN KiDispatchLazyApcQueue (
    IN current : ^KeThread,
)

    // Dispatch the lazy APC queue. This is called upon an attempt to perform a
    // KE_USER_MODE wait, and upon a return to userspace with pending lazy APCs.
    // We are in the context of the current thread at KI_IPL_LOW.

    KeAssert ( KiCurrentIpl () == KI_IPL_LOW )

    KiDispatchKernelModeApcs (
        current, // thread
        &current^.LapcListHead, // head
        KI_THREAD_PENDING_LAPC, // pendingbit
    )
END

FN KiDispatchUserApcQueue (
    IN current : ^KeThread,
)

    // Dispatch the user APC queue. This is called upon return to usermode after
    // an attempt to perform an alertable KE_USER_MODE wait. We are in the
    // context of the current thread at KI_IPL_LOW.

    ipl := KiAcquireThread ( current )

    IF RtlEmptyList ( &current^.UapcListHead ) THEN
        // None to dispatch.

        current^.PendingEventsSet &= ~KI_THREAD_PENDING_UAPC

        KiReleaseThread ( current, ipl )

        LEAVE
    END

    listentry := current^.UapcListHead.Next

    // Remove the user APC from the queue.

    RtlRemoveEntryList ( listentry )

    apc := CONTAINEROF listentry TO KeApc.Entry

    // Capture the context before unlocking the queue, or the APC could get
    // re-enqueued again from an interrupt or another processor, destroying
    // the context.

    context1 := apc^.Context1
    context2 := apc^.Context2

    // Set dequeued.

    apc^.Enqueued = FALSE

    // Unlock the queue.

    KiReleaseThread ( current, ipl )

    // Call the kernel function. It will probably free the APC structure, so we
    // can't touch it after this.

    apc^.Function (
        apc, // apc
        context1, // context1
        context2, // context2
        FALSE, // rundown
    )

    // Dispatch the APC in usermode.

    KiInterruptUsermode (
        KeUexecApcDispatcher, // dispatchfunc
        current^.UserFrame, // trapframe
        context1, // userfunc
        context2, // context
        current, // thread
    )
END

FN KeAreApcsDeliverable () : UWORD

    // Return whether APCs are deliverable.

    RETURN KiCurrentIpl () == KI_IPL_LOW
END