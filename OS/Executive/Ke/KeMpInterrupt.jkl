//
// Implements support for multiprocessor communication (IPIs).
//

#IF BLD_MP

#INCLUDE "Kep.hjk"

KepFreezeCount : ULONG = 0
KepFrozenProcessorCount : ULONG = 0

KepSendTable : ^KepPrb[8]
KepSendTableSet : ULONG = 0

#SECTION "text"
PUBLIC KepIndexFirstBitClear : UBYTE[256] = {
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 4,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 5,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 4,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 6,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 4,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 5,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 4,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 7,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 4,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 5,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 4,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 6,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 4,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 5,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 4,
    0, 1, 0, 2, 0, 1, 0, 3, 0, 1, 0, 2, 0, 1, 0, 8,
}

#DEFINE KEP_FREEZE_SPIN 5000000
#DEFINE KEP_FREEZE_RETRY 3

EXPORT FN KepFreezeOtherProcessors ()

    // Freeze all other processors in the system in place.
    // Assumes interrupts are disabled on this processor and that they will
    // remain disabled until freezing is over (otherwise we might take an IPI
    // that makes us freeze too).
    //
    // This will jam up spinlocks and mutexes at random so this is primarily for
    // debugging and crash time and not for willy nilly usage.

    // First increment the global freeze count.

    oldcount := KeIncrementUlong (
        &KepFreezeCount, // ptr
        1, // ulong
    )

    IF oldcount THEN
        // Already frozen.

        LEAVE
    END

    KepFrozenProcessorCount = 0

    // Now send IPIs to all of the processors in the system (other than me).

    myprb := KEP_CURRENT_PRB

    j := 0

    WHILE j < KEP_FREEZE_RETRY DO
        prb := CAST KeuLoaderBlock.VirtualPrbBase TO ^KepPrb

        HaluBroadcastIpi (
            myprb, // target
            KEP_IPI_SYNCHRONOUS, // type
        )

        // Now wait for everyone except for me to freeze.

        expected := KeuLoaderBlock.ProcessorCount - 1
        spincount := KEP_FREEZE_SPIN

        WHILE KepFrozenProcessorCount != expected AND spincount DO
            // Language BARRIER so this loop isn't optimized out.

            BARRIER

            KeSpinPause ()

            spincount -= 1
        END

        IF NOT spincount THEN
            // We ran out of spins. Try IPI'ing everyone again.

            j += 1

            CONTINUE
        END

        BREAK
    END

    IF j == KEP_FREEZE_RETRY THEN
        // We failed to freeze everyone but we're going to just print a warning
        // and proceed anyway. Someone's probably frozen somewhere anyway and
        // can't hurt us, hopefully.

        RtlPrint ( "KepFreezeOtherProcessors: FAILED! Timeout.\n" )
    END
END

EXPORT FN KepUnfreezeOtherProcessors ()

    // Unfreeze the processors in the system.

    old := KeIncrementUlong (
        &KepFreezeCount, // ptr
        0xFFFFFFFF, // ulong
    )

    IF old == 1 THEN
        // It's zero now, they've been released. Wait for them.

        WHILE KepFrozenProcessorCount DO
            // Language BARRIER so this loop isn't optimized out.

            BARRIER

            KeSpinPause ()
        END
    END
END

FN KepExecuteOnOtherProcessor (
    IN target : ^KepPrb,
    IN function : KepIpiF,
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // Execute the specified function on another processor.
    // Actually this works even for self-IPIs but hopefully we aren't taking any
    // of those because that is not very efficient.

    KeAssert ( KepCurrentIpl () == KEP_IPL_DPC )

    prb := KEP_CURRENT_PRB

    // Initialize our IPI context.

    prb^.IpiFunction = function
    prb^.IpiContext1 = context1
    prb^.IpiContext2 = context2

    prb^.IpiSpin = 1

@Retry

    // Acquire a sender table entry. The allocation state is described by a
    // set of 8 bits, the KepSendTableSet. This lets us be O(1) and not take any
    // locks here. Since there are 8, thats how many IPIs can occur in parallel
    // before someone has to spin here.

    mask := KepSendTableSet

    WHILE mask == 0xFF DO
        // 0xFF means theres no clear bits; no free table slots. Spin-wait for
        // a free table slot.
        
        // Use a language BARRIER to stop this loop from being optimized out.

        BARRIER

        KeSpinPause ()

        mask = KepSendTableSet
    END

    // There's at least one bit free in the set, so find the index of the lowest
    // one with the lookup table.

    index := KepIndexFirstBitClear[mask]

    // Set the bit to allocate the table entry.

    newmask := mask | (1 << index)

    // Try to atomic CAS the new bit set.

    IF KeCompareSwapUlong (
        &KepSendTableSet, // ptr
        newmask, // newvalue
        mask, // expectedvalue
    ) != mask THEN

        // Someone else modified it inbetween (rare case). Retry.

        GOTO Retry
    END

    // We've successfully allocated a table entry for our IPI.
    
    // Note that the following operations MUST be done in this order.
    // 1. Store our Prb in the table.

    KepSendTable[index] = prb

    // 2. Mark pending.

    KeOrUlong (
        &target^.IpiPendingSet, // ptr
        1 << index, // bitset
    )

    // 3. Trigger the IPI.

    HaluTriggerIpi (
        target, // target
        KEP_IPI_SYNCHRONOUS, // type
    )

    // 4. Spin-wait for execution of our callback.

    WHILE prb^.IpiSpin DO
        // Do a language BARRIER so that this loop isn't optimized out.

        BARRIER

        KeSpinPause ()
    END

    // 5. Now release the sender table entry.

    KeMaskUlong (
        &KepSendTableSet, // ptr
        ~(1 << index), // mask
    )
END

FN KepExecuteOnEveryoneElse (
    IN function : KepIpiF,
    IN context1 : UWORD,
    IN context2 : UWORD,
    IN deferred : UWORD,
)

    // Execute the specified function on every other processor.

    KeAssert ( KepCurrentIpl () == KEP_IPL_DPC )

    count := KeuLoaderBlock.ProcessorCount

    prb := KEP_CURRENT_PRB

    // Initialize our IPI context.

    prb^.IpiFunction = function
    prb^.IpiContext1 = context1
    prb^.IpiContext2 = context2

    prb^.IpiSpin = count - 1

@Retry

    // Acquire a sender table entry. The allocation state is described by a
    // set of 8 bits, the KepSendTableSet. This lets us be O(1) and not take any
    // locks here. Since there are 8, thats how many IPIs can occur in parallel
    // before someone has to spin here.

    mask := KepSendTableSet

    WHILE mask == 0xFF DO
        // 0xFF means theres no clear bits; no free table slots. Spin-wait for
        // a free table slot.
        
        // Use a language BARRIER to stop this loop from being optimized out.

        BARRIER

        KeSpinPause ()

        mask = KepSendTableSet
    END

    // There's at least one bit free in the set, so find the index of the lowest
    // one with the lookup table.

    index := KepIndexFirstBitClear[mask]

    // Set the bit to allocate the table entry.

    newmask := mask | (1 << index)

    // Try to atomic CAS the new bit set.

    IF KeCompareSwapUlong (
        &KepSendTableSet, // ptr
        newmask, // newvalue
        mask, // expectedvalue
    ) != mask THEN

        // Someone else modified it inbetween (rare case). Retry.

        GOTO Retry
    END

    // We've successfully allocated a table entry for our IPI.
    // Store our Prb in the table. This MUST be done before we set any pending
    // bits or send any IPIs.

    KepSendTable[index] = prb

    i := 0

    targetprb := CAST KeuLoaderBlock.VirtualPrbBase TO ^KepPrb

    // Now set pending to all of the processors in the system (other than me).

    WHILE i < count DO
        IF targetprb != prb THEN
            IF deferred AND targetprb^.InIdleLoop THEN
                // This is an idle-deferrable IPI and the target is in his idle
                // loop. Just set the idle deferred event flag. This lets us
                // avoid spin-waiting for idle processors to come out of their
                // low-power state (which can take a long while) and makes idle
                // processors less of a burden to their active brethren.

                // NOTE: KeOrUlong does the necessary read-side barriers, so we
                //       don't need to repeat them before re-examining
                //       InIdleLoop; it has an effective SeqCst ordering effect.

                KeOrUlong (
                    &targetprb^.IdleDeferredIpiEvents, // ptr
                    deferred, // set
                )

                // Language-level BARRIER to stop the read from InIdleLoop being
                // smushed together.

                BARRIER

                IF targetprb^.InIdleLoop THEN
                    // It's still in the idle loop, which means we can avoid
                    // waiting for this guy. It doesn't really matter if it left
                    // the idle loop and returned, all that matters is that it's
                    // in this quiescent state at some point after we set the
                    // event bit, and before we returned from this routine. This
                    // is because return from this routine indicates delivery of
                    // the notification, leading to assumptions that must be
                    // respected such as "from this point on, other processors
                    // will not access a stale TB entry" and so on. So, we need
                    // a guarantee that the target processor either has seen or
                    // will see this notification before it has a chance to do
                    // an illegal thing like that. The idle loop is therefore a
                    // good quiescent state to choose for this. Note the caveat
                    // that even interrupt handlers that execute while we were
                    // in the idle loop cannot be considered quiescent, because
                    // they may access stale system space mappings (mapped MDLs
                    // and so on). So the architectural interrupt handler should
                    // be written to disable the InIdleLoop bit and process the
                    // pending events; actually, that is the only place that
                    // needs to do that, since any event that causes a processor
                    // to stop being idle was ultimately caused by an interrupt.

                    KeIncrementUlong (
                        &prb^.IpiSpin, // ptr
                        0xFFFFFFFF, // inc
                    )

                    GOTO Next
                END
            END

            // Mark pending.

            KeOrUlong (
                &targetprb^.IpiPendingSet, // ptr
                1 << index, // bitset
            )

            HaluTriggerIpi (
                targetprb, // target
                KEP_IPI_SYNCHRONOUS, // type
            )
        END

@Next

        i += 1
        targetprb += BL_PRB_SIZE
    END

    // Spin-wait for execution of our callback.

    WHILE prb^.IpiSpin DO
        // Do a language BARRIER so that this loop isn't optimized out.

        BARRIER

        KeSpinPause ()
    END

    // Now release the sender table entry.

    KeMaskUlong (
        &KepSendTableSet, // ptr
        ~(1 << index), // mask
    )
END

FN (KeInterruptF) KepServiceIpi (
    IN interrupt : ^KeInterrupt,
    IN context : ^OsContext,
)

    // An IPI interrupt has been delivered. Walk all of the PRBs and see who
    // wants our attention. Note that this interrupt is the highest priority in
    // the system so we should try to keep it brief - nothing can happen while
    // we are here, not even the timer can tick, and the reason for that is to
    // make sure IPI response times are high so we don't tie up other processors
    // waiting for us to release them from a spin wait.
    //
    // Not all IPIs cause waits on behalf of the requesting processor. Some of
    // them are perfectly happy being asynchronous such as requesting a software
    // interrupt in order to get us to service our APC or DPC queue or to switch
    // to a newly readied thread for which we were selected, to dispatch signals
    // in usermode, etc.

    prb := KEP_CURRENT_PRB
    pendingset : UWORD

    IF prb^.IpiPendingSet THEN
        // There are pending IPIs.

        // Atomically fetch the bit set of pending senders and set to zero.
        // Any IPIs sent after we do this we will catch on the next
        // IPI which will remain pending until we exit here.

        pendingset = KeFetchAndSetUlong (
            &prb^.IpiPendingSet, // ptr
            0, // newvalue
        )

        // For each bit set, execute the IPI function.

        index := 0

        WHILE pendingset DO
            IF pendingset & 1 THEN
                requestingprb := KepSendTable[index]

                // Call the requesting processor's function.

                requestingprb^.IpiFunction (
                    requestingprb^.IpiContext1, // context1
                    requestingprb^.IpiContext2, // context2
                )

                // Release him from his spin.

                KeIncrementUlong (
                    &requestingprb^.IpiSpin, // ptr
                    0xFFFFFFFF, // inc
                )
            END

            index += 1
            pendingset >>= 1
        END
    END

    IF KepFreezeCount THEN
        // Oh snap! Lock up until the freeze count reaches zero.

        // Set our context in our Prb for the debugger.
        // If it was already set, then don't reset it as this is
        // probably the more relevant information.

        IF NOT prb^.FrozenContext THEN
            prb^.FrozenContext = context
        END

        // Increment this count so that the freezer knows when we froze.

        KeIncrementUlong (
            &KepFrozenProcessorCount, // ptr
            1, // inc
        )

        WHILE KepFreezeCount DO
            // Do a language BARRIER so that this loop isn't optimized out.

            BARRIER

            KeSpinPause ()
        END

        // Decrement the count on the way out.

        KeIncrementUlong (
            &KepFrozenProcessorCount, // ptr
            0xFFFFFFFF, // inc
        )

        prb^.FrozenContext = NULLPTR
    END
END

#END