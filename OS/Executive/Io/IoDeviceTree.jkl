//
// Implements management for the device tree.
//
// This is actually a device DAG. In fact, it's multiple device DAGs.
// Each independent DAG is called a "plane". A device object can have an
// independent existence (or lack thereof) as a vertex within each plane.
//
// This is directly inspired by the IOKit Device Registry from the Apple XNU
// kernel.
//

#INCLUDE "Iop.hjk"

IopTreeLock : KeLock = KE_INITIAL_LOCK

#MACRO IopLockPlane ( plane ) [
    KeAcquireLockExclusive ( &(plane)^.Lock )
]

#MACRO IopUnlockPlane ( plane ) [
    KeReleaseLock ( &(plane)^.Lock )
]

#MACRO IopLockNode ( treenode ) [
    KeAcquireLockExclusive ( &(treenode)^.Lock )
]

#MACRO IopUnlockNode ( treenode ) [
    KeReleaseLock ( &(treenode)^.Lock )
]

STRUCT IouPlane
    Name : ^UBYTE,

    NodeListHead : RtlListEntry,

    PlaneListEntry : RtlListEntry,

    Lock : KeLock,
END

STRUCT IouPlaneEntry
    Plane : ^IouPlane,

    TreeNode : ^IouTreeNode,

    RelationSets : ExuObjectSet[IOU_RELATION_MAX],

    Entry : RtlListEntry,
END

EXPORT IouServicePlane : ^IouPlane

IopServicePlane : IouPlane

IopPlaneList : RtlListEntry

IopPlaneListLock : KeLock = KE_INITIAL_LOCK

#SECTION "PAGEtext"
FN IouInitializePlane (
    IN plane : ^IouPlane,
    IN name : ^UBYTE,
)

    // Initialize a plane of the device tree.

    plane^.Name = name

    RtlInitializeList ( &plane^.NodeListHead )

    KeInitializeLock ( &plane^.Lock )

    KeAcquireLockExclusive ( &IopPlaneListLock )

    RtlInsertAtTailList (
        &IopPlaneList, // head
        &plane^.PlaneListEntry, // entry
    )

    KeReleaseLock ( &IopPlaneListLock )
END

#SECTION "INITtext"
FN IopInitializeDeviceTree ()

    // Initialize the device tree.

    IouServicePlane = &IopServicePlane

    IouInitializePlane (
        IouServicePlane, // plane
        "IouService", // name
    )
END

#SECTION "PAGEtext"
FN IopFindPlaneEntry (
    IN treenode : ^IouTreeNode,
    IN plane : ^IouPlane,
) : ^IouPlaneEntry

    // Find the plane entry associated with the tree node.
    // If not found, allocate. Returns NULLPTR on failure.
    // Assumes node lock is held if relevant.

    listhead := &treenode^.PlaneEntryListHead
    listentry := listhead^.Next

    WHILE listentry != listhead DO
        node := CONTAINEROF listentry TO IouPlaneEntry.Entry

        IF node^.Plane == plane THEN
            // Found it.

            RETURN node
        END

        listentry = listentry^.Next
    END

    // Create a new one.

    node := CAST MmuAllocatePool (
        MMU_PAGED_POOL, // poolindex
        SIZEOF IouPlaneEntry, // bytes
        'PlNd', // tag
        TRUE, // wait
    ) TO ^IouPlaneEntry

    IF NOT node THEN
        RETURN NULLPTR
    END

    // Initialize the plane node.

    node^.Plane = plane
    node^.TreeNode = treenode

    i := 0

    WHILE i < IOU_RELATION_MAX DO
        ExuInitializeSet (
            &node^.RelationSets[i], // set
            'IoRs', // tag
            MMU_PAGED_POOL, // poolindex
        )

        i += 1
    END

    RtlInsertAtTailList (
        &treenode^.PlaneEntryListHead, // head
        &node^.Entry, // entry
    )

    RETURN node
END

#SECTION "PAGEtext"
FN IouAttachNodes (
    IN plane : ^IouPlane,
    IN parent : ^IouTreeNode,
    IN child : ^IouTreeNode,
) : OsStatus

    // Attach two nodes in the specified plane.

    // A note about error cases: we don't bother cleaning up a newly allocated
    // plane entry because it will just get cleaned up when the tree node is
    // deleted.

    status := OS_STATUS_SUCCESS

    IopLockNode ( parent )
    IopLockNode ( child )

    parentchildren := IopFindPlaneEntry (
        parent, // treenode
        plane, // plane
    )

    IF NOT parentchildren THEN
        status = OS_STATUS_NO_MEMORY

        GOTO Exit
    END

    childparents := IopFindPlaneEntry (
        child, // treenode
        plane, // plane
    )

    IF NOT childparents THEN
        status = OS_STATUS_NO_MEMORY

        GOTO Exit
    END

    // Insert as a parent of the child.

    status = ExAppendSet (
        &childparents^.RelationSets[IOU_RELATION_PARENTS], // set
        parent, // object
    )

    IF OsError ( status ) THEN
        GOTO Exit
    END

    // Insert as a child of the parent.

    status = ExAppendSet (
        &parentchildren^.RelationSets[IOU_RELATION_CHILDREN], // set
        child, // object
    )

    IF OsError ( status ) THEN
        // Undo the parent addition.

        ExRemoveObjectFromSet (
            &childparents^.RelationSets[IOU_RELATION_PARENTS], // set
            parent, // object
        )

        GOTO Exit
    END

    // The link has been made, so reference the parent to reflect the new child.

    ObuReferenceObject ( parent )

@Exit

    IopUnlockNode ( child )
    IopUnlockNode ( parent )

    RETURN status
END

#SECTION "PAGEtext"
FN IouDetachNodes (
    IN plane : ^IouPlane,
    IN parent : ^IouTreeNode,
    IN child : ^IouTreeNode,
)

    // Destroy a parent-child relationship in the specified plane.

    IopLockNode ( parent )
    IopLockNode ( child )

    parentchildren := IopFindPlaneEntry (
        parent, // treenode
        plane, // plane
    )

    KeAssert ( parentchildren != NULLPTR )

    childparents := IopFindPlaneEntry (
        child, // treenode
        plane, // plane
    )

    KeAssert ( childparents != NULLPTR )

    removed := ExRemoveObjectFromSet (
        &childparents^.RelationSets[IOU_RELATION_PARENTS], // set
        parent, // object
    )

    KeAssert ( removed != FALSE )

    removed = ExRemoveObjectFromSet (
        &parentchildren^.RelationSets[IOU_RELATION_CHILDREN], // set
        child, // object
    )

    KeAssert ( removed != FALSE )

    IopUnlockNode ( child )
    IopUnlockNode ( parent )

    // Unreference the parent to reflect the loss of a child.
    // This may cause the parent to go away as well.

    ObuUnreferenceObject ( parent )
END

#SECTION "PAGEtext"
FN (ExEnumerateSetF) IopEnumerateRelationsFunc (
    IN set : ^ExuObjectSet,
    IN object : ^VOID,
    IN context : ^VOID,
) : OsStatus

    // Add an object to the relation set.

    IF NOT ObuConditionallyReferenceObject ( object ) THEN
        // It's being deleted and just hasn't been removed from the relation set
        // yet. Pretend it's not here.

        RETURN OS_STATUS_SUCCESS
    END

    status := ExAppendSet (
        CAST context TO ^ExuObjectSet, // set
        object, // object
    )

    IF OsError ( status ) THEN
        // Failed to append. Remove the reference.
        // Use DeferDelete so that we don't call the deletion routine while
        // holding a node lock.

        ObuUnreferenceObjectDeferDelete ( object )
    END

    RETURN status
END

#SECTION "PAGEtext"
FN (ExEnumerateSetF) IopEnumerateDestroyRelationsFunc (
    IN set : ^ExuObjectSet,
    IN object : ^VOID,
    IN context : ^VOID,
) : OsStatus

    // Remove a reference from an object that was previously in a relation
    // enumeration set.

    ObuUnreferenceObject ( object )

    RETURN OS_STATUS_SUCCESS
END

#SECTION "PAGEtext"
FN IouEmptyRelationSet (
    IN set : ^ExuObjectSet,
)

    // Uninitialize a set previously filled by a call to IouEnumerateRelations.

    ExEmptySet (
        set, // set
        &IopEnumerateDestroyRelationsFunc, // enumfunc
        NULLPTR, // context
    )
END

#SECTION "PAGEtext"
FN IouEnumerateRelations (
    IN plane : ^IouPlane,
    IN treenode : ^IouTreeNode,
    IN set : ^ExuObjectSet,
    IN relation : IouNodeRelation,
) : OsStatus

    // Enumerate the relations of the tree node into the provided object set.
    // The object set must be empty.
    //
    // The returned tree nodes are referenced and safe to use. Because they're
    // referenced, the set must be emptied by a call to IouEmptyRelationSet once
    // the extra reference is no longer required.
    //
    // An important detail is that this only captures the set of relations at a
    // particular moment in time, and it may change after the capture. If it
    // returns a successful status, however, it is guaranteed to provide a
    // *complete* picture of the relations at that moment. If it returns an
    // error status, the set remains empty.

    status := OS_STATUS_SUCCESS

    IopLockNode ( treenode )

    planeentry := IopFindPlaneEntry (
        treenode, // treenode
        plane, // plane
    )

    IF planeentry == NULLPTR THEN
        // Failed to get a plane entry, so there can be no relations anyway.
        // Just exit.

        GOTO Exit
    END

    status = ExEnumerateSet (
        &planeentry^.RelationSets[relation], // set
        &IopEnumerateRelationsFunc, // enumfunc
        set, // context
    )

@Exit

    IopUnlockNode ( treenode )

    IF OsError ( status ) THEN
        // Failed to capture a complete picture of the relations, so
        // remove whatever items we did get.

        IouEmptyRelationSet ( set )
    END

    RETURN status
END