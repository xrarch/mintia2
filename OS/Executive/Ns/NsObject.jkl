//
// Implements namespace-level object management.
//

#INCLUDE "Nsp.hjk"
#INCLUDE "<inc>/Iou.hjk"

EXTERN IouFileType : ObuType

#SECTION "PAGEtext"
FN NsuCaptureOpenParameters (
    IN userparams : ^OsOpenParameters,
    IN sysparams : ^NsuOpenParameters,
) : OsStatus

    // Copy open parameters into system space.

    status := MmuSafeCopyIn (
        &sysparams^.Captured, // dest
        userparams, // src
        SIZEOF OsOpenParameters, // sz
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    IF NOT sysparams^.Captured.Path.Data THEN
        sysparams^.Path.Data = NULLPTR

        RETURN OS_STATUS_SUCCESS
    END

    // Copy in the path.

    status = MmuCaptureString (
        &sysparams^.Captured.Path, // string
        &sysparams^.Path, // output
        OS_PATH_MAX, // maxlen
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    // There's a path, so initialize the initial handle.

    IF sysparams^.Captured.InitialHandle == OS_NULL_HANDLE THEN
        // The initial path is the root directory.

        sysparams^.InitialHandle.Entry = NULLPTR

        RETURN OS_STATUS_SUCCESS
    END

    // Translate the initial handle.

    file : ^IouFile
    access : UWORD

    status = ObuReferenceByHandle (
        sysparams^.Captured.InitialHandle, // handle
        &IouFileType, // type
        OUT file, // object
        OUT access, // access
    )

    IF OsError ( status ) THEN
        MmuFreeCapturedString ( &sysparams^.Path )

        RETURN status
    END

    // Capture the namespace handle stored in the file object.

    NsuCopyHandle (
        &sysparams^.InitialHandle, // dest
        &file^.Handle, // src
    )

    NsuReferenceHandle ( &sysparams^.InitialHandle )

    ObuUnreferenceObject ( file )

    RETURN OS_STATUS_SUCCESS
END

#SECTION "PAGEtext"
FN NsuFreeOpenParameters (
    IN params : ^NsuOpenParameters,
)

    // Free open parameters that were previously copied in from userspace.

    IF params^.Path.Data THEN
        MmuFreeCapturedString ( &params^.Path )

        IF params^.InitialHandle.Entry THEN
            // Unreference the initial handle.

            NsuUnreferenceHandle ( &params^.InitialHandle )
        END
    END
END

#SECTION "PAGEtext"
FN NsuCreateObject (
    IN params : ^NsuOpenParameters,
    IN obparams : ^ObuParameters,
    OUT object : ^VOID,
) : OsStatus

    // This is a helper routine for simple cases of creating an object.
    // Most instances of file object creation are not a simple case, so that
    // does not call this, but most other things do.

    // 'params' should be a fully initialized NsuOpenParameters structure, which
    // means it should have been in-copied from userspace and the initial handle
    // should have been resolved. Caller takes care of undoing these things.

    // 'obparams' should have been initialized by the NsuInitializeParameters
    // macro.

    IF params^.Path.Data THEN
        // We are atomically creating the object within the namespace.

        params^.Captured.Flags |= OS_OPEN_CREATE
        params^.ResultFlags = 0

        handle : NsuHandle

        status := NsuLookupEntryByPath (
            params, // params
            &handle, // handle
            &obparams^.Permissions.Cred, // cred
            obparams, // obparams
            obparams^.Type, // type
        )

        IF OsError ( status ) THEN
            RETURN status
        END

        object = handle.Entry^.Object

        ObuReferenceObject ( object )

        NsuUnreferenceHandle ( &handle )

        RETURN OS_STATUS_SUCCESS
    END

    params^.ResultFlags = NSU_RESULT_CREATED

    RETURN ObuAllocateObject (
        obparams, // obparams
        OUT object, // object
    )
END

#SECTION "PAGEtext"
FN NsuLookupObject (
    IN params : ^NsuOpenParameters,
    IN type : ^ObuType,
    OUT object : ^VOID,
) : OsStatus

    // This is a helper routine for looking up an object within the namespace.

    IF NOT params^.Path.Data THEN
        RETURN OS_STATUS_INVALID_ARGUMENT
    END

    IF params^.Captured.Flags & OS_OPEN_CREATE THEN
        RETURN OS_STATUS_INVALID_ARGUMENT
    END

    handle : NsuHandle

    status := NsuLookupEntryByPath (
        params, // params
        &handle, // handle
        PsuCurrentCredentials (), // cred
        NULLPTR, // obparams
        type, // type
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    object = handle.Entry^.Object

    ObuReferenceObject ( object )

    NsuUnreferenceHandle ( &handle )

    RETURN OS_STATUS_SUCCESS
END

#SECTION "PAGEtext"
FN NsuInitializeProcess (
    IN process : ^PsuProcess,
    IN parentprocess : ^PsuProcess,
)

    // Initialize the process for usage by the namespace.

    paged := process^.Paged

    // Initialize the root handle from that of the parent process.

    NspCaptureRootHandle (
        parentprocess, // process
        &paged^.RootHandle, // roothandle
    )

    // Initialize the root handle lock.

    KeInitializeLock ( &paged^.RootLock )
END

#SECTION "PAGEtext"
FN NsuUninitializeProcess (
    IN process : ^PsuProcess,
)

    // Uninitialize a process with respect to the namespace.

    paged := process^.Paged

    // Unreference the root handle.

    NsuUnreferenceHandle ( &paged^.RootHandle )
END