//
// Manipulation of reference counted objects.
//

#INCLUDE "Obp.hjk"

PUBLIC ObpDeferredDeletionList : KeCustodyList

PUBLIC ObpDeferredDeletionTimedWorkItem : ExTimedWorkItem

#SECTION "PAGEtext"
FN ObAllocateObject (
    IN obparams : ^ObParameters,
    OUT object : ^VOID,
) : OsStatus

    // Allocate an object of the specified type.

    // Calculate the sizes of the paged and nonpaged part of the object.

    psize : UWORD
    npsize : UWORD

    IF obparams^.Type^.IsPaged THEN
        npsize = 0

        psize = SIZEOF ObHeader +
            SIZEOF ObpCommonHeader +
            obparams^.BodySize

    ELSE
        npsize = obparams^.BodySize + SIZEOF ObpCommonHeader

        psize = SIZEOF ObHeader + obparams^.PagedBodySize
    END

    // Charge pool quota.

    npquota := MmGetOverheadOfBytes ( npsize ) + obparams^.NpQuotaBias
    pquota := MmGetOverheadOfBytes ( psize ) + obparams^.PgQuotaBias

    process := PsCurrentProcess ()

    quotablock := PsQuotaBlock ( process )

    status := MmChargePoolQuota (
        quotablock, // quotablock
        npquota, // charge
        MM_NONPAGED_POOL, // poolindex
    )

    IF OsError ( status ) THEN
        RETURN status
    END

    status = MmChargePoolQuota (
        quotablock, // quotablock
        pquota, // charge
        MM_PAGED_POOL, // poolindex
    )

    IF OsError ( status ) THEN
        GOTO Error1
    END

    nppart := NULLPTR

    IF npsize THEN
        nppart = MmAllocatePool (
            MM_NONPAGED_POOL, // poolindex
            npsize, // bytes
            obparams^.Type^.Tag, // tag
            TRUE, // wait
        )

        IF NOT nppart THEN
            status = OS_STATUS_NO_MEMORY

            GOTO Error2
        END
    END

    pgpart := NULLPTR

    IF psize THEN
        pgpart = MmAllocatePool (
            MM_PAGED_POOL, // poolindex
            psize, // bytes
            obparams^.Type^.Tag, // tag
            TRUE, // wait
        )

        IF NOT pgpart THEN
            status = OS_STATUS_NO_MEMORY

            GOTO Error3
        END
    END

    header : ^ObHeader
    commonheader : ^ObpCommonHeader

    IF obparams^.Type^.IsPaged THEN
        header = pgpart
        commonheader = pgpart + SIZEOF ObHeader

    ELSE
        header = pgpart
        commonheader = nppart
    END

    commonheader^.Header = header

    header^.PagedQuotaCharge = pquota
    header^.NonpagedQuotaCharge = npquota

    MmReferenceQuotaBlock ( quotablock )

    header^.QuotaBlock = quotablock

    header^.Flags = obparams^.Flags

    ObMoveCredentials (
        &header^.Permissions.Cred, // dest
        &obparams^.Permissions.Cred, // src
    )

    header^.Permissions.Access = obparams^.Permissions.Access

    commonheader^.PointerCount = 1
    header^.HandleCount = 0

    header^.NamespaceEntry = NULLPTR

    header^.Type = obparams^.Type

    KeInitializeLock ( &header^.Lock )

    object = commonheader + SIZEOF ObpCommonHeader

    IF obparams^.Type^.Initialize THEN
        // Call the initialization callback.

        status = obparams^.Type^.Initialize (
            object, // object
            obparams^.Context, // context
        )

        IF OsError ( status ) THEN
            // Initialization failed so just free everything we did.

            ObFreeObject ( object )

            RETURN status
        END
    END

    RETURN OS_STATUS_SUCCESS

@Error3

    IF nppart THEN
        MmFreePool (
            nppart, // ptr
            obparams^.Type^.Tag, // tag
        )
    END

@Error2

    MmUnchargePoolQuota (
        quotablock, // quotablock
        pquota, // charge
        MM_PAGED_POOL, // poolindex
    )

@Error1

    MmUnchargePoolQuota (
        quotablock, // quotablock
        npquota, // charge
        MM_NONPAGED_POOL, // poolindex
    )

    RETURN status
END

#SECTION "PAGEtext"
FN ObFreeObject (
    IN object : ^VOID,
)

    // Free an object. Don't call the deletion routine.

    header := ObFindHeader ( object )

    // Shouldn't be possible because temporary objects are removed from the
    // namespace when their handle count reaches zero, which happened before
    // now. Permanent objects have their refcount biased when inserted into
    // the namespace.

    KeAssert ( header^.NamespaceEntry == NULLPTR )

    quotablock := header^.QuotaBlock

    pquota := header^.PagedQuotaCharge
    npquota := header^.NonpagedQuotaCharge
    type := header^.Type

    // Free the object parts.

    IF type^.IsPaged THEN
        // Only a single part.

        MmFreePool (
            header, // ptr
            type^.Tag, // tag
        )

    ELSE
        // Two parts.

        MmFreePool (
            header, // ptr
            type^.Tag, // tag
        )

        MmFreePool (
            object - SIZEOF ObpCommonHeader, // ptr
            type^.Tag, // tag
        )
    END

    // Uncharge quota.

    IF pquota THEN
        MmUnchargePoolQuota (
            quotablock, // quotablock
            pquota, // charge
            MM_PAGED_POOL, // poolindex
        )
    END

    IF npquota THEN
        MmUnchargePoolQuota (
            quotablock, // quotablock
            npquota, // charge
            MM_NONPAGED_POOL, // poolindex
        )
    END

    MmUnreferenceQuotaBlock ( quotablock )
END

#SECTION "PAGEtext"
FN ObDeleteObject (
    IN object : ^VOID,
)

    // Delete an object. Call the deletion routine.

    header := ObFindHeader ( object )

    IF header^.Type^.Delete AND
        NOT header^.Type^.Delete ( object ) THEN

        // Type routine said nevermind, don't delete.

        LEAVE
    END

    ObFreeObject ( object )
END

FN ObReferenceObject (
    IN object : ^VOID,
)

    // Reference an object by pointer.

    commonheader := ObFindCommonHeader ( object )

    oldcount := KeIncrementPtr (
        &commonheader^.PointerCount, // ulong
        1, // inc
    )

    KeAssert ( oldcount != 0 )
END

#SECTION "PAGEtext"
FN ObpEmptyDeferredList (
    IN paged : ^PsPagedThread,
)

    // Empty the deferred object deletion list of the thread.

    KeAssert ( paged^.DeletingObject != FALSE )

    // Loop this whole process because we might enqueue even more objects while
    // deleting the objects we have.

    WHILE paged^.DeferredObjectHead DO
        // Detach the whole chain.

        object := paged^.DeferredObjectHead
        paged^.DeferredObjectHead = NULLPTR

        // Free every object in the chain.

        WHILE object DO
            next := ObFindCommonHeader ( object )^.NextToReap

            ObDeleteObject ( object )

            object = next
        END
    END
END

#SECTION "PAGEtext"
FN (KeProcessCustodyListF) ObpDeferredDeleteObject (
    IN list : ^KeCustodyList,
    IN object : ^VOID,
    IN context : ^VOID,
)

    // Called to delete an object as a KeProcessCustodyList callback.

    commonheader := CONTAINEROF object TO ObpCommonHeader.NextToReap

    ObDeleteObject ( ObFindObjectFromCommonHeader ( commonheader ) )
END

#SECTION "PAGEtext"
FN (ExWorkItemF) ObpDeferredDeletionWorker (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // An object deletion was deferred to worker thread context and WE are that
    // worker thread.

    paged := PsCurrentThread ()^.Paged

    paged^.DeletingObject = TRUE

    KeProcessCustodyList (
        &ObpDeferredDeletionList, // list
        &ObpDeferredDeleteObject, // callback
        NULLPTR, // context
    )

    IF paged^.DeferredObjectHead THEN
        ObpEmptyDeferredList ( paged )
    END

    paged^.DeletingObject = FALSE
END

FN (KeCustodyListActivateF) ObpActivateDeletionWorker (
    IN list : ^KeCustodyList,
)

    // The list was previously empty, so we should enqueue the work item.
    // Note that this guarantees that the work item is not actively in the
    // queue, because the list is only emptied by the worker itself.

    interval : RtlUquad

    RtlSetUquadToUlong (
        &interval, // uquad
        100, // interval
    )

    ExEnqueueTimedWorkItem (
        &ObpDeferredDeletionTimedWorkItem, // item
        EX_LOW_WORK_PRIORITY, // priority
        &interval, // interval
        0, // context1
        0, // context2
    )
END

FN ObpDeferDeletion (
    IN object : ^VOID,
)

    // Enqueue deleting this object as some work to the worker thread pool.

    commonheader := ObFindCommonHeader ( object )

    // Atomically add it to the list.

    KeInsertCustodyList (
        &ObpDeferredDeletionList, // list
        &commonheader^.NextToReap, // link
    )
END

FN ObUnreferenceObject (
    IN object : ^VOID,
)

    // Unreference an object by pointer.

    commonheader := ObFindCommonHeader ( object )

    oldcount := KeIncrementPtr (
        &commonheader^.PointerCount, // ulong
        -1, // inc
    )

    KeAssert ( oldcount != 0 )

    IF oldcount == 1 THEN
        // Delete it.

        thread := PsCurrentThread ()

        IF NOT KeAreApcsDeliverable () OR
            thread^.VmPrivileged THEN

            // APCs are not deliverable so we're gonna defer this deletion to
            // worker thread context. This is to avoid illegally trying to
            // recursively acquire locks inside deletion routines that are not
            // APC-safe.
            //
            // We also defer deletion if the current thread is VM privileged.
            // This is because actually deleting an object will invariably touch
            // paged pool (to delete the paged part of the header) and we assume
            // a VM privileged thread is involved in page-out and so this could
            // otherwise cause deadlock.

            ObpDeferDeletion ( object )

            LEAVE
        END

        // If APCs are deliverable then we aren't in an APC and are not
        // holding a lock depended upon by APCs, so we can delete this
        // object inline.

        paged := thread^.Paged

        IF paged^.DeletingObject THEN 
            // We're already deleting an object. To avoid causing a big
            // call stack due to recursively deleting lots of objects, and
            // potentially overflowing our kernel stack, enqueue this
            // object to a thread-local list of pending deletions. It'll
            // get handled when the outermost object deletion returns.

            // Note that there's no need to synchronize this list since we
            // only access it from our own context.

            commonheader^.NextToReap = paged^.DeferredObjectHead
            paged^.DeferredObjectHead = object

        ELSE
            // Delete the object right away.

            paged^.DeletingObject = TRUE

            KeIgnoreEvents ()

            ObDeleteObject ( object )

            IF paged^.DeferredObjectHead THEN
                ObpEmptyDeferredList ( paged )
            END

            KeAcceptEvents ()

            paged^.DeletingObject = FALSE
        END
    END
END

FN ObUnreferenceObjectDeferDelete (
    IN object : ^VOID,
)

    // Unreference an object by pointer.

    // Unconditionally defer deletion instead of doing it in the context of this
    // thread. This is useful when a lock is held that is also taken in the
    // deletion routine.

    commonheader := ObFindCommonHeader ( object )

    oldcount := KeIncrementPtr (
        &commonheader^.PointerCount, // ulong
        -1, // inc
    )

    KeAssert ( oldcount != 0 )

    IF oldcount == 1 THEN
        // Delete it.

        ObpDeferDeletion ( object )
    END
END

FN ObConditionallyReferenceObject (
    IN object : ^VOID,
) : UWORD

    // Reference an object only if the pointer count would not increase from
    // zero. Only the caller can know whether this is safe, for instance, by
    // holding a lock that the object's deletion routine must take before it
    // finally frees the object. Returns TRUE if referenced successfully, FALSE
    // otherwise.

    commonheader := ObFindCommonHeader ( object )

    oldcount := commonheader^.PointerCount

    WHILE TRUE DO
        IF oldcount == 0 THEN
            // Count is already zero, so it may be undergoing deletion. Not safe
            // to reference this object.

            RETURN FALSE
        END

        new := KeCompareSwapPtr (
            &commonheader^.PointerCount, // ptr
            oldcount + 1, // newvalue
            oldcount, // expectedvalue
        )

        IF new == oldcount THEN
            // Got it. The previous count was nonzero so we know the object is
            // safe.

            RETURN TRUE
        END

        oldcount = new
    END
END

#SECTION "PAGEtext"
FN ObCheckAccess (
    IN permissions : ^ObPermissions,
    IN cred : ^ObCredentials,
    IN access : UWORD,
) : UWORD

    // Check the requested access against the permissions.

    shift := 0

    IF permissions^.Cred.Uid == cred^.Uid THEN
        shift = 6 // owner
    ELSEIF permissions^.Cred.Gid == cred^.Gid THEN
        shift = 3 // group
    END

    permbits := (permissions^.Access >> shift) & OS_ACCESS_ALL

    IF permbits | access == permbits THEN
        // Access is a subset of the permissions we have.

        RETURN TRUE
    END

    IF cred^.Uid == OS_SYSTEM_UID THEN
        // Access isn't a subset, but we are the system user.

        RETURN TRUE
    END

    IF permissions^.Cred.Uid == OS_SYSTEM_UID THEN
        // Access isn't a subset, and we aren't the system user, and the owner
        // of this object is the system user. Permission is denied.

        RETURN FALSE
    END

    // As an admin, we can access all objects except those owned by the system
    // user.

    RETURN PsCheckPrivilege ( PsCurrentProcess (), OS_PRIVILEGE_ADMIN )
END

#SECTION "PAGEtext"
FN ObCheckAccessForCurrent (
    IN object : ^VOID,
    IN access : UWORD,
) : UWORD

    // Check the requested access against the object's permissions, with respect
    // to the current process's credentials.

    RETURN ObCheckAccess (
        &ObFindHeader ( object )^.Permissions, // permissions
        PsCurrentCredentials (), // cred
        access, // access
    )
END