//
// Implements the MINTIA Zero Page Worker thread.
//

#INCLUDE "Mmp.hjk"
#INCLUDE "<ll>/System/OsProcess.hjk"

#DEFINE MMP_ZERO_BATCH 4
#DEFINE MMP_ZERO_MAXIMUM 64
#DEFINE MMP_ZERO_DELAY 100

FN (KeStartThreadF) MmuZeroPageWorker (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // Loop waiting on an event object which is signaled when there are more
    // than a threshold of free pages. Then, zero them out and place them on
    // the zeroed page list.

    partition := PsuCurrentPartition ()

    zerobatch : ^MmpPfe[MMP_ZERO_BATCH]

    interval : RtlUquad

    RtlSetUquadToUlong (
        &interval, // uquad
        MMP_ZERO_DELAY, // ulong
    )

    // Set myself to the idle priority.

    KeSetBasePriorityThread (
        KeCurrentThread (), // thread
        OS_PRIORITY_IDLE, // priority
        TRUE, // setcurrentpriority
    )

    WHILE TRUE DO
        // Wait for there to be more than a threshold of free pages.

        KeuWaitForSingleObject (
            KE_KERNEL_MODE, // waitmode
            KE_UNALERTABLE, // alertable
            NULLPTR, // timeout
            &partition^.ZeroPageEvent.Header, // object
        )

        // Zero at most 64 pages during each 100ms interval.
        // We will zero a maximum of 640 pages per second which is about 2.5MB.

        count : WORD = MMP_ZERO_MAXIMUM

        WHILE count > 0 DO
            IF partition^.FreeList.Count < MMP_ZERO_BATCH THEN
                BREAK
            END

            i := 0

            ipl := MmpAcquireListExclusive ( partition )

            IF partition^.AvailablePageCount < partition^.SufficientPageCount OR
                partition^.FreeList.Count < MMP_ZERO_BATCH THEN

                // Don't zero any pages if people are trying to make memory
                // right now. We'd only step on their toes by holding pages
                // hostage at idle priority.

                MmpReleaseList ( partition, ipl )

                BREAK
            END

            WHILE i < MMP_ZERO_BATCH DO
                zerobatch[i] = MmpAllocatePageFromList (
                    &partition^.FreeList, // list
                    0, // color
                )

                i += 1
            END

            // Decrement available pages to reflect the ones we removed from
            // the zero page list.

            avail := partition^.AvailablePageCount - MMP_ZERO_BATCH
            partition^.AvailablePageCount = avail

            MmpReleaseList ( partition, ipl )

            // Zero the pages.

            i = 0

            WHILE i < MMP_ZERO_BATCH DO
                MmZeroPage ( MmpPfeToPfn ( zerobatch[i] ) )

                i += 1
            END

            // Place on zeroed list.

            i = 0

            ipl = MmpAcquireListExclusive ( partition )

            WHILE i < MMP_ZERO_BATCH DO
                MmpInsertPageIntoList (
                    &partition^.ZeroList, // list
                    zerobatch[i], // pfe
                    TRUE, // head
                )

                i += 1
            END

            // Increment available pages to reflect the ones added to the zero
            // page list.

            avail = partition^.AvailablePageCount + MMP_ZERO_BATCH
            partition^.AvailablePageCount = avail

            MmpReleaseList ( partition, ipl )

            // Wake waiters for available pages.

            MmpNewAvailablePage ( partition, avail )

            count -= MMP_ZERO_BATCH
        END

        // Sleep for 100ms to avoid using too much memory bandwidth.

        KeSleep (
            &interval, // interval
            KE_KERNEL_MODE, // waitmode
            KE_UNALERTABLE, // alertable
        )
    END
END