//
// Implements the MINTIA Zero Page Worker thread.
//

#INCLUDE "Mmp.hjk"
#INCLUDE "<ll>/System/OsProcess.hjk"

#DEFINE MMP_ZERO_BATCH 4
#DEFINE MMP_ZERO_MAXIMUM 64
#DEFINE MMP_ZERO_DELAY 100

FN (KeStartThreadF) MmuZeroPageWorker (
    IN context1 : UWORD,
    IN context2 : UWORD,
)

    // Loop waiting on an event object which is signaled when there are more
    // than a threshold of free pages. Then, zero them out and place them on
    // the zeroed page list.

    mmnode := MmCurrentNode ()

    zerobatch : ^MmpPfe[MMP_ZERO_BATCH]

    interval : RtlUquad

    RtlSetUquadToUlong (
        &interval, // uquad
        MMP_ZERO_DELAY, // ulong
    )

    // Set myself to the idle priority.

    KeSetBasePriorityThread (
        KeCurrentThread (), // thread
        OS_PRIORITY_IDLE, // priority
        TRUE, // setcurrentpriority
    )

    WHILE TRUE DO
        // Wait for there to be more than a threshold of free pages.

        KeWaitForSingleObject (
            KE_KERNEL_MODE, // waitmode
            KE_UNALERTABLE, // alertable
            NULLPTR, // timeout
            &mmnode^.ZeroPageEvent.Header, // object
        )

        // Zero at most 64 pages during each 100ms interval.
        // We will zero a maximum of 640 pages per second which is about 2.5MB.

        count : WORD = MMP_ZERO_MAXIMUM

        WHILE count > 0 DO
            IF mmnode^.FreeList.Count < MMP_ZERO_BATCH THEN
                BREAK
            END

            i := 0

            ipl := MmpAcquireListExclusive ( mmnode )

            IF mmnode^.AvailablePageCount < mmnode^.SufficientPageCount OR
                mmnode^.FreeList.Count < MMP_ZERO_BATCH THEN

                // Don't zero any pages if people are trying to make memory
                // right now. We'd only step on their toes by holding pages
                // hostage at idle priority.

                MmpReleaseList ( mmnode, ipl )

                BREAK
            END

            WHILE i < MMP_ZERO_BATCH DO
                zerobatch[i] = MmpAllocatePageFromList (
                    &mmnode^.FreeList, // list
                    0, // color
                )

                i += 1
            END

            // Decrement available pages to reflect the ones we removed from
            // the zero page list.

            avail := mmnode^.AvailablePageCount - MMP_ZERO_BATCH
            mmnode^.AvailablePageCount = avail

            MmpReleaseList ( mmnode, ipl )

            // Zero the pages.

            i = 0

            WHILE i < MMP_ZERO_BATCH DO
                MmZeroPage ( MmpPfeToPfn ( zerobatch[i] ) )

                i += 1
            END

            // Place on zeroed list.

            i = 0

            ipl = MmpAcquireListExclusive ( mmnode )

            WHILE i < MMP_ZERO_BATCH DO
                MmpInsertPageIntoList (
                    &mmnode^.ZeroList, // list
                    zerobatch[i], // pfe
                    TRUE, // head
                )

                i += 1
            END

            // Increment available pages to reflect the ones added to the zero
            // page list.

            avail = mmnode^.AvailablePageCount + MMP_ZERO_BATCH
            mmnode^.AvailablePageCount = avail

            MmpReleaseList ( mmnode, ipl )

            // Wake waiters for available pages.

            MmpNewAvailablePage ( mmnode, avail )

            count -= MMP_ZERO_BATCH
        END

        // Sleep for 100ms to avoid using too much memory bandwidth.

        KeSleep (
            &interval, // interval
            KE_KERNEL_MODE, // waitmode
            KE_UNALERTABLE, // alertable
        )
    END
END