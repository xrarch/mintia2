//
// Implements system address space allocation for the MINTIA Memory Manager.
//

#INCLUDE "Mmp.hjk"
#INCLUDE "../../Loader/Headers/Loader.hjk"

STRUCT MmpChunkEntry
    Next : ^MmpChunkEntry,
END

#DEFINE MMP_DEFERRED_FLUSH_PAGES 128

#SECTION "INITtext"
FN MmpInitializeChunkSpace (
    IN chunkspace : ^MmpChunkSpace,
    IN base : ^VOID,
    IN entryshift : UWORD,
    IN pages : UWORD,
)

    // Initialize a chunked region of system space.

    entrysize := 1 << entryshift
    entrycount := (pages << RTL_PAGE_SHIFT) / entrysize
    entrypages := entrysize >> RTL_PAGE_SHIFT

    chunkspace^.EntryShift = entryshift
    chunkspace^.EntryCount = entrycount
    chunkspace^.FreeListHead = NULLPTR

    chunkspace^.PendingFlushCount = 0

    KeInitializeLock ( &chunkspace^.Lock )

    // Link the chunks through the PTEs.

    pte := MmpPteAddress ( base )

    WHILE entrycount DO
        entry := CAST pte TO ^MmpChunkEntry

        entry^.Next = chunkspace^.FreeListHead
        chunkspace^.FreeListHead = entry

        entrycount -= 1
        pte += entrypages * SIZEOF MmpPte
    END
END

FN MmpFlushChunkSpace (
    IN chunkspace : ^MmpChunkSpace,
)

    // Flush TB entries across all processors for the delayed chunks.

    // Insert these entries into the free list.

    i := 0
    max := chunkspace^.PendingFlushCount

    WHILE i < max DO
        chunk := CAST MmpPteAddress ( chunkspace^.PendingFlush[i] )
            TO ^MmpChunkEntry

        chunk^.Next = chunkspace^.FreeListHead
        chunkspace^.FreeListHead = chunk

        i += 1
    END

    chunkspace^.PendingFlushCount = 0

    // Flush the TB.

    IF chunkspace^.EntryShift == RTL_PAGE_SHIFT AND
        max < 16 THEN

        // Chunks are pages, and there's fewer than 16.
        // Flush them individually.

        KeuFlushMultipleTb (
            &chunkspace^.PendingFlush[0], // vaddrtable
            max, // pagecount
        )

    ELSE
        // Chunks are multiple pages, or there's more than 16.
        // Flush everyone's entire TB. Don't keep global pages.

        KeuSweepTb ( FALSE )
    END
END

FN MmpAllocateChunkSpace (
    IN chunkspace : ^MmpChunkSpace,
) : ^VOID

    // Allocate a chunk from the given chunk space.
    // Returns the virtual address of the chunk. Nothing is mapped into the
    // returned virtual address - this is the caller's responsibility.
    // Returns NULLPTR if no chunks remaining.

    ipl := KeAcquireApcLockExclusive ( &chunkspace^.Lock )

    chunk := chunkspace^.FreeListHead

    IF NOT chunk THEN
        IF NOT chunkspace^.PendingFlushCount THEN
            KeReleaseApcLock ( &chunkspace^.Lock, ipl )

            RETURN NULLPTR
        END

        // There are chunks pending flush.
        // We can flush them and grab the first freed one.

        MmpFlushChunkSpace ( chunkspace )

        chunk = chunkspace^.FreeListHead
    END

    chunkspace^.FreeListHead = chunk^.Next

    KeReleaseApcLock ( &chunkspace^.Lock, ipl )

    RETURN MmpVirtualAddress ( chunk )
END

FN MmpFreeChunkSpace (
    IN chunkspace : ^MmpChunkSpace,
    IN ptr : ^VOID,
    IN flush : UWORD,
)

    // Return a chunk to the given chunk space.

    ipl := KeAcquireApcLockExclusive ( &chunkspace^.Lock )

    IF flush THEN
        // Insert on the delayed flush table.

        count := chunkspace^.PendingFlushCount

        IF count == MMP_MAXIMUM_CHUNKS_PENDING_FLUSH THEN
            // Full. Flush it.

            MmpFlushChunkSpace ( chunkspace )

            count = 0
        END

        chunkspace^.PendingFlush[count] = ptr
        chunkspace^.PendingFlushCount = count + 1

    ELSE
        // Insert directly into the free chunk list.

        chunk := CAST MmpPteAddress ( ptr ) TO ^MmpChunkEntry

        chunk^.Next = chunkspace^.FreeListHead
        chunkspace^.FreeListHead = chunk
    END

    KeReleaseApcLock ( &chunkspace^.Lock, ipl )
END

#ENTERSECTION "INITtext"

#SECTION "INITtext"
FN MmpInitializeSystemVa (
    IN kenode : ^KeuNode,
)

    // Initialize the system space allocation.

    // There are three regions of dynamically managed system space:
    //
    // MMP_POOL_SPACE:    Sized at boot time with enough page tables for a
    //                    quantity of virtual space equivalent to the total
    //                    physical memory in the system.
    //
    //                    The free list for these pages is threaded through the
    //                    page tables.
    //
    //                    Maximum size on 32-bit is capped at 256MB.
    //
    //
    // MMP_CACHE_SPACE:   Occupies 4x phys mem of virtual space. Page tables are
    //                    allocated at boot time. Used to map demand-paged views
    //                    of cached files. Allocated in fixed size chunks,
    //                    varying in size depending on the system size:
    //
    //                    Tiny:   32KB
    //                    *:      64KB
    //                    Large+: 256KB
    //
    //                    The free list for these chunks is threaded through the
    //                    page tables.
    //
    //                    Maximum size on 32-bit is capped at 256MB.
    //
    //
    // MMP_DYNAMIC_SPACE: Page tables are allocated at boot time. Allocations
    //                    can be any count of pages. Used to map kernel stacks,
    //                    MDLs, etc.
    //
    //                    An AVL tree is used to allocate this space. The AVL
    //                    nodes (containing parent, left child, right child, and
    //                    an optional MmuObject pointer) are allocated in bulk
    //                    from nonpaged pool on-demand and stored in a lookaside
    //                    list.
    //
    //                    Maximum size on 32-bit is capped at 256MB.

    mmnode := kenode^.Mmp

    size := mmnode^.SizeLevel

    KeAssert ( SIZEOF MmpChunkEntry <= SIZEOF MmpPte )

    // Initialize nonpaged space.

    base : ^VOID = kenode + KE_POOL_SPACE_OFFSET

    MmpInitializeChunkSpace (
        &mmnode^.PoolSpace, // chunkspace
        base, // base
        RTL_PAGE_SHIFT, // entryshift
        kenode^.PoolSpaceSize, // pages
    )

    // Initialize cache space.

    entryshift : UWORD

    IF size <= MM_TINY_SYSTEM THEN
        entryshift = 15 // 1 << 15 = 32KB

    ELSEIF size < MM_LARGE_SYSTEM THEN
        entryshift = 16 // 1 << 16 = 64KB

    ELSE
        entryshift = 18 // 1 << 18 = 256KB
    END

    mmnode^.CacheSpaceChunkPages = (1 << entryshift) >> RTL_PAGE_SHIFT

    base = kenode + KE_CACHE_SPACE_OFFSET

    MmpInitializeChunkSpace (
        &mmnode^.CacheSpace, // chunkspace
        base, // base
        entryshift, // entryshift
        kenode^.CacheSpaceSize, // pages
    )

    // Initialize dynamic space.

    RtlInitializeAvl ( &mmnode^.DynamicSpaceRoot )

    KeInitializeLock ( &mmnode^.DynamicSpaceLock )

    KeInitializeLock ( &mmnode^.DynamicSpaceTreeLock )

    base = kenode + KE_DYNAMIC_SPACE_OFFSET

    mmnode^.DynamicSpaceBase = base

    // Trim four pages off the end of dynamic space per CPU, to be used as
    // 'quick' mappings.

    quickpages := KeQueryMostProcessorsInAnyNode () * MMP_QUICK_PER_CPU

    dsize := kenode^.DynamicSpaceSize

    IF dsize < quickpages THEN
        KeCrash ( "Dynamic space too small\n" )
    END

    dsize -= quickpages

    RtlInitializeBitmap (
        &mmnode^.DynamicSpaceBitmap, // header
        dsize, // sizeinbits
        kenode^.DynamicSpaceBitmap, // data
    )

    // Remember the base of the quick pages.

    mmnode^.QuickPteBase =
        MmpPteAddress ( base + (dsize << RTL_PAGE_SHIFT) )
END

#LEAVESECTION

#SECTION "INITtext"
FN MmpInitializeSpaceNodeAllocation (
    IN kenode : ^KeuNode,
)

    // Initialize the cache for dynamic space nodes.
    // This is done later as part of pool initialization because that's where
    // they ultimately come from.

    // XXX The sanity of using a pool cache for this depends on the
    //     MmpDynamicSpaceNode being small enough to not trigger page-aligned
    //     pool allocation when slabs are created for it.

    mmnode := kenode^.Mmp

    mmnode^.SpaceNodeCache = MmCreatePoolCache (
        mmnode, // mmnode
        "Space Nodes", // name
        SIZEOF MmpDynamicSpaceNode, // size
        MM_NONPAGED_POOL, // poolindex
        'Dyna', // tag
    )

#ENTERSECTION "INITtext"
    IF NOT mmnode^.SpaceNodeCache THEN
        KeCrash ( "Failed to create space node cache\n" )
    END
#LEAVESECTION "INITtext"

END

FN MmpFlushDynamicSpace (
    IN mmnode : ^MmpNode,
)

    // Perform the deferred flushes for dynamic space. Assumes the dynamic space
    // lock is held.

    // First free the regions.

    i := 0
    max := mmnode^.DeferredFlushIndex
    flush := &mmnode^.DeferredFlushTable[0]
    base := mmnode^.DynamicSpaceBase

    WHILE i < max DO
        RtlClearBitsBitmap (
            &mmnode^.DynamicSpaceBitmap, // header
            (flush^.Ptr - base) >> RTL_PAGE_SHIFT, // index
            flush^.Pages, // runlength
        )

        flush += SIZEOF MmpDeferredFlush
        i += 1
    END

    mmnode^.DeferredFlushIndex = 0
    mmnode^.DeferredFlushPages = 0

    // Now do a global TB flush.

    KeuSweepTb ( FALSE )
END

FN MmpFindDynamicSpaceNode (
    IN ptr : ^VOID,
) : ^MmpDynamicSpaceNode

    // Find the dynamic space node in which the pointer resides.
    // Returns NULLPTR if no such node exists.

    mmnode := KeNumaNodeFromSpacePointer ( ptr ) ^.Mmp

    spacenode : ^MmpDynamicSpaceNode

    // We could hold this lock shared but that would violate our codebase
    // policy that a shared lock is never taken subordinate to an exclusive
    // lock. There are many imaginable situations where that could occur here.

    ipl := KeAcquireApcLockExclusive ( &mmnode^.DynamicSpaceTreeLock )

    avlnode := mmnode^.DynamicSpaceRoot.Right

    WHILE TRUE DO
        IF NOT avlnode THEN
            spacenode = NULLPTR

            BREAK
        END

        spacenode = CONTAINEROF avlnode TO MmpDynamicSpaceNode.Entry

        IF ptr < spacenode^.StartVa THEN
            avlnode = avlnode^.Left

        ELSEIF ptr >= spacenode^.EndVa THEN
            avlnode = avlnode^.Right

        ELSE
            BREAK
        END
    END

    KeReleaseApcLock ( &mmnode^.DynamicSpaceTreeLock, ipl )

    RETURN spacenode
END

#MACRO MmpAllocateDynamicSpaceNode ( mmnode ) [
    CAST MmAllocateFromPoolCache (
        (mmnode)^.SpaceNodeCache, // cache
        FALSE, // wait
    ) TO ^MmpDynamicSpaceNode
]

#MACRO MmpFreeDynamicSpaceNode ( mmnode, spacenode ) [
    MmFreeToPoolCache (
        (mmnode)^.SpaceNodeCache, // cache
        spacenode, // ptr
    )
]

FN (RtlAvlLessThanF) MmpCompareDynamicSpaceNodes (
    IN a : ^RtlAvlNode,
    IN b : ^RtlAvlNode,
) : UWORD

    // Return whether A < B.

    node1 := CONTAINEROF a TO MmpDynamicSpaceNode.Entry
    node2 := CONTAINEROF b TO MmpDynamicSpaceNode.Entry

    RETURN node1^.StartVa < node2^.StartVa
END

FN MmpAllocateDynamicPages (
    IN mmnode : ^MmpNode,
    IN pages : UWORD,
) : ^VOID

    // Allocate a page aligned region of dynamic space.
    // Returns NULLPTR if none available.

@Retry

    // Find a clear run of bits locklessly.
    // Note that the bitmap hint is just a hint, so there's no need to do any
    // synchronization for it, since "corruption" is harmless.

    index := RtlFindRunBitmap (
        &mmnode^.DynamicSpaceBitmap, // header
        pages, // runlength
        mmnode^.DynamicSpaceHint, // hint
    )

    ipl := KeAcquireApcLockExclusive ( &mmnode^.DynamicSpaceLock )

    IF index == -1 THEN
        // Dynamic space has completely filled up.

        IF mmnode^.DeferredFlushIndex THEN
            // Try freeing the deferred regions and trying again.

            MmpFlushDynamicSpace ( mmnode )
        END

        // Search again with the lock held, to be absolutely sure it's full.

        index = RtlFindRunBitmap (
            &mmnode^.DynamicSpaceBitmap, // header
            pages, // runlength
            mmnode^.DynamicSpaceHint, // hint
        )

        IF index == -1 THEN
            // Yep, full.

            KeReleaseApcLock ( &mmnode^.DynamicSpaceLock, ipl )

            RETURN NULLPTR
        END
    
    ELSE
        // Check that the range is still free with the lock held.

        IF NOT RtlCheckClearBitsBitmap (
            &mmnode^.DynamicSpaceBitmap, // header
            index, // index
            pages, // runlength
        ) THEN
            // Someone else nabbed it, so retry.

            KeReleaseApcLock ( &mmnode^.DynamicSpaceLock, ipl )

            GOTO Retry
        END
    END

    // Set the hint.

    mmnode^.DynamicSpaceHint = index

    // Mark bits set.

    RtlSetBitsBitmap (
        &mmnode^.DynamicSpaceBitmap, // header
        index, // index
        pages, // runlength
    )

    KeReleaseApcLock ( &mmnode^.DynamicSpaceLock, ipl )

    RETURN CAST mmnode^.DynamicSpaceBase + (index << RTL_PAGE_SHIFT) TO ^VOID
END

FN MmpReleaseDynamicPages (
    IN mmnode : ^MmpNode,
    IN ptr : ^VOID,
    IN pages : UWORD,
    IN flush : UWORD,
)

    // Release a portion of dynamic space by pointer and page count.

    ipl := KeAcquireApcLockExclusive ( &mmnode^.DynamicSpaceLock )

    IF flush THEN
        IF mmnode^.DeferredFlushIndex == MMP_DEFERRED_FLUSH_PAIRS THEN
            // This exceeds the maximum number of regions pending flush.

            MmpFlushDynamicSpace ( mmnode )
        END

        // Add this region to the list of pairs pending flush.

        entry := &mmnode^.DeferredFlushTable[mmnode^.DeferredFlushIndex]

        entry^.Ptr = ptr
        entry^.Pages = pages

        mmnode^.DeferredFlushIndex += 1
        mmnode^.DeferredFlushPages += pages

        IF mmnode^.DeferredFlushPages >= MMP_DEFERRED_FLUSH_PAGES THEN
            // Too many pages pending flush.

            MmpFlushDynamicSpace ( mmnode )
        END

    ELSE
        RtlClearBitsBitmap (
            &mmnode^.DynamicSpaceBitmap, // header
            (ptr - mmnode^.DynamicSpaceBase) >> RTL_PAGE_SHIFT, // index
            pages, // runlength
        )
    END

    KeReleaseApcLock ( &mmnode^.DynamicSpaceLock, ipl )
END

FN MmpAllocateDynamicSpace (
    IN mmnode : ^MmpNode,
    IN pages : UWORD,
    IN wait : UWORD,
) : ^MmpDynamicSpaceNode

    // Allocate a region of dynamic space and create an AVL node for lookup.

    ptr := MmpAllocateDynamicPages (
        mmnode, // mmnode
        pages, // pages
    )

    IF NOT ptr THEN
        RETURN NULLPTR
    END

@Retry

    spacenode := MmpAllocateDynamicSpaceNode ( mmnode )

    IF NOT spacenode THEN
        IF NOT wait THEN
            // Failed to allocate a node instantly and caller told us not to
            // wait.

            MmpReleaseDynamicPages (
                mmnode, // mmnode
                ptr, // ptr
                pages, // pages
                FALSE, // flush
            )

            RETURN NULLPTR
        END

        // Caller requested wait upon being unable to allocate a node.

        MmpWaitForPages (
            mmnode, // mmnode
            FALSE, // low
        )

        GOTO Retry
    END

    ipl := KeAcquireApcLockExclusive ( &mmnode^.DynamicSpaceTreeLock )

    // Insert the node in the AVL tree.

    spacenode^.StartVa = ptr
    spacenode^.EndVa = ptr + (pages << RTL_PAGE_SHIFT)

    RtlInsertAvl (
        &mmnode^.DynamicSpaceRoot, // root
        &spacenode^.Entry, // node
        &MmpCompareDynamicSpaceNodes, // comparisonfunc
    )

    // Unlock the tree and return the node.

    KeReleaseApcLock ( &mmnode^.DynamicSpaceTreeLock, ipl )

    RETURN spacenode
END

FN MmpReleaseDynamicSpace (
    IN mmnode : ^MmpNode,
    IN spacenode : ^MmpDynamicSpaceNode,
    IN flush : UWORD,
)

    // Release a region of dynamic space by node.

    ptr := spacenode^.StartVa
    pages := (spacenode^.EndVa - ptr) >> RTL_PAGE_SHIFT

    ipl := KeAcquireApcLockExclusive ( &mmnode^.DynamicSpaceTreeLock )

    // Remove the AVL node from the tree.

    RtlRemoveAvl (
        &mmnode^.DynamicSpaceRoot, // root
        &spacenode^.Entry, // node
    )

    KeReleaseApcLock ( &mmnode^.DynamicSpaceTreeLock, ipl )

    // Free the node.

    MmpFreeDynamicSpaceNode ( mmnode, spacenode )

    // Free the space.

    MmpReleaseDynamicPages (
        mmnode, // mmnode
        ptr, // ptr
        pages, // pages
        flush, // flush
    )
END