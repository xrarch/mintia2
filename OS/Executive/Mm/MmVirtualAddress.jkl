//
// Miscellaneous routines for dealing with virtual addresses.
//

#INCLUDE "Mi.hjk"

EXPORT FN MmIsVirtualValid (
    IN vaddr : ^VOID,
) : UWORD

#IF ( NOT MI_COMPLETE_TABLE )
    // Some architectures have valid addresses not described by the page table.

    IF MiIsArchitecturallyValid ( vaddr ) THEN
        RETURN TRUE
    END
#END

#IF ( == BLD_BITS 64 )
    // 64-bit architectures have "non-canonical" addresses.

    IF NOT ( MI_IS_CANONICAL ( vaddr ) ) THEN
        RETURN FALSE
    END
#END

    l1 := MiPteAddress ( vaddr )
    l2 := MiPteAddress ( l1 )

#IF ( >= MI_TABLE_LEVELS 3 )
    l3 := MiPteAddress ( l2 )
#END

#IF ( >= MI_TABLE_LEVELS 4 )
    l4 := MiPteAddress ( l3 )

    IF NOT ( MiIsPteValid ( l4^ ) ) THEN
        RETURN FALSE
    END
#END

#IF ( >= MI_TABLE_LEVELS 3 )
    IF NOT ( MiIsPteValid ( l3^ ) ) THEN
        RETURN FALSE
    END
#END

    IF NOT ( MiIsPteValid ( l2^ ) ) THEN
        RETURN FALSE
    END

    RETURN MiIsPteValid ( l1^ )
END