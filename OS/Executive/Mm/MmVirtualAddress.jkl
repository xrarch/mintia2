//
// Miscellaneous routines for dealing with virtual addresses.
//

#INCLUDE "Mi.hjk"

EXPORT FN MmIsVirtualValid (
    IN vaddr : ^VOID,
) : UWORD

#IF ( NOT MI_COMPLETE_TABLE )
    // Some architectures have valid addresses not described by the page table.

    IF MI_IS_ARCHITECTURALLY_VALID ( vaddr ) THEN
        RETURN TRUE
    END
#END

#IF ( == BLD_BITS 64 )
    // 64-bit architectures have "non-canonical" addresses.

    IF NOT ( MI_IS_CANONICAL ( vaddr ) ) THEN
        RETURN FALSE
    END
#END

    l1 := CAST MI_PTE_ADDRESS ( vaddr ) TO ^MiPte
    l2 := CAST MI_PTE_ADDRESS ( l1 ) TO ^MiPte

#IF ( >= MI_TABLE_LEVELS 3 )
    l3 := CAST MI_PTE_ADDRESS ( l2 ) TO ^MiPte
#END

#IF ( >= MI_TABLE_LEVELS 4 )
    l4 := CAST MI_PTE_ADDRESS ( l3 ) TO ^MiPte

    IF NOT ( MI_IS_PTE_VALID ( l4^ ) ) THEN
        RETURN FALSE
    END
#END

#IF ( >= MI_TABLE_LEVELS 3 )
    IF NOT ( MI_IS_PTE_VALID ( l3^ ) ) THEN
        RETURN FALSE
    END
#END

    IF NOT ( MI_IS_PTE_VALID ( l2^ ) ) THEN
        RETURN FALSE
    END

    RETURN MI_IS_PTE_VALID ( l1^ )
END