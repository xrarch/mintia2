//
// Miscellaneous routines for dealing with virtual addresses.
//

#INCLUDE "Mmp.hjk"

EXPORT FN MmuIsVirtualValid (
    IN vaddr : ^VOID,
) : UWORD

#IF ( NOT MMP_COMPLETE_TABLE )
    // Some architectures have valid addresses not described by the page table.

    IF MmpIsArchitecturallyValid ( vaddr ) THEN
        RETURN TRUE
    END
#END

#IF ( == BLD_BITS 64 )
    // 64-bit architectures have "non-canonical" addresses.

    IF NOT MMP_IS_CANONICAL ( vaddr ) THEN
        RETURN FALSE
    END
#END

    l1 := MmpPteAddress ( vaddr )
    l2 := MmpPteAddress ( l1 )

#IF ( >= MMP_TABLE_LEVELS 3 )
    l3 := MmpPteAddress ( l2 )
#END

#IF ( >= MMP_TABLE_LEVELS 4 )
    l4 := MmpPteAddress ( l3 )

    IF NOT ( MmpIsPteValid ( l4^ ) ) THEN
        RETURN FALSE
    END
#END

#IF ( >= MMP_TABLE_LEVELS 3 )
    IF NOT ( MmpIsPteValid ( l3^ ) ) THEN
        RETURN FALSE
    END
#END

    IF NOT ( MmpIsPteValid ( l2^ ) ) THEN
        RETURN FALSE
    END

    RETURN MmpIsPteValid ( l1^ )
END